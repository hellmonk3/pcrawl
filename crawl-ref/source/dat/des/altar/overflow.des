###############################################################################
# overflow.des: This is the place where vaults containing overflow altars go.
#
# Altar vaults do not just provide flavour. They are crucial for ensuring
# that granted gods without altars in the Temple will be present in the
# early part of the dungeon. Such altars are called "overflow temples".
# Sometimes, ordinary altar maps featuring a single C glyph (random altar)
# are used. However, there are also vaults dedicated to a special god.
# These look much more interesting, of course.
#
# There are two TAGs to be used for single-altar maps with a specified god:
#
# TAG: temple_overflow_1 temple_overflow_FOO
#      Says the map will only ever be used for overflow temples to god FOO.
#      (The 1 indicates there's only one god placed by this vault.)
# TAG: uniq_altar_FOO
#      If you also add this, then the map can be placed randomly. Random
#      placement will only occur after the deepest possible overflow depth.
#
# And there is a TAG if you want a single-altar map to a random god:
#
# TAG: temple_overflow_generic_1
#      Allows any of the overflow temple altars to be used.
#      For this, use one B glyph in your map.
#
# The level builder treats these tags specially, and vaults with
# a temple_overflow_FOO tag but without uniq_altar_FOO will not place as mini
# vaults. Generic overflow temples should not receive uniq_altar_ tags.
#
# Technical requirements for tags and depth directives for uniq_altar_*
# mini vaults
# - Add an interest_check(_G) if the vault is purely decorative to
#   automatically apply an extra tag as needed.
#
# If you want to check overflow temple placement in wizard mode, use the &:
# command. The levels overflow temples are placed on, and which gods are
# in them, are chosen at new-game time.
#
###############################################################################

{{
function interest_check(e)
  if not you.in_branch("D") or you.depth() > 10 then
    e.tags('extra')
  end
end

function get_overflow_altar_count(e)
  local params = dgn.map_parameters()
  if params ~= nil then
    local tag = unpack(params)
    if tag ~= nil then
      -- temple_overflow_generic_ is 24 characters
      local result = string.sub(tag, 25)
      if result ~= "" then return result end
    end
  end

  return nil
end
}}

default-depth: D:3-, Depths

# These must feature one of the temple_overflow_* TAGs (see header).
# Altars are sorted alphabetically by god, with the Shining One coming
# after Zin. At the end, there are altar vaults which can be used for
# more than just one god.

### Ashenzari overflow altars #################################################

NAME:   ashenzari_altar_1
TAGS:   uniq_altar_ashenzari temple_overflow_1 temple_overflow_ashenzari
TAGS:   transparent decor
KFEAT:  _ = altar_ashenzari
: interest_check(_G)
MAP
b@b
@_@
b@b
ENDMAP

# A ruined temple
NAME:   evilmike_ruin_ashenzari
TAGS:   uniq_altar_ashenzari temple_overflow_1 temple_overflow_ashenzari
TAGS:   transparent decor
SUBST:  x = xxxxx-, - = ----.
TILE:   x = WALL_BRICK_VINES
FTILE:  -V_ = FLOOR_SANDSTONE
KFEAT:  _ = altar_ashenzari
SUBST:  - = .
: interest_check(_G)
MAP
      .......
      .xx+xx.
     ..x---x..
   ....x---x....
   .xxxx---xxxx.
  ..xV-xx+xx-Vx..
....x---------x....
.xxxxx-------xxxxx.
.x---x-------x---x.
.x---+---_---+---x.
.x---x-------x---x.
.xxxxx-------xxxxx.
....x---------x....
  ..xV-xx+xx-Vx..
   .xxxx---xxxx.
   ....x---x....
     ..x---x..
      .xxxxx.
      .......
ENDMAP

NAME:  cheibrodos_ashenzari_chains
TAGS:  temple_overflow_1
TAGS:  temple_overflow_ashenzari uniq_altar_ashenzari transparent decor
TAGS:  uniq_altar_ashenzari
KFEAT: _ = altar_ashenzari
KFEAT: m = iron_grate
: interest_check(_G)
MAP
.............
.mmm.....mmm.
.mcmmc_cmmcm.
.mmm.....mmm.
.............
ENDMAP

NAME: nicolae_ashenzari_panopticon
TAGS: uniq_altar_ashenzari temple_overflow_1 temple_overflow_ashenzari \
      transparent decor
SHUFFLE: ABCDEF/abcdef
SUBST: ACEbdf = x, Ba = m, Dc = ., Fe = @
NSUBST: m = + / m
KFEAT: _ = altar_ashenzari
: interest_check(_G)
MAP
E....F..E..F...CE
CC...D..C..D..CC.
.CC..DD.C.DD.CC..
..CC..D.C.D.CC...
...CC.DDCDDCC....
FDD.CC.DCDCC..DDF
..DDDCABABA.DDD..
....DDB...BDD....
ECCCCCA._.ACCCCCE
....DDB...BDD....
..DDD.ABABACDDD..
FDD..CCDCD.CC.DDF
....CCDDCDD.CC...
...CC.D.C.D..CC..
..CC.DD.C.DD..CC.
.CC..D..C..D...CC
EC...F..E..F....E
ENDMAP

### Cheibriados overflow altars ###############################################

NAME:  cheibriados_altar_1
TAGS:  uniq_altar_cheibriados temple_overflow_1 temple_overflow_cheibriados
TAGS:  transparent decor
KFEAT: _ = altar_cheibriados
: interest_check(_G)
MAP
...
._.
...
ENDMAP

NAME:    slow_altar_2
TAGS:    no_item_gen transparent
TAGS:    uniq_altar_cheibriados temple_overflow_1 temple_overflow_cheibriados
KFEAT:   _ = altar_cheibriados
SUBST:   x : xxxcccvb
: interest_check(_G)
MAP
x@@G@@x
x.....x
x.....x
xx+++xx
x.....x
x.....x
x.....x
x.....x
x.._..x
xxxxxxx
ENDMAP

### Dithmenos overflow altars ################################################

NAME:   grunt_dithmenos_smoke
TAGS:   uniq_altar_dithmenos temple_overflow_1 temple_overflow_dithmenos
TAGS:   transparent decor
KFEAT:  C = altar_dithmenos
MARKER: C = lua:fog_machine { cloud_type = "black smoke", \
                              pow_min = 10, pow_max = 20, \
                              delay = 100, size = 1, walk_dist = 1 }
: interest_check(_G)
MAP
...
.C.
...
ENDMAP

# Reflect upon the darkness, mortal.
NAME:   grunt_dithmenos_smoke_and_mirrors
TAGS:   uniq_altar_dithmenos temple_overflow_1 temple_overflow_dithmenos
TAGS:   transparent decor
KFEAT:  C = altar_dithmenos
KFEAT:  O = .
MARKER: O = lua:fog_machine { cloud_type = "black smoke", \
                              pow_min = 25, pow_max = 25, \
                              delay = 30, size = 1, walk_dist = 0 }
: interest_check(_G)
MAP
  b@b
 b.O.b
b.b.b.b
@O.C.O@
b.b.b.b
 b.O.b
  b@b
ENDMAP

NAME: nicolae_dithmenos_the_darkroom
TAGS: temple_overflow_dithmenos temple_overflow_1 uniq_altar_dithmenos
TAGS: decor transparent
KFEAT: _ = altar_dithmenos
MARKER: ' = lua:fog_machine { cloud_type = "black smoke", pow_min = 30, \
   pow_max = 50, delay_min = 50, delay_max = 90, size = 1, walk_dist = 1, \
   spread_rate = 10 }
: interest_check(_G)
MAP
xxxx@@@xxxx
xxx.....xxx
xx'.....'xx
x.........x
@.........@
@...._....@
@.........@
x.........x
xx'.....'xx
xxx.....xxx
xxxx@@@xxxx
ENDMAP

NAME: nicolae_dithmenos_shadow_blocks
TAGS: temple_overflow_dithmenos temple_overflow_1 uniq_altar_dithmenos
TAGS: decor transparent
COLOUR: s' = magenta, c = white
TILE: c = dngn_stone_wall_white
TILE: s = dngn_stone_dark
FTILE: 's_ = floor_pebble_darkgray
KFEAT: cs = stone_wall
KFEAT: _ = altar_dithmenos
: interest_check(_G)
MAP
      .....
      ..c..
  .....ccc.....
  .cc..scs..cc.
  .ccs''s''scc.
  ..ss'''''ss..
....'''''''''....
..cs'''''''''sc..
.cccs'''_'''sccc.
..cs'''''''''sc..
....'''''''''....
  ..ss'''''ss..
  .ccs''s''scc.
  .cc..scs..cc.
  .....ccc.....
      ..c..
      .....
ENDMAP

NAME: nicolae_dithmenos_through_a_glass_darkly
TAGS: temple_overflow_dithmenos uniq_altar_dithmenos temple_overflow_1
TAGS: decor transparent
COLOUR: x = magenta, m = lightmagenta
TILE: m = dngn_transparent_wall_darkgray
TILE: x = wall_brick_darkgray
KFEAT: _ = altar_dithmenos
: interest_check(_G)
MAP
  ..xxx
 .....xx
...mm..xx
..mm....x
@.m._...x
..mm....x
...mm..xx
 .....xx
  ..xxx
ENDMAP

### Elyvilon overflow altars ##################################################

NAME:   elyvilon_altar_1
TAGS:   uniq_altar_elyvilon temple_overflow_1 temple_overflow_elyvilon
TAGS:   transparent decor
KFEAT:  C = altar_elyvilon
SUBST:  X : T G t 1 2 3
MONS:   plant, bush, fungus
KMASK:  T = no_monster_gen
: interest_check(_G)
MAP
      xx@xx
      x...x
      x.X.x
      x...x
      xx.xx
       x.x
xxxxx xx.xx xxxxx
x...xxx...xxx...x
@.X.....C.....X.@
x...xxx...xxx...x
xxxxx xx.xx xxxxx
       x.x
      xx.xx
      x...x
      x.X.x
      x...x
      xx@xx
ENDMAP

NAME:   elyvilon_altar_5
TAGS:   uniq_altar_elyvilon temple_overflow_1 temple_overflow_elyvilon decor transparent
KFEAT:  C = altar_elyvilon
KMASK:  T = no_monster_gen
: interest_check(_G)
MAP
.............
.c.c.c.c.c.c.
.............
...T.....T...
@.....C.....@
...T.....T...
.............
.c.c.c.c.c.c.
.............
ENDMAP

### Fedhas overflow altars ####################################################

NAME:   fedhas_altar_1
TAGS:   uniq_altar_fedhas temple_overflow_1 temple_overflow_fedhas
TAGS:   transparent decor
KFEAT:  C = altar_fedhas
MONS:   plant
: interest_check(_G)
MAP
.....
.1.1.
..C..
.1.1.
.....
ENDMAP

NAME:   fedhas_altar_3
TAGS:   uniq_altar_fedhas temple_overflow_1 temple_overflow_fedhas
TAGS:   transparent decor
KFEAT:  C = altar_fedhas
NSUBST: b = 1:. / *:1
MONS:   plant w:5 / fungus / nothing w:3
: interest_check(_G)
MAP
@111@
1b1b1
11C11
1b1b1
@111@
ENDMAP

NAME:   fedhas_altar_4
TAGS:   uniq_altar_fedhas temple_overflow_1 temple_overflow_fedhas transparent
TAGS:   decor
KFEAT:  C = altar_fedhas
FTILE:  . = floor_moss
FTILE:  t = floor_moss
: interest_check(_G)
MAP
xxxxxxxxxxxxxxx
xxxtttttttxxxxx
xxtttttttttxxxx
xtttt.ttttttxxx
xtttt.C.ttttttt
xxtttt...ttttt.
xxtttttt......@
xxxxttttttttt..
xxxxxxttttttttx
xxxxxxxtttttttx
xxxxxxxxxxxxxxx
ENDMAP

NAME:   fedhas_altar_fruit_tree
TAGS:   uniq_altar_fedhas temple_overflow_1 temple_overflow_fedhas transparent
TAGS:   no_item_gen no_rotate no_vmirror decor
NSUBST: g = 4:f/*:.
NSUBST: f = 4:A/*:.
NSUBST: w = 2:4 / *:w
KFEAT:  C = altar_fedhas
KFEAT:  A = .
FTILE:  .tfCA = floor_moss
FTILE:  d = floor_hall
COLOUR: fd = brown
COLOUR: AC. = green
: interest_check(_G)
MAP
xxxxxxxxxxxxxxxxx
xtttttttttttttttx
xtttttttttttttttx
xttttttgfgttttttx
xtttttgf.fgtttttx
xttttgf.f.fgttttx
xtttgf..C..fgtttx
xtttgf.f.f.fgtttx
xttgfffffffffgttx
xttgt.......tgttx
xttttttdddttttttx
xttttttdddttttttx
xtttttdd@ddtttttx
ENDMAP

### Gozag overflow altars #####################################################

NAME:   grunt_gozag_statues
TAGS:   transparent no_item_gen no_monster_gen no_trap_gen temple_overflow_1
TAGS:   temple_overflow_gozag uniq_altar_gozag decor
KFEAT:  _ = altar_gozag
TILE:   G = dngn_golden_statue
FTILE:  _-G = floor_limestone
RTILE:  x = wall_sandstone
COLOUR: xG = yellow
: set_feature_name("granite_statue", "golden statue")
: interest_check(_G)
MAP
     @
  xxx-xxx
 xx-----xx
 x-G---G-x
 x-------x
@----_----@
 x-------x
 x-G---G-x
 xx-----xx
  xxx-xxx
     @
ENDMAP

NAME:  nicolae_gozag_cash_rules_everything_around_me
TAGS:  temple_overflow_gozag temple_overflow_1 uniq_altar_gozag \
       no_hmirror no_vmirror no_rotate no_item_gen transparent decor
KFEAT: _ = altar_gozag
: interest_check(_G)
MAP
bbb@bbb
b....$b
b.b.bbb
b.._..b
bbb.b.b
b$....b
bbb@bbb
ENDMAP

### Hepliaklqana overflow altars #############################################

NAME:   nzn_hepliaklqana_mist_gate
TAGS:   temple_overflow_1 temple_overflow_hepliaklqana no_monster_gen
TAGS:   transparent decor uniq_altar_hepliaklqana
KFEAT:  _ = altar_hepliaklqana
MARKER: A = lua:fog_machine { cloud_type = "grey smoke", pow_min = 20, \
                              pow_max = 40, delay = 10, size = 1, \
                              spread_rate = -1 }
: interest_check(_G)
MAP
xxxxx
x...x
x._.x
x...x
xx.xx
 xAx
 x.x
  @
ENDMAP

# A long-forgotten ecumenical temple. Only Hepliaklqana clearly remains,
# although a few forgotten altars are still around.
NAME:   nicolae_hepliaklqana_ecumenical_no_more
TAGS:   temple_overflow_hepliaklqana temple_overflow_1 transparent decor
TAGS:   uniq_altar_hepliaklqana
WEIGHT: 3
KMONS:  WD = plant / nothing
NSUBST: E = - / E, H = _ / - / E, E = E / . / E., P = 4=W / WDD / D
SUBST:  ' = '''., c = ccx
KFEAT:  WS = W
KFEAT:  _ = altar_hepliaklqana
KFEAT:  - = altar_ecumenical
FTILE:  WVD = floor_mud
COLOUR: D = darkgrey
FTILE:  ' = floor_w_marble
COLOUR: ' = white
FTILE:  _-E = floor_limestone
COLOUR: E = yellow
: interest_check(_G)
MAP
xxxxx.@.xxxxx
xxccxx+xxccxx
xccc''.''cccx
xcc''E'E''ccx
xc'''''''''cx
xc'E'PPP'E'cx
xc'''PVP'''cx
xc'H'PPP'H'cx
xc'''''''''cx
xcc''H'H''ccx
xccc'''''cccx
xxcccccccccxx
xxxxxxxxxxxxx
ENDMAP

NAME:   nicolae_hepliaklqana_hazy_memory
TAGS:   temple_overflow_hepliaklqana temple_overflow_1 transparent decor
TAGS:   uniq_altar_hepliaklqana
MONS:   plant
SUBST:  ' = ..', . = ...1
CLEAR:  '
KFEAT:  _ = altar_hepliaklqana
MARKER: _ = lua:fog_machine { cloud_type = "thin mist", size = 1, pow_min = 5, \
  pow_max = 7, delay = 10, start_clouds = 1, walk_dist = 5, spread_rate = 10 }
: interest_check(_G)
MAP
    '''
 ''''.''''
 '.'.'.'.'
 ''.'''.''
''.'...'.''
'.''._.''.'
''.'...'.''
 ''.'''.''
 '.'.'.'.'
 ''''.''''
    '''
ENDMAP

# Or should that be "heppocampus"...?
NAME:  nicolae_hepliaklqana_hippocampus
TAGS:  temple_overflow_hepliaklqana temple_overflow_1 transparent decor
TAGS:  uniq_altar_hepliaklqana
KFEAT: _ = altar_hepliaklqana
SUBST: D = cxx, E = cx
: interest_check(_G)
MAP
    xxxxxxxx
   @....xxxx
    xxx..xxx
xxxxxxxx.xxx
xcccccxx..xx
xc...cxx..xx
xc._.cxD..xx
xc...cxD..Dx
xc..ccxD..Dx
xc...EE...Dx
xc........Dx
xcE......DDx
xxEEEEEDDDxx
xxxxxxxxxxxx
ENDMAP

### Kikubaaqudgha overflow altars #############################################

NAME:   kiku_gazebo_garden_becter
TAGS:   temple_overflow_1 temple_overflow_kikubaaqudgha transparent decor
TAGS:   uniq_altar_kikubaaqudgha
KMONS:  f = withered plant
KFEAT:  _ = altar_kikubaaqudgha
SUBST:  ` = f....
COLOUR: c = magenta
COLOUR: i' = brown
COLOUR: " = yellow
TILE:   c = dngn_stone_wall_magenta
FTILE:  '"_ = floor_pebble_yellow
: interest_check(_G)
MAP
..````
.ccc`````
.c'c"`````
.c''""````
.c'''"c````
.c'_''cc```
.c'''"c````
.c''""````
.c'c"`````
.ccc`````
..````
ENDMAP

NAME:   nicolae_kikubaaqudgha_miasma_corner
TAGS:   temple_overflow_kikubaaqudgha temple_overflow_1 transparent decor
TAGS:   uniq_altar_kikubaaqudgha
KFEAT:  _ = altar_kikubaaqudgha
MARKER: M = lua:fog_machine { cloud_type = "foul pestilence", pow_min = 10, \
    pow_max = 20, delay_min = 1, delay_max = 1, size = 1, walk_dist = 0, \
    spread_rate = 0 }
COLOUR: c = darkgrey
: interest_check(_G)
MAP
    .
 ccc.ccc
 cM...Mc
 c.....c
...._....
 c.....c
 cM...Mc
 ccc.ccc
    .
ENDMAP

### Makhleb overflow altars ###################################################

NAME:  bloody_makhleb
TAGS:  uniq_altar_makhleb temple_overflow_1 temple_overflow_makhleb
TAGS:  transparent decor
DEPTH: D, Armory
KPROP: . = bloody / nothing
KFEAT: _ = altar_makhleb
: interest_check(_G)
MAP
...
._.
...
ENDMAP

### Nemelex overflow altars ###################################################

NAME:  nemelex_xobeh_ov_fountains_minmay
TAGS:  temple_overflow_1 temple_overflow_nemelex_xobeh uniq_altar_nemelex_xobeh
TAGS:  transparent decor
KFEAT: _ = altar_nemelex_xobeh
: interest_check(_G)
MAP
U@U
@_@
U@U
ENDMAP

NAME:    nemelex_dance_club_becter
TAGS:    temple_overflow_1 temple_overflow_nemelex_xobeh transparent
TAGS:    no_rotate no_vmirror no_monster_gen no_item_gen
TAGS:    uniq_altar_nemelex_xobeh
DEPTH:   D:3-
KFEAT:   _ = altar_nemelex_xobeh
SHUFFLE:  -_
COLOUR:  x = darkgray
COLOUR:  -' = random
TILE:    x = wall_pebble_darkgray
MAP
    .....
   ..xxx..
   .xx'xx.
 ...x'-'x...
..xxxx'xxxx..
.xx''x+x''xx.
.x'-'+.+'_'x.
.xx'xx.xx'xx.
..xxx.x.xxx..
 .....x.....
    .xxx.
    .....
ENDMAP

NAME:   nemelex_diamond_rough_becter
TAGS:   temple_overflow_1 temple_overflow_nemelex_xobeh transparent decor
TAGS:   uniq_altar_nemelex_xobeh
KFEAT:  _ = altar_nemelex_xobeh
COLOUR: x = lightred
COLOUR: 'b = silver
TILE:   x = wall_pebble_red
TILE:   b = dngn_crystal_white
: interest_check(_G)
: set_feature_name("crystal_wall", "wall of white crystal")
MAP
    ...
   ..x..
  ..xxx..
 ..xxbxx..
..xxb'bxx..
.xxb'_'bxx.
..xxb'bxx..
 ..x+b+x..
  ..xxx..
   ..x..
    ...
ENDMAP

NAME:   nemelex_lonely_heart_becter
TAGS:   uniq_altar_nemelex_xobeh transparent decor
TAGS:   temple_overflow_1 temple_overflow_nemelex
TAGS:   no_rotate no_vmirror no_monster_gen no_item_gen
KFEAT:  _ = altar_nemelex_xobeh
COLOUR: x = lightred
TILE:   x = wall_pebble_red
: interest_check(_G)
MAP
  .... ....
 ..xx...xx..
..xxxx.xxxx..
.xx..x+x..xx.
.xx.......xx.
..xx.._..xx..
 ..xx...xx..
  ..xx.xx..
   ..xxx..
    .....
ENDMAP

NAME:   nemelex_spade_bored_becter
TAGS:   temple_overflow_1 temple_overflow_nemelex_xobeh transparent decor
TAGS:   no_rotate no_vmirror no_monster_gen no_item_gen
TAGS:   uniq_altar_nemelex_xobeh
KFEAT:  _ = altar_nemelex_xobeh
NSUBST: ' = 6:a / *:.
COLOUR: x = darkgray
TILE:   x = wall_pebble_darkgray
: interest_check(_G)
MAP
     .....
    ..xxx..
   ..xx'xx..
  ..xx'''xx..
 ..xx''_''xx..
..xx'''''''xx..
.xx'''''''''xx.
.x'''xxxxx'''x.
.xx'+x.x.x+'xx.
..xxx..x..xxx..
 .....xxx.....
     .....
ENDMAP

### Okawaru overflow altars ###################################################

NAME:    okawaru_metal_pillars_1_2
TAGS:    temple_overflow_1 temple_overflow_okawaru uniq_altar_okawaru
TAGS:    transparent decor
DEPTH:   D, Armory
WEIGHT:  7
KFEAT:   _ = altar_okawaru
SHUFFLE: v'
SUBST:   ' = .
: interest_check(_G)
MAP
.....
.v'v.
.'_'.
.v'v.
.....
ENDMAP

NAME:   okawaru_metal_pillars_3
TAGS:   temple_overflow_1 temple_overflow_okawaru uniq_altar_okawaru
TAGS:   transparent decor
DEPTH:  D, Armory
WEIGHT: 3
KFEAT:  _ = altar_okawaru
: interest_check(_G)
MAP
.......
...v...
..v.v..
.v._.v.
..v.v..
...v...
.......
ENDMAP

NAME:   okawaru_trees_1
TAGS:   temple_overflow_1 temple_overflow_okawaru uniq_altar_okawaru
TAGS:   transparent decor
WEIGHT: 5
KFEAT:  _ = altar_okawaru
: interest_check(_G)
MAP
.........
.v.t.t.v.
.........
.t.t.t.t.
...._....
.t.t.t.t.
.........
.v.t.t.v.
.........
ENDMAP

NAME:   okawaru_trees_2
TAGS:   temple_overflow_1 temple_overflow_okawaru uniq_altar_okawaru
TAGS:   transparent decor
WEIGHT: 5
KFEAT:  _ = altar_okawaru
: interest_check(_G)
MAP
...........
.v...t...v.
....t.t....
...t.t.t...
..t.t.t.t..
.t.t._.t.t.
..t.t.t.t..
...t.t.t...
....t.t....
.v...t...v.
...........
ENDMAP

NAME:   okawaru_gym_becter
TAGS:   uniq_altar_okawaru temple_overflow_1 temple_overflow_okawaru
TAGS:   transparent decor
KFEAT:  _ = altar_okawaru
: interest_check(_G)
MAP
   .....
 .........
 .vv...vv.
..v.....v..
....._.....
..v.....v..
 .vv...vv.
 .........
   .....
ENDMAP

### Qazlal overflow altars ####################################################

# There's always a safe path to the altar.
NAME:   grunt_qazlal_clouds
TAGS:   transparent no_monster_gen no_item_gen no_trap_gen temple_overflow_1
TAGS:   temple_overflow_qazlal uniq_altar_qazlal decor
KFEAT:  _ = altar_qazlal
MARKER:  L = lua:fog_machine { cloud_type = "flame", \
                               pow_min = 5, pow_max = 7, delay_min = 55, \
                               delay_max = 75, size = 1, walk_dist = 0, \
                               spread_rate = 0, excl_rad = 0 }
MARKER:  M = lua:fog_machine { cloud_type = "freezing vapour", \
                               pow_min = 5, pow_max = 7, delay_min = 55, \
                               delay_max = 75, size = 1, walk_dist = 0, \
                               spread_rate = 0, excl_rad = 0 }
MARKER:  N = lua:fog_machine { cloud_type = "sparse dust", \
                               pow_min = 5, pow_max = 7, delay_min = 55, \
                               delay_max = 75, size = 1, walk_dist = 0, \
                               spread_rate = 0 }
# not a storm cloud - it would be too noisy
MARKER:  O = lua:fog_machine { cloud_type = "rain", \
                               pow_min = 5, pow_max = 7, delay_min = 55, \
                               delay_max = 75, size = 1, walk_dist = 0, \
                               spread_rate = 0 }
SHUFFLE: LMNO
KPROP:   LMNO- = no_tele_into
KFEAT:   - = .
: interest_check(_G)
MAP
ccc@ccc
cL-.-Mc
c-...-c
@.._..@
c-...-c
cN-.-Oc
ccc@ccc
ENDMAP

NAME: nicolae_qazlal_scattered_showers
# The rain is localized to a 5x5 square centered on the altar.
# There's some rain water that occasionally gets deep but it's passable.
TAGS:   temple_overflow_qazlal temple_overflow_1 transparent decor
TAGS:   no_trap_gen uniq_altar_qazlal
KFEAT:  _ = altar_qazlal
MARKER: _ = lua:fog_machine { cloud_type = "rain", pow_min = 10, \
    pow_max = 15, delay_min = 5, delay_max = 15, size = 1, walk_dist = 2, \
    spread_rate = 0 }
: interest_check(_G)
MAP
.......
.......
.......
..._...
.......
.......
.......
ENDMAP

# Ru overflow altar vaults ####################################################

NAME: nicolae_ru_awakened_eye
TAGS: temple_overflow_ru temple_overflow_1 uniq_altar_ru transparent decor
TAGS: no_pool_fixup
: interest_check(_G)
KFEAT: _ = altar_ru
MAP
    .......
   .ccccccc.
  .c..WWW..c.
 .c..W...W..c.
.c...W._.W...c.
 .c..W...W..c.
  .c..WWW..c.
   .ccccccc.
    .......
ENDMAP

# Overgrown and disused, since many pilgrims find Ru's sacrifices too onerous.
NAME:   nicolae_ru_the_path_less_chosen
TAGS:   temple_overflow_ru temple_overflow_1 uniq_altar_ru transparent decor
TAGS:   no_item_gen no_monster_gen
: interest_check(_G)
KMONS:  p = plant / fungus w:3
NSUBST: r = 3=^ / ^p / 7=p / 2:p. / ., i = ^^p. / 2=p / .
NSUBST: D = . / xxp / x, E = . / xxp / x, F = xxp / .
KFEAT:  _ = altar_ru
KFEAT:  ^ = web trap
MAP
xxxxxxx
xrrrrrxxxxx
xriiirxDFEx
xri_ir.D.E.@
xriiirxDFEx
xrrrrrxxxxx
xxxxxxx
ENDMAP

NAME: nicolae_ru_sacrificial_ziggurat
TAGS: temple_overflow_ru temple_overflow_1 uniq_altar_ru transparent decor
: interest_check(_G)
SUBST: b = B.., @ = %@@
KPROP: B% = bloody
SUBST: B = ., % = @
FTILE: ' = floor_sandstone
FTILE: .@_ = floor_limestone
TILE: c = wall_sandstone
KFEAT: _ = altar_ru
MAP
cccccc@cccccc
c'''''b'''''c
c'ccccbcccc'c
c'c'''b'''c'c
c'c'ccbcc'c'c
c'c'cBBBc'c'c
@bbbbB_Bbbbb@
c'c'cBBBc'c'c
c'c'ccbcc'c'c
c'c'''b'''c'c
c'ccccbcccc'c
c'''''b'''''c
cccccc@cccccc
ENDMAP

NAME: nicolae_ru_path_of_blood
TAGS: temple_overflow_ru temple_overflow_1 uniq_altar_ru transparent decor
TAGS: no_monster_gen
: interest_check(_G)
KPROP: , = bloody
KPROP: l = no_cloud_gen
KFEAT: _ = altar_ru
MAP
@lcccccccccc
l,......,llc
@l,l,l,l,_lc
l,......,llc
@lcccccccccc
ENDMAP

NAME:   hangedman_ru_absence
TAGS:   temple_overflow_ru temple_overflow_1 uniq_altar_ru transparent decor
TAGS:   no_pool_fixup
WEIGHT: 5
KFEAT:  _ = altar_ru
SUBST:  X : x., x : cxx, b: cbxx, + : W+++, m : mmw
KMASK:  wW = no_monster_gen
: interest_check(_G)
MAP
......xxxxx......
..x...........x..
xxxxxx.....xxxxxx
..x..x.....x..x..
..x..x.XxX.x..x..
.....+..b..+.....
....xxbbbbbxx....
.....+..b_.+.....
..x..x.XxX.m..m..
..x..x.....m..m..
xxxxxx.....mmmmmm
..x...........m..
......xxxxx......
ENDMAP

### Sif Muna overflow altars ##################################################

NAME:   lemuel_blue_sif_altar
TAGS:   no_monster_gen temple_overflow_1 temple_overflow_sif_muna
TAGS:   uniq_altar_sif_muna decor transparent
COLOUR: . = blue
FTILE:  . = floor_hall, C = floor_hall, @ = floor_hall, + = floor_hall
KFEAT:  _ = altar_sif_muna
: interest_check(_G)
MAP
xxxxxxxxxxxxxx
...........xxx
@..........+_x
...........xxx
xxxxxxxxxxxxxx
ENDMAP

NAME:   led_sif_pool
TAGS:   no_item_gen no_monster_gen no_pool_fixup transparent
TAGS:   temple_overflow_1 temple_overflow_sif_muna uniq_altar_sif_muna decor
KFEAT:  _ = altar_sif_muna
COLOUR: . = blue
FTILE:  ._ = floor_marble
: interest_check(_G)
MAP
 xxx@xxx
 xxx+xxx
xxWW.WWxx
xWWW.WWWx
xWW...WWx
xWW._.WWx
xWW...WWx
xWWWWWWWx
xxWWWWWxx
 xxxxxxx
ENDMAP

NAME:   nicolae_sif_muna_condensation
TAGS:   temple_overflow_sif_muna temple_overflow_1 transparent decor
TAGS:   no_trap_gen no_pool_fixup uniq_altar_sif_muna
KFEAT:  _ = altar_sif_muna
MARKER: _ = lua:fog_machine { cloud_type = "magical condensation", \
    pow_min = 5, pow_max = 15, delay_min = 5, delay_max = 20, size = 1, \
    walk_dist = 3, spread_rate = 1 }
FTILE:  ._w = floor_marble
COLOUR: . = blue
: interest_check(_G)
MAP
.......
.wwwww.
.wwwww.
..._ww.
.wwwww.
.wwwww.
.......
ENDMAP

### Trog overflow altars ######################################################

NAME:  bloody_trog
TAGS:  uniq_altar_trog temple_overflow_1 temple_overflow_trog
TAGS:  transparent decor
KPROP: . = bloody
KFEAT: _ = altar_trog
: interest_check(_G)
MAP
...
._.
...
ENDMAP

NAME:   trog_three_pillars_becter
TAGS:   temple_overflow_1 temple_overflow_trog transparent decor
TAGS:   uniq_altar_trog
KFEAT:  _ = altar_trog
SUBST:  y = x..
SUBST:  z = xx.
: interest_check(_G)
MAP
 ..........
.......yy..
..yy..zxxy.
.yxxz.xbxy.
.yxb...xxy.
.yxx._.....
.......xy..
.....zbxy..
   ..yxxy..
   ...yy...
    ......
ENDMAP

### Uskayaw overflow altars ###################################################

NAME:    nicolae_uskayaw_dance_hall
# Either a square dance or a round dance depending on how the shuffle goes.
TAGS:    temple_overflow_uskayaw temple_overflow_1 transparent decor
TAGS:    uniq_altar_uskayaw
SHUFFLE: Gg
SUBST:   g : ., b : bvcmn
KFEAT: _ = altar_uskayaw
: interest_check(_G)
MAP
xxxx.........
x......GgG...
x.b...g...g..
x....G.....G.
 ._..g.....g.
x....G.....G.
x.b...g...g..
x......GgG...
xxxx.........
ENDMAP

NAME:   nicolae_uskayaw_murder_on_the_dancefloor
# But you'd better not kill the groove.
TAGS:   temple_overflow_uskayaw temple_overflow_1 transparent decor
TAGS:   uniq_altar_uskayaw
KFEAT:  _ = altar_uskayaw
NSUBST: . = 4=D / 3=D.
SUBST:  . = ..a, D = DDE
: dgn.delayed_decay(_G, "DE","random corpse")
KPROP:  aE = bloody
KFEAT:  b = .
COLOUR: ._aDE = disco
FTILE:  ._aDE = floor_pebble
: interest_check(_G)
MAP
xxxx@xxxx
x.......x
x.......x
x.......x
@..._...@
x.......x
x.......x
x.......x
xxxx@xxxx
ENDMAP

# https://www.youtube.com/watch?v=vpULWBT1WEg
NAME:    nicolae_uskayaw_suspiria
TAGS:    temple_overflow_uskayaw temple_overflow_1 transparent decor
TAGS:    uniq_altar_uskayaw
SHUFFLE: Aa, Bb
SUBST:   D : l, E : ...l, F : l, H : .l, J : ...l, K : ...l, L : ...l
SUBST:   M : ...l, N : .lll, O : ...l, P : ...l, Q : ...l
KPROP:   l = no_cloud_gen
KFEAT: _ = altar_uskayaw
: interest_check(_G)
MAP
xxxxxxlll..O..ll
x.FllH.L..ll.JNl
x.....M.ll..P.K.
x_.lll.Q...E.D.@
x.....M.ll..P.K.
x.FllH.L..ll.JNl
xxxxxxlll..O..ll
ENDMAP


### Vehumet overflow altars ###################################################

NAME:    vehumet_crystals
TAGS:    temple_overflow_1 temple_overflow_vehumet transparent decor
TAGS:    uniq_altar_vehumet
KFEAT:   _ = altar_vehumet
: interest_check(_G)
MAP
.........
..b...b..
.bbb.bbb.
..b...b..
...._....
..b...b..
.bbb.bbb.
..b...b..
.........
ENDMAP

# It's part of the design from the Vehumet altar tile.
NAME:   nicolae_vehumet_altar_sigil
TAGS:   temple_overflow_vehumet temple_overflow_1 transparent decor
TAGS:   uniq_altar_vehumet
KFEAT:  _ = altar_vehumet
COLOUR: . = white
COLOUR: + = green
COLOUR: D = lightblue
COLOUR: E = lightgreen
FTILE:  .DE+ = floor_pebble
FTILE:  _ = floor_pebble_lightgreen
: interest_check(_G)
MAP
 bbb+bbb
bb..E..bb
b..DDD..b
b.D.E.D.b
+EDE_EDE+
b.D.E.D.b
b..DDD..b
bb..E..bb
 bbb+bbb
ENDMAP

NAME:    nicolae_vehumet_radiance
TAGS:    temple_overflow_vehumet temple_overflow_1 no_trap_gen transparent
TAGS:    decor uniq_altar_vehumet
SHUFFLE: DEFHJKLM / FHDELMJK, DE, JK
SUBST:   DJ = N, EKFHLM = ., N : cb
KFEAT:   _ = altar_vehumet
: interest_check(_G)
MAP
     xxxxx
    .......
   ..MLMLM..
  ..LJKJKJL..
 ..MK.HFH.KM..
x.LJ.FDEDF.JL.x
x.MKHE...EHKM.x
x.LJFD._.DFJL.x
x.MKHE...EHKM.x
x.LJ.FDEDF.JL.x
 ..MK.HFH.KM..
  ..LJKJKJL..
   ..MLMLM..
    .......
     xxxxx
ENDMAP

### Wu Jian overflow altars ###################################################

NAME:   nzn_wu_jian_council_hall
TAGS:   temple_overflow_1 temple_overflow_wu_jian no_monster_gen no_item_gen
TAGS:   transparent decor uniq_altar_wu_jian
KFEAT:  _ = altar_wu_jian
: interest_check(_G)
MAP
    x@x
  xxx.xxx
 xxG...Gxx
xx.......xx
xG..._...Gx
xx.......xx
 xxG...Gxx
  xxxGxxx
    xxx
ENDMAP

# Abstract representations of the moves of Wu Jian's three martial arts.
# The green space is where you start, the red space is where you move to,
# the statue is where the monster is.
NAME:   nicolae_wu_jian_immortal_techniques
TAGS:   temple_overflow_wu_jian temple_overflow_1 decor transparent
TAGS:   uniq_altar_wu_jian
KFEAT:  _ = altar_wu_jian
KFEAT:  DE = .
FTILE:  D = floor_pebble_lightgreen
FTILE:  E = floor_pebble_lightred
COLOUR: D = green
COLOUR: E = red
: interest_check(_G)
MAP
cc+cccccc
c.D.c.EEc
c.E...DGc
c.G.c.EEc
ccnccc.cc
c...c...c
c._..EGDc
c...c...c
ccccccccc
ENDMAP

# The five elements, or five phases, Wood, Fire, Earth, Metal, Water,
# in a cycle of either mutual generation or mutual overcoming.
NAME:    nicolae_wu_jian_wuxing_cycle
TAGS:    temple_overflow_wu_jian temple_overflow_1 transparent \
         no_pool_fixup decor uniq_altar_wu_jian
SHUFFLE: tlcvw/lcvwt/cvwtl/vwtlc/wtlcv/tcwlv/cwlvt/wlvtc/lvtcw/vtwcl
NSUBST:  Q = 4=@ / x
KFEAT:   _ = altar_wu_jian
: interest_check(_G)
MAP
     xxQxx
   xx.....xx
  Q.........Q
 x......ll...x
 x..tt..ll...x
x...tt........x
x.............x
Q......_..cc..Q
x..ww.....cc..x
x..ww.........x
 x.....vv....x
 x.....vv....x
  Q.........Q
   xx.....xx
     xxQxx
ENDMAP

### Xom overflow altars #######################################################

NAME:    xom_teletrap_fun
TAGS:    temple_overflow_1 temple_overflow_xom uniq_altar_xom
TAGS:    transparent decor
KFEAT:   _ = altar_xom
NSUBST:  T = . / T
KFEAT:   T = teleport trap
: interest_check(_G)
MAP
.....
.TTT.
.T_T.
.TTT.
.....
ENDMAP

NAME:    xom_confusion_cloud
TAGS:    temple_overflow_1 temple_overflow_xom uniq_altar_xom transparent
KFEAT:   _ = altar_xom
MARKER:  _ = lua:fog_machine { cloud_type = "noxious fumes", \
               pow_min = 6, pow_max = 8, delay_min = 35, delay_max = 55, \
               size = 3, walk_dist = 0, spread_rate= 0 }
SUBST:   . = 1.
SHUFFLE: wW/l.
MONS:    random
MAP
  wwwww
 ww...ww
 w.....w
@W.._..w
 w.....w
 ww...ww
  wwwww
ENDMAP

NAME: nicolae_xom_altar_mimics
TAGS: temple_overflow_1 temple_overflow_xom uniq_altar_xom decor transparent
WEIGHT: 1
NSUBST: _ = 1:_ / 2:XXX_ / *:X
KFEAT: _ = altar_xom
KFEAT: X = altar_xom mimic
: interest_check(_G)
MAP
  .......
  ._..._.
 .........
 .........
...........
._..._..._.
...........
 .........
 .........
  ._..._.
  .......
ENDMAP

NAME:   doesnt_xom_only_xom
TAGS:   temple_overflow_1 temple_overflow_xom uniq_altar_xom transparent decor
KFEAT:  _ = altar_xom
KFEAT:  C = C mimic
NSUBST: C = 1:_ / *:C
: interest_check(_G)
MAP
xxxxxxxxxxx
x.G.G.G.G.x
xGC..C..CGx
x.........x
x.........x
xG.......Gx
x.C..C..C.x
xG.......Gx
xxxx...xxxx
   x...x
   x...x
   xG.Gx
   xx+xx
ENDMAP

### Yredelemnul overflow altars ###############################################

NAME:   yredelemnul_altar_generic
TAGS:   uniq_altar_yredelemnul temple_overflow_1 temple_overflow_yredelemnul
TAGS:   transparent decor
KFEAT:  C = altar_yredelemnul
KMONS:  f = withered plant
: interest_check(_G)
MAP
.....
.f.f.
..C..
.f.f.
.....
ENDMAP

### Zin overflow altars #######################################################

NAME:   zin_academy
TAGS:   no_item_gen no_monster_gen temple_overflow_1 temple_overflow_zin
TAGS:   transparent uniq_altar_zin
KFEAT:  _ = altar_zin
COLOUR: c = white
COLOUR: ' = white
TILE:   c = wall_church
FTILE:  _' = floor_limestone
SUBST:  ' = .
: interest_check(_G)
MAP
  ccccccc
ccc.ttttccc
c.tt'_'t..c
....'''....
...........
...'''''...
 .........
   .....
ENDMAP

NAME:   zin_statuary
TAGS:   no_item_gen no_monster_gen temple_overflow_1 temple_overflow_zin
TAGS:   transparent uniq_altar_zin
KFEAT:  _ = altar_zin
COLOUR: c = white
TILE:   c = wall_church
FTILE:  _'+ = floor_limestone
SUBST:  ' = .
: interest_check(_G)
MAP
 .......
..ccccc..
.cc'_'cc.
..cc+cc..
...'''...
.G.....G.
 ..G.G..
  .....
ENDMAP

### The Shining One overflow altars ###########################################

# TSO's small temple (1KB)
# Idea stolen from protected_by_tso_3.
NAME:     tso_altar
TAGS:     no_item_gen no_rotate decor transparent
TAGS:     temple_overflow_1 temple_overflow_the_shining_one
TAGS:     uniq_altar_the_shining_one
COLOUR:   _ = yellow
FTILE:    _A = floor_limestone
SUBST:    _ = .
COLOUR:   c = white
TILE:     c = wall_church
KFEAT:    A = altar_the_shining_one
: interest_check(_G)
MAP
  xxxxx
xxxcccxxx
xcccAcccx
xc.___.cx
xct._.tcx
xc.._..cx
xct...tcx
xccc+cccx
ENDMAP

NAME:    tso_metal_star
TAGS:    no_monster_gen no_trap_gen no_pool_fixup no_item_gen decor
TAGS:    temple_overflow_1 temple_overflow_the_shining_one transparent
TAGS:    uniq_altar_the_shining_one
KFEAT:   _ = altar_the_shining_one
SUBST:   ! : vmmTG..
: interest_check(_G)
MAP
          @
          .
         ...
         .v.
        ..v..
       ..vvv..
      ..vv.vv..
     ..vv...vv..
    ..vm..!..mv..
  ...vv..!.!..vv...
@..vvv..!._.!..vvv..@
  ...vv..!.!..vv...
    ..vm..!..+v..
     ..vv...vv..
      ..vv.vv..
       ..vvv..
        ..v..
         .v.
         ...
          .
          @
ENDMAP

NAME:   tso_oasis_becter
TAGS:   temple_overflow_1 temple_overflow_the_shining_one transparent
TAGS:   no_item_gen no_monster_gen uniq_altar_the_shining_one
KMONS:  f = plant
SUBST:  ` = ff.
COLOUR: . = yellow
FTILE:  `.Tf = floor_sand
FTILE:  _ = floor_limestone
COLOUR: c = white
TILE:   c = wall_church
KFEAT:  _ = altar_the_shining_one
: interest_check(_G)
MAP
  ....
 ..``....
 .`ww`...
 ..``..c..
......+cc..
..T..cc_cc.
 .....ccc..
   ....c..
     ....
ENDMAP

NAME: nicolae_tso_swordbearing_angel
TAGS: temple_overflow_the_shining_one uniq_altar_the_shining_one
TAGS: temple_overflow_1 decor transparent
COLOUR: . = yellow
COLOUR: c = white
FTILE: @._+ = floor_limestone
TILE: c = wall_church
KFEAT: _ = altar_the_shining_one
: interest_check(_G)
MAP
     xxxxx
    xxcccxx
    xcc.ccx
xxxxxc._.cxxxxx
xcccccc.ccccccx
xc...........cx
xcc....v....ccx
xxcc..vvv..ccxx
 xxcc..v..ccxx
  xxcc.v.ccxx
   xxc.v.cxx
    xc.v.cx
    xc.v.cx
    xc...cx
    xcc+ccx
ENDMAP


NAME: nicolae_tso_carved_into_rock
TAGS: temple_overflow_the_shining_one uniq_altar_the_shining_one
TAGS: temple_overflow_1 transparent decor
SUBST: X = xxx.
COLOUR: ' = yellow
COLOUR: c = white
TILE: G = dngn_golden_statue
TILE: c = wall_church
FTILE: G'_ = floor_limestone
KFEAT: _ = altar_the_shining_one
: interest_check(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxXXXXxxxxxxxxxxxxx
xxxxxxxXXXXXX....XXXXXXxxxxxxx
xxxxXXX................XXXxxxx
xxXX......................XXxx
xX......XxX........XxX......Xx
X.....XXxxxxcG'''GcxxxXX.....X
.....Xxxxxxxccc'cccxxxxxX.....
@...Xxxxxxxxxc'''cxxxxxxxX...@
..Xxxxxxxxxxxc'_'cxxxxxxxxXX..
XXxxxxxxxxxxxc'''cxxxxxxxxxxXX
xxxxxxxxxxxxxcccccxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

### Multiple god overflow altars ###############################################

# These ones are multiple overflows to specific gods;
# they get tags temple_overflow_N and temple_overflow_<god>.

NAME:    good_gods_temple_1
TAGS:    temple_overflow_3 temple_overflow_elyvilon transparent
TAGS:    temple_overflow_zin temple_overflow_the_shining_one
TAGS:    no_monster_gen no_trap_gen decor
MONS:    plant
KFEAT:   D = altar_elyvilon
KFEAT:   E = altar_zin
KFEAT:   F = altar_the_shining_one
SHUFFLE: DEF
SUBST:   1 : GTV1
SUBST:   X : GTt
SUBST:   ? : t@
MAP
  ttttttt
 tt.....tt
tt...E...tt
tt.D...F.tt
tt...1...tt
 tt.....tt
  tt...tt
   ?X@X?
ENDMAP

NAME:    good_gods_mini_temple
TAGS:    temple_overflow_3 temple_overflow_elyvilon
TAGS:    temple_overflow_zin temple_overflow_the_shining_one
TAGS:    no_monster_gen no_trap_gen no_pool_fixup
TAGS:    no_item_gen decor transparent
MONS:    plant
KFEAT:   D = altar_elyvilon
KFEAT:   E = altar_zin
KFEAT:   F = altar_the_shining_one
: if crawl.coinflip() then
SHUFFLE: x" / t1 / wT / IJ
: else
SUBST:   " : ..x
: end
SUBST:   HI : x
SUBST:   x : xcvb
SUBST:   ' : .
SHUFFLE: EG/'E
SUBST:   J : G
SHUFFLE: DEF
MAP
 xxxxx
xxxGxxx
xD.E.Fx
xx+++xx
 ""@""
ENDMAP

NAME:    dreamdust_divine_triumvirate
TAGS:    temple_overflow_3 temple_overflow_elyvilon
TAGS:    temple_overflow_zin temple_overflow_the_shining_one
TAGS:    no_monster_gen no_item_gen transparent
KFEAT:   D = altar_zin
KFEAT:   E = altar_elyvilon
KFEAT:   F = altar_the_shining_one
NSUBST:  ? = 1:D / 1:E / 1:F
TILE:    G = dngn_statue_triangle
KFEAT:   a = granite_statue
TILE:    a = dngn_statue_angel
: if crawl.one_chance_in(2) then
# Silver theme
KFEAT:   c = v
TILE:    c = dngn_metal_wall_white
COLOUR:  c- = white
FTILE:   -aDEFG = floor_w_marble
: else
# Gold theme
TILE:    c = wall_church
COLOUR:  c- = yellow
FTILE:   -aDEFG = floor_limestone
: end
MAP
.....................
.cccccccca-acccccccc.
..ccccccca-accccccc..
 ..cca---------acc..
  ..cc---?-?---cc..
   ..cc---G---cc..
    ..cc--?--cc..
     ..cc---cc..
      ..ccacc..
       ..ccc..
        ..c..
         ...
ENDMAP

NAME:  grunt_temple_overflow_growth_and_decay
TAGS:  temple_overflow_2 temple_overflow_fedhas temple_overflow_kikubaaqudgha
TAGS:  transparent
KFEAT: A = altar_fedhas
KFEAT: B = altar_kikubaaqudgha
MONS:  plant / nothing
MONS:  withered plant / nothing
MAP
xxxxx
x111xx@@
x1A111..xxx
x11.....22xxxx
xxxx11.....22x
   xxx..222B2x
      @@xx222x
         xxxxx
ENDMAP

NAME:  grunt_temple_overflow_lords_of_destruction
TAGS:  temple_overflow_2 temple_overflow_makhleb temple_overflow_vehumet
TAGS:  transparent
KFEAT: A = altar_makhleb
KFEAT: B = altar_vehumet
MAP
 xxxxx
xxA.Bxx
x.....x
xx...xx
 xx.xx
  x+x
ENDMAP

NAME:    grunt_temple_overflow_opulence
TAGS:    temple_overflow_2 temple_overflow_gozag temple_overflow_nemelex_xobeh
TAGS:    transparent
SHUFFLE: GU, _O
KFEAT:   _ = altar_gozag
KFEAT:   O = altar_nemelex_xobeh
TILE:    G = dngn_golden_statue
FTILE:   ._GOU+ = floor_limestone
RTILE:   x = wall_sandstone
COLOUR:  xG = yellow
: set_feature_name("granite_statue", "golden statue")
MAP
  xxxxxxx
 xx.....xx
xx...U...xx
x.........x
x.G._.O.G.x
x.........x
xx...U...xx
 xx.....xx
  xxx+xxx
ENDMAP

NAME:   grunt_temple_overflow_forces_of_nature
TAGS:   temple_overflow_2 temple_overflow_fedhas temple_overflow_qazlal
TAGS:   transparent
MONS:   fungus / plant / bush
SUBST:  2 = 1.
KFEAT:  _ = altar_fedhas
KFEAT:  O = altar_qazlal
KPROP:  12Ll = no_tele_into
SUBST:  L = ll^.
SUBST:  l = cll.
MAP
 222.@.LLL
21112.LlllL
21_.....OlL
21112.LlllL
 222.@.LLL
ENDMAP

NAME:   kennysheep_ying_yang
TAGS:   temple_overflow_2 temple_overflow_elyvilon temple_overflow_yredelemnul
TAGS:   no_monster_gen no_trap_gen no_pool_fixup patrolling transparent
KFEAT:  A = altar_elyvilon
KFEAT:  B = altar_yredelemnul
KFEAT:  D = tree
KFEAT:  '"wW = floor
TILE:   D = dngn_tree_dead
FTILE:  .t1ATW = floor_grass
COLOUR: tW = white
FTILE:  'D2BVw = floor_crypt
COLOUR: Dw = black
MAP
       """""""
     """""ccc"""
  """""""''Dccc"""
  "ccccccD''DDccc"
 ""ctttttcD'''DDc""
 "cct....tcD'''Dcc"
 "ct.www.TtD''''Dc""
""ct.wwwA.tD''''Dcc"
"cct.www.TtD'''''Dc"
"ct......tcD'''''Dc"
"ct.....tccD'''''Dc"
"ct.....tcD''''''Dc"
"ct.....tDV'WWW'DDc"
"cct....tD'BWWW'Dcc"
""ct....tDV'WWW'Dc""
 "cct...tcD''''Dcc"
 ""ctt...tcDDDDDc""
  "ccctt..tcccccc"
  """ccct.."""""""
    """ccc"""""
      """""""
ENDMAP

### Variable overflow altars ##################################################

# To make an overflow temple for N altars, give it the tag
# "temple_overflow_generic_N".

# The boring standard.
# B places the altar to a guaranteed god.
NAME:   overflow_temple_1_default
TAGS:   temple_overflow_generic_1 allow_dup transparent
MAP
B
ENDMAP

NAME:    old_standard_altar
TAGS:    transparent allow_dup decor temple_overflow_generic_1
SHUFFLE: cvxxxx
MAP
...........
.xxxx.xxxx.
.xx.....xx.
.x.......x.
.x.......x.
.....B.....
.x.......x.
.x.......x.
.xx.....xx.
.xxxx.xxxx.
...........
ENDMAP

NAME:    lemuel_round_altar
TAGS:    allow_dup transparent decor temple_overflow_generic_1
SUBST:   X:x@
SHUFFLE: cvxx
MAP
   xxXxx
  xx...xx
 xx.....xx
 x.......x
 X...B...@
 x.......x
 xx.....xx
  xx...xx
   xxXxx
ENDMAP

NAME:  lemuel_crystal_altar
TAGS:  transparent allow_dup decor temple_overflow_generic_1
MAP
b@b
@B@
b@b
ENDMAP

NAME:    lemuel_doored_altar
TAGS:    transparent decor temple_overflow_generic_1
SHUFFLE: XY
SUBST:   X = .
SUBST:   Y = +
MAP
.........
.xxxmxxx.
.xxxmxxx.
.xxxmxxx.
.X.YBY.X.
.xxxYxxx.
.xxx.xxx.
.xxxXxxx.
.........
ENDMAP

NAME:   lemuel_coloured_pillars_altar
TAGS:   no_rotate decor temple_overflow_generic_1 transparent
COLOUR: c = blue / yellow / red / cyan
MAP
xxxxxxxxxxxxxxxxxxxx
...................x
c...c...c...c...c..x
@.................Bx
c...c...c...c...c..x
...................x
xxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: nicolae_overflow_crystal_walls
TAGS: temple_overflow_generic_1 transparent
SHUFFLE: ACDE, FGHI
SUBST: A : b, C : b., D : b., E : b., F : b, G : b., H : b., I : b.
MAP
AAAAA@FFFFF
ACCCC.GGGGF
ACDDD.HHHGF
ACDEE.IIHGF
ACDE...IHGF
@....B....@
FGHI...EDCA
FGHII.EEDCA
FGHHH.DDDCA
FGGGG.CCCCA
FFFFF@AAAAA
ENDMAP

NAME:    marbit_many_paths
TAGS:    temple_overflow_generic_1 transparent
SHUFFLE: pqr / bqr / pbr / pqb / bbr / pbb / bqb
SUBST:   pqr = .
NSUBST:  . = 1:0 / *:.
FTILE:   .B0 = FLOOR_PEBBLE_GREEN
MAP
 xxxxxxxxx
xx..qxq..xx
xpqrpqrpqrxx
@qrpqrpqrpBx
xrpqrpqrpqxx
xx..pxp..xx
 xxxxxxxxx
ENDMAP

NAME:   mainiacjoe_overflow_brackets
TAGS:   temple_overflow_generic_1 transparent
SUBST:  c : cnvbt
NSUBST: w = 1:. / *:w
SUBST:  w : wl
KMASK:  lw = no_monster_gen
KMASK:  w = no_pool_fixup
MAP
...........
.wwwwwwwww.
.w.......w.
.w.ccccc.w.
.w.c.....w.
.w.c.B.c.w.
.w.....c.w.
.w.ccccc.w.
.w.......w.
.wwwwwwwww.
...........
ENDMAP

NAME:  mainiacjoe_overflow_boulevard
TAGS:  temple_overflow_generic_1 transparent
SUBST: P : cnvbtwl123
SUBST: Q : cnvbtwl123
MONS:  plant, bush, fungus
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
.............................
@.PPPP..QQQQ..B..QQQQ..PPPP.@
.............................
ENDMAP

NAME:  mainiacjoe_overflow_six_doors_small
TAGS:  temple_overflow_generic_1 transparent
SUBST: c : cvbt
SUBST: P : cnvbtwl
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
cccc+cccc
+.......+
c.P...P.c
c.P.B.P.c
c.P...P.c
+.......+
cccc+cccc
ENDMAP

NAME:  mainiacjoe_overflow_tiny_flanked
TAGS:  temple_overflow_generic_1 transparent
SUBST: P : cnvbtwl123
MONS:  plant, bush, fungus
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
.......
.P.B.P.
.......
ENDMAP

NAME:   mainiacjoe_overflow_cross_hatched
TAGS:   temple_overflow_generic_1 transparent
SUBST:  p : cnvbtwl123
MONS:   plant, bush, fungus
KMASK:  wl = no_monster_gen
KMASK:  w = no_pool_fixup
MAP
...........
.p.ppppp.p.
.p.......p.
.p.p...p.p.
.p.p.B.p.p.
.p.p...p.p.
.p.......p.
.p.ppppp.p.
...........
ENDMAP

NAME:  mainiacjoe_overflow_chevrons
TAGS:  temple_overflow_generic_1
SUBST: c : cvbn
SUBST: p : cnvbtwl123
SUBST: q : cnvbtwl123G
MONS:  plant, bush, fungus
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
...........
...........
..ppppppp..
..ppppppp..
..pp.......
..pp.......
..pp..qqqq.
..pp..q....
..pp..q.B..
......q....
...........
ENDMAP

NAME:  mainiacjoe_overflow_24_doors
TAGS:  temple_overflow_generic_1 transparent
SUBST: c : xcvb
MAP
ccc+cc+cc+ccc
c.....c.....c
c.....c.....c
+cc+cc+cc+cc+
c..c.....c..c
c..c+ccc+c..c
c..c.....c..c
+..+..B..+..+
c..c.....c..c
c..c+ccc+c..c
c..c.....c..c
+cc+cc+cc+cc+
c.....c.....c
c.....c.....c
ccc+cc+cc+ccc
ENDMAP

NAME:   mainiacjoe_overflow_surrounded_room
TAGS:   temple_overflow_generic_1 transparent
SUBST:  c : cvb
MAP
    .......
.....ccccc.....
.ccccc...ccccc.
.+.....B.....+.
.ccccc...ccccc.
.....ccccc.....
    .......
ENDMAP

NAME:  mainiacjoe_overflow_alcove_1
TAGS:  temple_overflow_generic_1 transparent
SUBST: c : xcvbt
MAP
ccccccc
c.....c
c..B..c
cc@@@cc
ENDMAP

NAME:  mainiacjoe_overflow_alcove_2
TAGS:  temple_overflow_generic_1 transparent
SUBST: c : xcvb
SUBST: G : GTUV
MAP
ccccccc
c.....c
c..B..c
c.....c
cGc.cGc
ccc@ccc
ENDMAP

NAME:  mainiacjoe_overflow_alcove_4
TAGS:  temple_overflow_generic_1 transparent
SUBST: c : xcnvb
SUBST: p : cnvbt123wl
MONS:  plant, bush, fungus
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
ccccccccc
c.......c
c.p.B.p.c
c.p...p.c
c.ppppp.c
c.......c
ccc+++ccc
ENDMAP

NAME:    mainiacjoe_overflow_concentric_impassable
TAGS:    temple_overflow_generic_1 transparent
NSUBST:  l = 1:. / *:l
NSUBST:  w = 1:. / *:w
KMASK:   wl = no_monster_gen
KMASK:   w = no_pool_fixup
SHUFFLE: wl
MAP
...........
.lllllllll.
.l.......l.
.l.wwwww.l.
.l.w...w.l.
.l.w.B.w.l.
.l.w...w.l.
.l.wwwww.l.
.l.......l.
.lllllllll.
...........
ENDMAP

NAME:  mainiacjoe_overflow_catwalk
TAGS:  temple_overflow_generic_1
TAGS:  transparent
SUBST: l : lw
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
..........
..lllllll.
.llllll...
.llB....G.
.llllll...
..lllllll.
..........
ENDMAP

NAME:   mainiacjoe_overflow_paths
TAGS:   temple_overflow_generic_1 transparent
SUBST:  c : xcbvt
SUBST:  w : lw
KMASK:  wl = no_monster_gen
KMASK:  w = no_pool_fixup
MAP
@ccccccccc@
c..wwwww..c
c.w.....w.c
c.w.www.w.c
c.ww...ww.c
c.ww.B.ww.c
c.ww...ww.c
c.w.www.w.c
c.w.....w.c
c..wwwww..c
@ccccccccc@
ENDMAP

NAME:   mainiacjoe_overflow_small_starburst
TAGS:   temple_overflow_generic_1 transparent
SUBST:  c : cvbtwl
KMASK:  w = no_pool_fixup
KMASK:  wl = no_monster_gen
MAP
  .........
 ...cc.cc...
..c..c.c..c..
..cc.....cc..
......B......
..cc.....cc..
..c..c.c..c..
 ...cc.cc...
  .........
ENDMAP

NAME:   amethyst_overflow_temple_binary
TAGS:   temple_overflow_generic_2 transparent
SUBST:  p : .:15 c:10 g:3 n:2
SUBST:  G : G:20 T:10 U:1 .:1
SUBST:  g : t:10 T:10 .:5 w:5 U:1 l:1
SUBST:  c : c:20 x:5 v:1 b:1
SUBST:  + : +:10 .:10 A:5
KMASK:  TUw = no_monster_gen
MAP
  ccc     ccc
 ccGcccccccGcc
cc...ccgcc...cc
cB.p..+.+..p.Bc
cc...cc.cc...cc
 ccGccg.gccGcc
  ccccc+ccccc
       @
ENDMAP

NAME:   gammafunk_temple_overflow_statue
TAGS:   temple_overflow_generic_2 transparent
SUBST:  g : t:10 T:10 .:5 w:5 l:5 U:1
KMASK:  wlTU = no_monster_gen
MAP
  ccc
cccGccc
c.B.B.c
cc...cc
ccg.gcc
 cc+cc
   @
ENDMAP

NAME:   gammafunk_temple_overflow_pools
TAGS:   temple_overflow_generic_2 transparent
SUBST:  G : G:20 T:10 U:1 .:1
KMASK:  TU = no_monster_gen
MAP
 ccccc ccccc
cc.B.ccc.B.cc
c.....G.....c
c..T.....T..c
cc.........cc
 ccc.....ccc
   ccc+ccc
      @
ENDMAP

NAME:    gammafunk_temple_overflow_elements
TAGS:    temple_overflow_generic_2 transparent
SHUFFLE: lw
KMASK:   lw = no_monster_gen
MAP
 ccccccc
ccB...Bcc
cclc+cwcc
 ccc@ccc
ENDMAP

NAME:    gammafunk_temple_overflow_wave
TAGS:    temple_overflow_generic_2 transparent
SUBST:   p = G:20 c:20 1 w:5 l:5
MONS:    plant / bush
KMASK:   Twl = no_monster_gen
MAP
   ccccc
 cccpcc
ccTB.+@
ccTB.+@
 cccpcc
   ccccc
ENDMAP

NAME:    jmf_multi_god_temple
TAGS:    transparent temple_overflow_generic_2
SHUFFLE: abc
SUBST:   a:+, b:x, c:x
MAP
............
.axxxxxxxxa.
.x........x.
.bT......Tb.
.x..B..B..x.
.cT......Tc.
.xxxxmmxxxx.
.xxx$$$$xxx.
.xx0....0xx.
..xx....xx..
...xG..Gx...
............
ENDMAP

NAME:  jmf_multi_god_temple2
TAGS:  transparent temple_overflow_generic_2
MAP
............
..vvvvvvvv..
.vv......vv.
.v..x..x..v.
.v.Bx..xB.v.
.v..x..x..v.
.vT0x..x0Tv.
.vvvx==xvvv.
...Gx..xG...
...+....+...
...GxxxxG...
............
ENDMAP

NAME:   mainiacjoe_overflow_catch_the_wave
TAGS:   temple_overflow_generic_2 transparent
NSUBST: p = 1:@ / *:.
NSUBST: q = 1:@ / *:.
SUBST:  b : cvbtwl123
MONS:   plant, bush, fungus
MAP
              .......
            ...bbbbb...
    ... .....bbb...bbb...
.....B...bbbbb......bbbb.
.bbb...bbb......bbb....b.
..bbbbbb......bbbbbb.....
....bbb...bbbbbb...bbbb..
.b......bbbbbb...B...bbb.
.bbb...bbb..... ... .....
...bbbbb...
  .......
ENDMAP

NAME:  mainiacjoe_overflow_two_lobes
TAGS:  temple_overflow_generic_2 transparent
SUBST: l : lwn
SUBST: b : bvt
KMASK: w = no_pool_fixup
KMASK: wl = no_monster_gen
MAP
  cccccc        cccccc
 cc....ccc    ccc....cc
 c.......cc  cc.......c
cc.bbb....c  c....bbb.cc
c....bbb..cccc..bbb....c
c......bb..ll..bb......c
c...B...b..ll..b...B...c
c......bb..ll..bb......c
c....bbb..+cc+..bbb....c
cc.bbb....c@@c....bbb.cc
 c.......cc  cc.......c
 cc....ccc    ccc....cc
  cccccc        cccccc
ENDMAP

NAME:  mainiacjoe_overflow_big_doors
TAGS:  temple_overflow_generic_2 transparent
SUBST: c : cvb
SUBST: p : nvbtwl123G
MONS:  plant, bush, fungus
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
cccccc+++cccccc
c...+.....+...c
c.B.+.ppp.+.B.c
c...+.....+...c
cccccc+++cccccc
ENDMAP

NAME:  mainiacjoe_overflow_swirl
TAGS:  temple_overflow_generic_2 transparent
SUBST: c : cvb
MAP
       ccccc
       c...c
       c.B.c
 +cccccc...c
 c...c.ccc.c
 c.c.c....cc
 c.c...cc.c
 c.cc...c.c
cc....c.c.c
c.ccc.c...c
c...cccccc+
c.B.c
c...c
ccccc
ENDMAP

NAME:  mainiacjoe_overflow_large_starburst
TAGS:  temple_overflow_generic_2 transparent
SUBST: b : cnvbtwl
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
.............
.b.bbb.bbb.b.
.b...b.b...b.
.bbb.b.b.bbb.
...b.b.b.b...
.B.........B.
...b.b.b.b...
.bbb.b.b.bbb.
.b...b.b...b.
.b.bbb.bbb.b.
.............
ENDMAP

NAME:  nicolae_overflow_squiral
TAGS:  temple_overflow_generic_2 no_pool_fixup transparent
MAP
........wwwwwwww
.wwwwwww.......w
.w......cccccc.w
.w.ccccc.....c.w
.w.c....wwww.c.w
.w.c.www...w.c.w
.w.c.wB.cc.w.c.w
.w.c.w...c.w.c.w
c.w.c.w...c.w.c.
c.w.c.ww.Bc.w.c.
c.w.c...ccc.w.c.
c.w.cccc....w.c.
c.w.....wwwww.c.
c.wwwwww......c.
c.......ccccccc.
cccccccc........
ENDMAP

NAME:    nicolae_overflow_two_pavilions
TAGS:    temple_overflow_generic_2 no_pool_fixup transparent
SHUFFLE: DEFH
SUBST:   DE : wl, FH : wl
KPROP:   l = no_cloud_gen
MAP
DDDDDDD@FFFFFFF
DcccccD.FcccccF
DcEEEcD.FcHHHcF
DcEB.......BHcF
DcEEEcD.FcHHHcF
DcccccD.FcccccF
DDDDDDD@FFFFFFF
ENDMAP

NAME: minmay_overflow_temple_star_quad
TAGS: temple_overflow_generic_3 transparent
MAP
           c
           c
          ccc
          cBc
         cc.cc
         c...c
        cc...cc
        c.....c
      ccc.....ccc
    ccc....G....ccc
  ccc......c......ccc
cccB.....GcccG.....Bccc
  ccc......c......ccc
    ccc....G....ccc
      ccc.....ccc
        c.....c
        cc...cc
         c...c
         cc.cc
          c.c
          c+c
           @
ENDMAP

NAME:   gammafunk_temple_overflow_forgotten
TAGS:   temple_overflow_generic_3 transparent
SUBST:  p = x:50 . 1, T = V:30 T, + = +:20 .
NSUBST: _ = 3:1 / *:_
MONS:   plant / bush
KMASK:  T = no_monster_gen
TILE:   x = wall_brick_brown_vines
FTILE:  _+T12B = floor_pebble_yellow
MAP
 ppppppp
pxB_B_Bxp
p_______p
p___T___p
px_____xp
 xxx+xxx
    @
ENDMAP

NAME:   grunt_temple_overflow_diamond
TAGS:   temple_overflow_generic_3 transparent
SUBST:  T = T G V:1 U:1 t:1
MAP
      c
     ccc
    ccBcc
   cc...cc
  cccc+cccc
 cc.c...c.cc
ccB.+.T.+.Bcc
 cc.c...c.cc
  cccc+cccc
   cc...cc
    cc.cc
     c.c
      @
ENDMAP

NAME:   gammafunk_temple_overflow_claw
TAGS:   temple_overflow_generic_3 transparent
SUBST:  l : lw
KMASK:  lw = no_monster_gen
MAP
  ...........
 ..cc..c..cc..
..cc..ccc..cc..
.ccc..ccc..ccc.
.ccc.ccccc.ccc.
.ccccccGcccccc.
.ccGB..B..BGcc.
..ccl.....lcc..
..ccll...llcc..
 ..ccll.llcc..
  ..ccl.lcc..
   ..cc+cc..
    ...@...
ENDMAP

NAME:   grunt_temple_overflow_cloverleaf
TAGS:   temple_overflow_generic_4 transparent
SUBST:  T : T U:1 V:1
MAP
 ccccc.@.ccccc
cc...cc.cc...cc
c.G.G.c.c.G.G.c
c..B..c.c..B..c
c.G...c.c...G.c
cc...+c.c+...cc
.ccccc...ccccc.
@......T......@
.ccccc...ccccc.
cc...+c.c+...cc
c.G...c.c...G.c
c..B..c.c..B..c
c.G.G.c.c.G.G.c
cc...cc.cc...cc
 ccccc.@.ccccc
ENDMAP

NAME: nicolae_overflow_quartered_altars
TAGS: temple_overflow_generic_4 transparent
SHUFFLE: fgh/ijk, pq/rs
SUBST: pqi = ., rk = c, s = +, z : cm, j : GTt
CLEAR: fgh
MAP
  ffghgff
 @.ccccc.@
f.qp.c.pq.f
fcp..z..pcf
gc..BcB..cg
hcczccczcch
gc..BcB..cg
fcp..z..pcf
f.qp.c.pq.f
 @.ccccc.@
  ffghgff
ENDMAP

NAME:  mainiacjoe_overflow_alcove_3
TAGS:  temple_overflow_generic_4 transparent
SUBST: c : xcvb
SUBST: p : cnvbt123wl
MONS:  plant, bush, fungus
KMASK: wl = no_monster_gen
KMASK: w = no_pool_fixup
MAP
ccccccc
cB.c.Bc
c.....c
cc.p.cc
c..p..c
cB...Bc
ccc@ccc
ENDMAP

NAME:   nicolae_overflow_macrodata_refinement
TAGS:   temple_overflow_generic_4 transparent
COLOUR: n = white
COLOUR: . = green
TILE:   n = dngn_transparent_stone_white
FTILE:  .Bcn = floor_pebble_green
MAP
.........
....bn...
..B.bnB..
.nnnbn...
.bbbnbbb.
...nbnnn.
..Bnb.B..
...nb....
.........
ENDMAP

NAME:   minmay_overflow_temple_star_hex
TAGS:   temple_overflow_generic_5 transparent
KMASK:  T = no_monster_gen
MAP
          ccc
         ccBcc
        cc...cc
       cc.....cc
cccccccc...G...cccccccc
cB...................Bc
ccc......G...G......ccc
  ccc.............ccc
    ccc....T....ccc
  ccc.............ccc
ccc......G...G......ccc
cB...................Bc
cccccccc...G...cccccccc
       cc.....cc
        cc...cc
         cc.cc
          c+c
          ...
          .@.
ENDMAP

NAME:   minmay_overflow_temple_hex
TAGS:   temple_overflow_generic_6 transparent
MAP
   cccccccccc
   cB......Bc
  cc........cc
  c...c..c...c
 cc..........cc
 c...c....c...c
cc............cc
cB..c......c..Bc
cc............cc
 c...c....c...c
 cc..........cc
  c...c..c...c
  cc........cc
   cB......Bc
   cccc++cccc
      ....
      ....
      .@@.
ENDMAP

NAME:  grunt_temple_overflow_octagram
TAGS:  temple_overflow_generic_7 transparent
SUBST: T : T G t V:1
MAP
          c
         ccc
        ccBcc
   cccccc...cccccc
   cB..ccc+ccc..Bc
   c..cc.....cc..c
   c.c+.......+c.c
   ccc....T....ccc
  ccc...T...T...ccc
 cc.c...........c.cc
ccB.+..T..T..T..+.Bcc
 cc.c...........c.cc
  ccc...T...T...ccc
   ccc....T....ccc
   c.c+.......+c.c
   c..cc.....cc..c
   cB..ccc+ccc..Bc
   cccccc...cccccc
        cc.cc
         c.c
          @
ENDMAP

NAME:    grunt_temple_overflow_open
TAGS:    temple_overflow_generic_8 temple_overflow_generic_7
TAGS:    temple_overflow_generic_6 temple_overflow_generic_5
TAGS:    temple_overflow_generic_4 temple_overflow_generic_3
TAGS:    temple_overflow_generic_2
TAGS:    transparent
SHUFFLE: defghijk / efghijkd
{{
  local altarcount = 1
  if is_validating() then
    altarcount = 8
  else
    altarcount = get_overflow_altar_count(_G)
  end
  if altarcount == "1" then
    subst("d = B")
    subst("efghijk = .")
  elseif altarcount == "2" then
    subst("dh = B")
    subst("efgijk = .")
  elseif altarcount == "3" then
    subst("dgi = B")
    subst("efhjk = .")
  elseif altarcount == "4" then
    subst("dfhj = B")
    subst("egik = .")
  elseif altarcount == "5" then
    subst("dfgij = B")
    subst("ehk = .")
  elseif altarcount == "6" then
    subst("defhij = B")
    subst("gk = .")
  elseif altarcount == "7" then
    subst("defghij = B")
    subst("k = .")
  else
    subst("defghijk = B")
  end
}}
SUBST: G = G T V:1 U:1 t:1
MAP
   ...
 ...d...
 .k...e.
.........
.j..G..f.
.........
 .i...g.
 ...h...
   ...
ENDMAP

# Formerly minivault_3; resurrected after being removed way back in late 2009.
NAME:  minitemple
TAGS:  temple_overflow_generic_14 temple_overflow_generic_13
TAGS:  temple_overflow_generic_12 temple_overflow_generic_11
TAGS:  temple_overflow_generic_10 temple_overflow_generic_9
TAGS:  temple_overflow_generic_8 temple_overflow_generic_7
TAGS:  temple_overflow_generic_6 temple_overflow_generic_5
TAGS:  temple_overflow_generic_4 temple_overflow_generic_3
TAGS:  temple_overflow_generic_2
TAGS:  transparent
# The original vault had allow_dup; I think that's overkill.
{{
  local altarcount = get_overflow_altar_count(_G)
  if is_validating() or altarcount == nil then
    altarcount = 14
  end
  nsubst("B : " .. altarcount .. ":B / *:x")
}}
SUBST: x = .:50 c:50 G T t:1 V:1 U:1
MAP
cccccccccccc
cBcBcBcBcBcc
G.c.c.c.c.Bc
@.........Bc
@.........Bc
G.c.c.c.c.Bc
cBcBcBcBcBcc
cccccccccccc
ENDMAP

NAME:    nicolae_overflows_in_rows
TAGS:    temple_overflow_generic_15 temple_overflow_generic_14 \
         temple_overflow_generic_13 temple_overflow_generic_12 \
         temple_overflow_generic_11 temple_overflow_generic_10 \
         temple_overflow_generic_9 transparent allow_dup
# I don't think temple_overflow_generic_15 is ever actually used, but I figured
# I might as well include it in the layout just in case it's used in the future.
{{
  local altarcount = 1
  if is_validating() then
    altarcount = 15
  else
    altarcount = tonumber(get_overflow_altar_count(_G))
  end
  if not altarcount then
    altarcount = 9
  end
  -- altar arrangements by row. rows of 3 have G or T at the ends.
  -- 9: 3/3/3. 10: 3/4/3. 11: 4/3/4. 12: 4/4/4. 13: 4/5/4. 14: 5/4/5. 15: 5/5/5.
  if altarcount >= 13 then
    subst("T=F")
  end
  if altarcount % 2 == 0 then
    subst("T=.")
    subst("F=Z")
    subst("H=F")
    subst("Z=H")
  end
  if altarcount >= 11 then
    subst("G=D")
  end
  if altarcount >= 11 and altarcount <= 13 then
    subst("D=Z")
    subst("E=D")
    subst("Z=E")
  end
}}
SUBST:   DF : B
SHUFFLE: GT
MAP
ccccccccccccc
c...........c
c.GEDEDEDEG.c
.............
..THFHFHFHT..
.............
c.GEDEDEDEG.c
c...........c
ccccccccccccc
ENDMAP

NAME:    nicolae_overflow_hallway_of_the_gods
TAGS:    temple_overflow_generic_11 temple_overflow_generic_10 \
         temple_overflow_generic_9 temple_overflow_generic_8 \
         temple_overflow_generic_7 temple_overflow_generic_6 \
         temple_overflow_generic_5 transparent allow_dup
{{
  local altarcount = 1
  if is_validating() then
    altarcount = 11
  else
    altarcount = tonumber(get_overflow_altar_count(_G))
  end
  if altarcount == 5 then
    subst("DFJ : B")
  elseif altarcount == 6 then
    subst("EHK : B")
  elseif altarcount == 7 then
    shuffle("EFHJK")
    subst("DEFH : B")
  elseif altarcount == 8 then
    shuffle("EFHJK")
    subst("EFHJ : B")
  elseif altarcount == 9 then
    shuffle("FK")
    subst("DEFHJ : B")
  elseif altarcount == 10 then
    subst("EFHJK : B")
  else
    subst("DEFHJK : B")
  end
}}
SUBST:   D : GGTTUt, E : GGTTUt, F : GGTTUt, H : GGTTUt, J : GGTTUt, K : GGTTUt
MAP
ccccccccccc
ccc.....ccc
cc...H...cc
c..J...F..c
c....c....c
c.K.ccc.E.c
c...cc....c
c@@@c..D..c@@@c
    c....cc...c
    c.E.ccc.K.c
    c....c....c
    c..F...J..c
    cc...H...cc
    ccc.....ccc
    ccccccccccc
ENDMAP

NAME:   chequers_temple_overflow_statue_of_worship
TAGS:   temple_overflow_generic_2 temple_overflow_generic_3
TAGS:   temple_overflow_generic_4 transparent
{{
  local altarcount = get_overflow_altar_count(_G)
  if altarcount == nil then
    altarcount = "2"
  end
  if altarcount == "2" then
    subst("C = .")
  else
    subst("C = B")
  end
  nsubst("B = " .. altarcount .. ":B / *:.")
}}
MAP
.....
..C..
.BGB.
..C..
.....
ENDMAP

NAME:  chequers_temple_overflow_minitemple
TAGS:  temple_overflow_generic_5 temple_overflow_generic_4
TAGS:  temple_overflow_generic_3
TAGS:  transparent
{{
  local altarcount = get_overflow_altar_count(_G)
  if is_validating() or altarcount == nil then
    altarcount = 5
  end
  nsubst("O = " .. altarcount .. ":B / *:l")
}}
KFEAT: l = altar_ecumenical
MAP
        @
       c+c
   ccccc.ccccc
  cc.........cc
ccc..ccccccc..ccc
cO..cccO.Occc..Oc
ccc..cc...cc..ccc
  cc.........cc
   cccc...cccc
      ccOcc
       ccc
ENDMAP
