##############################################################################
# depths.des: Depths entry vaults.
#             At time of writing, Depths vaults are shared with D and
#             tend to go in variable/*.des.
##############################################################################

##########################################################################
# Depths entries.
# U:1 is intended to be a considerable change of pace from earlier;
# there is a single point of entry / exit between the two levels.
# These vaults hold the stair to U:1 and some threats representative of
# later in the dungeon to make it clear what to expect.
##########################################################################

{{
function depths_entry(e)
   e.tags("depths_entry uniq_depths_entry chance_depths_entry")
   e.weight("20")
   e.kfeat("O = enter_depths")
   e.orient("float")
end
}}

# Dummy Depths entry
NAME:   depths_entry_dummy
TAGS:   extra transparent
: depths_entry(_G)
MAP
O
ENDMAP

NAME:    grunt_enter_depths_hoard
NSUBST: 1 = 1:8 / *:.
: depths_entry(_G)
MAP
  ccccccc
 ccdddd.cc
cc...1...ccccc
cO.ccccc.....cc
cc...1...cc...cc
 ccdddd.ccc.G..cc
  cccccccc.@.@..c
ENDMAP

NAME: grunt_enter_depths_magical_seal
NSUBST: 1 = 1:8 / *:.
: depths_entry(_G)
MAP
  bbbbbbb
 bb..1..bb
bb.......bb
b..bb+bb..b
b..b...b..b
b.1b.O.b1.b
b..b...b..b
b..bbbbb..b
bb...1...bb
 bb.....bb
  bbb+bbb
ENDMAP

NAME: gammafunk_enter_depths_forms
NSUBST: 1 = 1:8 / *:.
: depths_entry(_G)
MAP
 xxxxxx
xx.x..x bbbb
x.1...xbb..bb
xx.xx..b....bb
x..xxx...1...+
x....xxb....bb
xxbb.bbbb..bb
 bb...bbbbbb
bb.....bb
b1.....1b
bb.....bb
 bbbObbb
   bbb
ENDMAP

NAME: gammafunk_depths_entry_grave
TAGS: patrolling transparent
KMONS: t = withered plant
NSUBST: . = 1:8 / *:.
SUBST: . = .....--__tG
TILE: G = dngn_gravestone
FTILE: - = floor_pebble_brown
FTILE: _ = floor_pebble_darkgray
: set_feature_name("granite_statue", "a gravestone")
: depths_entry(_G)
MAP
  ....@....
 ...........
.............
.............
.............
.............
@.....O.....@
.............
.............
.............
.............
 ...........
  ....@....
ENDMAP

NAME:    hangedman_depths_entry_try_elsewhere_first
TAGS:    no_pool_fixup transparent
SHUFFLE: +'X / XX'
SUBST:   XY = x, '" = .
NSUBST: . = 1:8 / *:.
: depths_entry(_G)
MAP
xxxx'''x..@
x..x.w.X.w.
x..X...x...
xxx'Xxx+xXx
 xxx...x..'
 xxx.w.x.w'
 xcc...X..'
 xcOcxx'Xxx
 xcccxxx..x
 xxxxxxx..x
       xxxx
ENDMAP

NAME:    hangedman_depths_entry_civil_slaughter
TAGS:    transparent
SUBST:   c : c:9 v:2 b:1
NSUBST:  1 = 1:8 / *:.
: depths_entry(_G)
MAP
          .x...
          ...1.
      .....x...
      .cccc.ccc
      .cO..x...
 xxxxx.c.....x
 x1....c...x.x
 x..x.x.x.x..x
 x.x...c....1x
 x...1.c.xxxxx
...x..Tc.
ccc.cccc.
...x.....
.1...
...x.
ENDMAP

NAME:   hellmonk_depths_entry_around_the_bend
TAGS:   transparent
SUBST:  A = c:25 .:4 G:1
SUBST:  B : b:30 n:15 T:3 G:2 V:1
SUBST:  c : c:15 v:1
NSUBST: - = 1:8 / *:.
: depths_entry(_G)
MAP
 ccccccccccccc
ccBA'AA'AA'ABcc
cB'''''O'''''Bc
c'''''''''''''c
cA'''-A'A-'''Ac
cA---BcccB---Ac
cA--Acc ccA--Ac
c---Ac   cA---c
cA--Ac   cA--Ac
cA---c   c---Ac
cA--Ac   cA--Ac
cA--Ac   cA--Ac
cB@@Bc   cB@@Bc
ENDMAP
