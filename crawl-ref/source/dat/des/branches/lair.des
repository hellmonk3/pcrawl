################################################################################
# lair.des: All maps relating to the Lair branch.
#           Content order: branch entries, vault sets, decorative vaults,
#           other normal vaults, small ending vaults, large ending vaults.
#
################################################################################

{{
function wad_woods_setup(e, uniq, weight, mist, delaynum, sizenum, spreadnum)
    if uniq == "decor" then
        e.tags("uniq_wad_woods_decor decor extra")
    elseif uniq ~= "other" then
        e.tags("uniq_wad_woods_" .. uniq)
    end
    e.weight(weight)
    e.mons("plant")
    e.mons("bush")
    e.mons("fungus")

    e.ftile(".t+%*|q012345678COGTU = floor_grass")

    e.kfeat("O = shallow_water")
    if mist then
        e.lua_marker("O", fog_machine { pow_min = 3, pow_max = 7, delay = delaynum,
                          size = sizenum, walk_dist = 1, spread_rate = spreadnum } )
    end
end
}}

################################################################################
# Lair entries
################################################################################
# Dummy Lair entry
NAME:   lair_entry_dummy
TAGS:   lair_entry extra transparent
WEIGHT: 100
ORIENT: float
KFEAT:  O = enter_lair
MAP
O
ENDMAP

######################################
# Generic Lair entry.
#
NAME:    david_lair_generic
TAGS:    lair_entry transparent
ORIENT:  float
KMONS:   P = plant / fungus
SUBST:   A = ., P = P.
KFEAT:   O = enter_lair
MAP
xxxx..@..xxxx
xx.........xx
x..P.A...P..x
..PP......P..
@.....O.....@
..P......P...
x..P...A....x
xx.........xx
xxxx..@..xxxx
ENDMAP

# Shrubbed Lair entry
NAME:   david_lair_shrubbed
TAGS:   lair_entry transparent
ORIENT: float
KMONS:  P = plant
KFEAT:  O = enter_lair
SUBST:  ' = P.
MAP
xxxxxxxxxxxxx
xxPPPPPPPPPxx
xPP..'PPPPPPx
xPP.O.'PPP''.
xPP'..PPP'..@
xPPPPPP''....
xP''PP......x
xx.''..'...xx
xxxx..@..xxxx
ENDMAP

NAME:    dpeg_lair_monsters
TAGS:    ruin lair_entry transparent
ORIENT:  float
SUBST:   y = xx.
KFEAT:   O = enter_lair
MAP
xxxxx...@...y
x............
x....y.......
x...y...y...@
x..yy.O.yy...
@...y...y....
.......y..yy.
..........yy.
y...@........
ENDMAP

# Lava Lair entry
NAME:   minmay_lair_entry_lava
TAGS:   lair_entry no_monster_gen transparent
ORIENT: float
KFEAT:  O = enter_lair
NSUBST: M = . / L
SUBST:  L = l.
MAP
.........
...lLll..
..llllll.
..llMMlL.
.LlMOMll.
.ll.Mlll.
.l.llll..
..lllL...
.........
ENDMAP

# Welcome to Lair Island. We make great pancakes.
NAME:   erik_lair_island
TAGS:   lair_entry transparent
ORIENT: float
KFEAT:  O = enter_lair
SUBST:  - = w.
MAP
.@........wwwwww-.....
..-.-.-..wwwwwww.-..-.
..--w..wwwwwwwww-.--..
...--w.wwwwwwwwwwww-..
...wwww-wwwwwwwwww--..
..wwwwww--w--w.wwww..@
..wwwwww--....wwww---.
............O..wwww...
..-wwwww---...www.ww-.
.....wwww...w..www....
..wwwwwwwww-.wwww---..
@..wwwwwwwwwwwwwww....
....-ww..ww-www..w....
....-......--.w..---..
...........-...@......
ENDMAP

# Orcish Takeover!
NAME:   erik_lair_orcish_takeover
TAGS:   lair_entry transparent
ORIENT: float
KFEAT:  O = enter_lair
MAP
xxxx..@..xxxx
xx.........xx
x...........x
.............
@.....O.....@
.............
x...........x
xx.........xx
xxxx..@..xxxx
ENDMAP

NAME:   hellmonk_lair_entry_icy
TAGS:   lair_entry transparent
ORIENT: float
SUBST:  x = xxx., - = W:5 ':25 x:20 .:15
NSUBST: ' = 16 = 1''' / '
KFEAT:  O = enter_lair
TILE:   x = wall_ice_block
FTILE:  '1 = floor_ice
COLOUR: '1 = blue
MAP
       -------
     --'''''''--
    -'''''''''''-
   -''''-----''''-
  -'''.........'''-
 -'''.x.x...x.x.'''-
 -'''..xxx.xxx..'''-
-'''-.xxx.x.xxx.-'''-
-'''-..x.....x..-'''-
-'''-...x.O.x...-'''-
-'''-..x.....x..-'''-
-'''-.xxx.x.xxx.-'''-
 -'''. xxx.xxx..'''-
 -'''.x x...x.x.'''-
  -'''.........'''-
   -''''-----''''-
    -'''''''''''-
     --'''''''--
       ---@---
ENDMAP

###############################################################################
# Regular Lair vaults.
###############################################################################

###############################################################################
# A long set of wood themed vaults, as salvaged scraps from an idea for an
# early outdoors portal vault. Can generate in Lair and occasionally D.
# uniq_ tagged by tier: large, medium, small, rock, and decor.
#
NAME:   wad_woods_tower
DEPTH:  D:2-, Lair
ORIENT: float
FTILE:  q13%OG. = floor_woodground
: wad_woods_setup(_G, "large", 2, true, 21, 2, 36)
FTILE:  '*+T4 = floor_dirt
MAP
   tttt....   .tt1   tt.t    tttt  ..t
t.1.tttt.tt.11O..t.11ttt..  15tttt....t
t..11xxx.t.tttt.tttt....t1t..11t.t..ttt.
 .t.xx4xxt.1ttt..t1t.tt.ttt.tttt.1.ttt1
 .1x+'*'+x..t....t..t..1.t..11tt...t....
  .txx4xx.....t.1ttt.1ttt.1..t.t...t.1
t.15txxx.t.t.t..tt1.1G...t.t.1tt..ttt.
 ..t.t...tt..t..1...'''%..1t..t..t...t.
  .tt.t1...1t.1t...G'T'G.tt......t......
 ...t..t.tt..111.t..'''.11.1..t.t.1..t.
 ....t.t.t.ttt1.tt.1.G1t.t....t.t.5ttt..
..t.tt.t.t..t1t..t.t1tt.ttt.t...1..ttt
.11O...t..5.t..11t..t....1..11tt.1.t..t.
t..t1t..t...11t..ttt..ttt....tOt...t.11t
  1..t..t.  t..tt. 1t..  tt..tt  ttt.tt
   t.t.1.   .t1.  ..     .11    .t.1...
ENDMAP

NAME:    wad_woods_stonehenge
DEPTH:   D:2-, Lair
ORIENT:  float
: wad_woods_setup(_G, "med", 2, true, 21, 2, 36)
SHUFFLE: 4% / '"
SUBST:   '" = .
MAP
       .....t..tt1.t  t.t    .. tt...
      tt1tt.1tOt.t.11ttt..1.15tttt.1..
     ttt%t.tttt.tttt....t1t..11t.t.1tt..
    ttttttt1ttt..t1t.tt.ttt.tttt.1.ttt1t
    ttttttt.t.t..t..t..1.t..11tt1..t.1.ttt
   tttttt1t.1.t.1ttt.1t..1..t.t...1t11..tt
  15tttttt1t.t...t1..G...t.t......tt..1t11
t.tt.t.t.tt..t..1......c...c...t..t..t.t
.t11t.t1.t.1t.1t.........%.......t..1...
t1..t1.t.tt.t111.t...c.04C40.c..1t.1.tt.t.
 .1t.t.11t.ttt1.t..1.....%......t.5tttt...
  t.tt.t.t..t1t......1.c...c...1.1..tt1.
  1O.ttt1.5.t..11t1....1.1..11tt.1.t1...
   t1tttt.1.11t..ttt1.tt11tt.tOt1.tt.11
    tt..t.t1   tt.11t1.1    .tt.1ttt.
      .t.t  .   .t..t        .t1.t
ENDMAP

NAME:   wad_woods_01
DEPTH:  Lair
: wad_woods_setup(_G, "small", 2, true, 21, 2, 8)
MAP
     tt.tt  tt.1ttt
    tt...tt1tOtt.1.t
   ..1ttt....tttt...
 t4t.ttt.tttttt4t.t.1t
 t.tt..1tt1.tt.t.tt.tt
 .tt.%1...tt.1ttt..t.tt
tO1.tt..tt.t.t.tttt4t1
t.t.tt.t.tt.tG..ttt.tt
tt.ttt.1..ttt..ttt.1.tt
t.tt.ttttt.tt1ttt1.t.tt
ttt.tt4ttt..%.4ttOttt
  ..1tt.t1t..G...t.t
   .tt..4t1t.t.tt1t
    .ttt  .t..ttt
ENDMAP

NAME:   wad_woods_02
DEPTH:  Lair
: wad_woods_setup(_G, "small", 2, false, 0, 0, 0)
MAP
   .1t1.t   .t.1t
   t.1...t.1t1tt.1
   151t.t.1..t1tt...
  1t.ttt.tt.1t1t..t1
 t.tt.11tt1....t.tt.t
t.tt.%4..WWW......1t.tt
tt1.....WWwWW..t.tt.t1.
.1t.tt...WWW...tt1.t.t4
 t.t.t.1..W.4..t1t.1.tt
t.tt.t1t..1..1t.t1.t.
...1t11t1t.1%.ttt...
 t1.1tt.t1t..G..1..
  t11..1tt    .1.1t
    .1..1       .1.
ENDMAP

NAME:   wad_woods_03
DEPTH:  Lair
: wad_woods_setup(_G, "rock", 3, false, 0, 0, 0)
FTILE:  '%0+14 = floor_dirt
MAP
          .t t.tt'xx
   .''t...tt'''''t.1x
@'''.t''''''ttttx11x1tx
  ttt..t.tttxttxx11xx1x
  ...1t.tttxxxxxxx+xxx1
  .t..tttxtx%'4x''''x1x
  t.tt.tttxx%'0+'''0xx1
  .tt.tttttxxxxxxxxxxxx
   ....tt..tttxxtxxxxxx
    ..tttt..1xtttxxxxxx
    ...t1.ttt.1ttxxxxxx
      ..t.tt1txxtxxxxxx
         ttxxxxxxxxxxxx
            t1txxtttxtx
ENDMAP

NAME:   wad_woods_04
DEPTH:  Lair
ORIENT: float
: wad_woods_setup(_G, "rock", 2, false, 0, 0, 0)
FTILE:  '+*0 = floor_dirt
MAP
            .tttttt.ttxtx
          t1t.tttxtt.tttx
      tt1t11tt11ttxxx.xxx
     tt.tt..tttttttx.x.xx
    tt1t..tt11txxtt.ttx..
    tttt.tttttxxxx.xxxxxx
   tt1t2tttx.xxxx....xxxx
   ttt11tt1.t..t.txxx.x.x
  ..tttt1t.ttxx.xxxxxx.xx
 .t.1.tt....ttxttxt..xx.x
.ttttt..tttttttt.xxx..1x.
tt1t.t.txx..xx..xxxxxxxxx
tt...tttttxx.t.xxtxxxxxxx
x.tt.x.xxxxx..xxxxx''*xxx
.xx1xxxxxxxxxxx101x'0'xxx
xxxx1.........4xxx+'''xxx
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   wad_woods_06
TAGS:   extra
DEPTH:  Lair
: wad_woods_setup(_G, "small", 2, false, 0, 0, 0)
MAP
    tt2ttt  1
   tt.ttttt1t
  tttt.ttt1tt
tt1tt.1.tttt
tttt0...2tt1
 tt...G0.1ttt
 ttt02..1ttt
tt1tt...ttttt
t1tt1ttt21tt
   tttttt.t
     tt1t.t
ENDMAP

NAME:   wad_woods_river_01
TAGS:   extra
DEPTH:  D:2-, Lair
ORIENT: float
: wad_woods_setup(_G, "med", 1, false, 0, 0, 0)
MAP
      tttt       ttttt
   t.tt1t.tt.   tt.t..tt
  t..t1ttt.t.t.tt11t.tt.t
 .1tt.ttttttt.t.t..t.t.t...
 .t.tttttt.t.tt1....t.t.t...
  t.t1tttt.1ttt.t.ttt.t.tt1t.
.ttt.t.tt1t.1t.tt1t0t.tt.t.ttt
t.1t5tt.tt.tt.t.ttt%t.t.t1tttt
ttt1t......0..1tWWWWWWWWWWWttt
 tt...tWWWWWWWWWWwwwwwwwwwWWWW
WWWWWWWWwwwwwwwwwwWWWWWWwwwwww
wwwwwwwwwwwWWWWWWWWW1.tWWWWWWW
WWWWWWWWWWWWWt....1.......ttt
tttt..tt.t..%.t.t..ttt1..t.tt
ttt5tt.t.tt1t1t.t%1tt.t5tttttt
 tt1t.tttttt.t.tttt.t.t1t1.ttt
  ttt.t.t1t.t.t.1tt.t.tt11tt
  tt.1tt1t.ttttt.t.t.t1t.ttt
    .t1tttttttttttt.ttt.t.tt
    .tttt   tt ttttt.ttttt
   .ttt           ttt.tt
ENDMAP

NAME:   wad_woods_river_02
TAGS:   extra
DEPTH:  Lair
: wad_woods_setup(_G, "small", 1, true, 32, 2, 24)
MAP
      tttttttttt1tt
   tt1ttt..WWWW..0tt
ttt1ttt0.WWWwwWWW..tt
t11t.WWtWWWwwwwwWWWtt
.tt.WWWWWWwww|twwwWWt
WWWWWWwwwwwww>OwwwWWt
wwwwwwwwwwwwwwwwwwWWt
WWWWWWwwwwwwwwwwWWW1t
..ttWWWwwwwwwwWWW..tt
tt.t.tWWWwWWWWW.t.ttt
ttt1ttt.WWWt.ttt0t1t
  tttt1t0.ttttttt1tt
    tttttttttttttt
ENDMAP

NAME:   wad_woods_river_03
DEPTH:  Lair
: wad_woods_setup(_G, "decor", 1, false, 32, 3, 24)
MAP
   1t.tWwW.t.t.t
   t.ttWwWt.tt1t
   t.ttWwW.t11tt
  t11.WWwWWt.t1t
tt1t.WWwwWW.ttt
t1ttWWwwWW.tttt
t.WWwwwWWtOttt
WWwwwWWtt.t.t
wwwWWtt.tttt.
WWWWt.t.t1t
.tt.t.t1tt
ENDMAP

NAME:   wad_woods_river_04
TAGS:   no_vmirror no_rotate
DEPTH:  D, Lair
TILE:   X = wall_brick_dark_leak
TILE:   x = wall_brick_dark
SUBST:  X = x
: wad_woods_setup(_G, "decor", 1, false, 0, 0, 0)
MAP
   ttttxxxtttt
 tt1txxxxxxxtt
ttt1xxxxXxxxx1tt
tttt1xxxWxxx1tt
ttt.t.1tWt.1t1tt
tt.t1tt1WW1tt.tt
.1ttt1tWWtt1tt.t
ttt11tttWWttt1t1
tt.ttt1WWWtt*ttt
t.ttttWWWttttt1t
 tttt.WwWttt.tt
   tttWwW1tttt
   tttWwW..ttt
    ttWwWt..t
ENDMAP

NAME:   wad_woods_road_01
DEPTH:  Lair
: wad_woods_setup(_G, "decor", 2, true, 32, 3, 24)
FTILE:  @' = floor_dirt
MAP
    t1t.t1  tt.1tt   .@
  .1tt1.1   tOtt.1.tt.'
 tt..2ttt.1..tttt...%'.
 t2t.ttt.ttt1tt2t.t.'.
1t.tt..1tt1.tt.t.tt'.1
t.tt.%1..1tt.1ttt''t1tt
.........G.....1'...1..
@'''''''''''''''''''''@
.1...1...T3............
t.t.tt.t.tt.tG..t.t.ttt
tt.ttt.1..ttt..ttt.1.tt
 .tt.t2ttt.tt1t1t1.t.t
 tt.tt2ttt..%.2ttOttt
 t..1tt.t1t..G...t.t
  t.tt..2 1t.t.tt1t
  t21t      ..tttt
ENDMAP

NAME:   wad_woods_road_02
TAGS:   extra
DEPTH:  Lair
: wad_woods_setup(_G, "small", 2, false, 32, 0, 0)
FTILE:  @' = floor_dirt
MAP
      ttt      t
   ttttt1t  111ttt
 t.tttt1ttttt1t1t.t
t1t.tt1ttttttt1ttt.t
t....t.'''''0t....t.
@'''''''.G%''''''''@
t....t0'''''.t....t.
ttt..ttt1tt11tt.t.tt
tt.tt.t1tt1tt1ttttt
tt1tt1ttt303tttttt
 ttt1ttt3.U.3t1ttt
   tttt1t3.3ttt1tt
     tt ttttt1 tt
ENDMAP

NAME:   wad_woods_road_03
DEPTH:  Lair
: wad_woods_setup(_G, "decor", 2, false, 0, 0, 0)
FTILE:  @' = floor_dirt
MAP
       tt..@.tt
     1tt..'..t.t
   ttt1t..'.t.ttt
 tt1ttt..'.t0t.t
tt1tt...'.tttt
ttt..'''.t1t
....'..11ttt
.'''..ttt1t
@....tt1t
...1tttt
tttt1tt
ENDMAP

NAME:   wad_woods_road_04
DEPTH:  Lair
KMONS:  P = oklob plant
: wad_woods_setup(_G, "small", 1, false, 0, 0, 0)
FTILE:  @'+T = floor_dirt
MAP
  ttt..@..ttttttttt
 t11...'.ttt1ttP11t
 tttt1..'.tttt1t*1tt
ttt1tttt.'..ttttt1t
  ttttttt.'..1tt1ttt
   tt1t...'tttt..t..
  t1tttt.''''''''...
  ttttt..'.t1.tt.''@
   ttt1t.'tttttttttt
   tt1ttt.'.ttttttt
  tt1tttttt'..ttttt
  ttt1tt1..'.ttttt
   ttt1tt..@ttt
ENDMAP

NAME:   wad_woods_road_05
DEPTH:  Lair
KMONS:  * = 9
KITEM:  * = *
: wad_woods_setup(_G, "small", 2, false, 0, 0, 0)
FTILE:  @'*+T = floor_dirt
MAP
       .ttttt1.@.tt
     .1ttttt..'t1tt
    .tt..1t.''tttttt
   tt1tttt.'.t.tW1tt
ttt.tt....'.t1t1WWtt
tt.t.tt..'..tttttWWW
 tttt...'..t1tttxtWw
 t.1t.''tt1t1ttxxxtW
 ttt.'.t1tttt1x+*xxt
ttt.'.tttt   t1xxxtt
tt.@t1ttt    tttxttt
ENDMAP

NAME:   wad_woods_road_06
DEPTH:  Lair
: wad_woods_setup(_G, "decor", 1, false, 0, 0, 0)
FTILE:  @'*+T = floor_dirt
MAP
   tt.'@@..tt1ttttt..@@@..ttt
  tttt.'..ttttt1t11..'...1t11t
 tt1tt..'...tt1t1t...'..tt1tx1t
 tttt1t..'....tttt...'..tttxxx1
 ttttttt..''...ttt..'...ttttx1t
  tttttt1t..'...t..'..1.tttt%tt
   tttt.t1tt.''.%.'....t1tt1ttt
     t1ttttttt.'''...tt111t1tt
      ttttttttt1....tt1ttt1tt
       tttttttttttt1tttttt
           tttttttttttt
ENDMAP

NAME:   wad_woods_minihive
TAGS:   uniq_beehive
DEPTH:  D:2-, Lair
ORIENT: float
MONS:   killer bee, queen bee
KMONS:  8 = meliai
KITEM:  8 = %
KFEAT:  a = x
: wad_woods_setup(_G, "med", 5, false, 0, 0, 0)
: if you.in_branch("Lair") and you.depth() < 4
:  or you.in_branch("D") and you.depth() < 12 then
SUBST:  8 = 6
: end
KMASK:  W = no_monster_gen
COLOUR: a = yellow
TILE:   a = wall_wax
MAP
        ttttttt..t    tt.
      .ttt.t.tt...t  ttt..
    ttt.tt..tt.1t.t.1t...t...
 t.t1ttt....1WW..t....t1...t...
t..t..t.1..tWWW.t...1..t.tt..tt.
ttt.t1...t...tWW.1t.t...tt...ttt
tttt....aaamaaa1Wt.t...t..tt1ttt
t.t1.t1aa6.a66aaWWt1.1t.ttttttt
 tt.ttaaa.aba.aaaWW.t..ttttt1ttt
  tt.aa86a7a.a.6aaWtt1t...tt.tt
  t.aaaaaab|baaaaaa.t.......ttt
 t1.1aa66a.a.a.8aa.t...t.t.ttttt
tt.1..aaa.aba6aaa.1..t..tttt%ttt
t.t....aa.8a6.aaWW.t.tt.tt..t
tt.t.1.1aaaaaaaWWwW1.t..t.tt
 tt.t1.1....t1WWwW.....1ttt
  1..t.tt..tt.tWWt1.1tt...
    t.tttxtttt1ttttttxx.
      .txxxt   ttxxtttt.
ENDMAP

###############################################################################
# Remaining ungrouped decorative Lair vaults.

# "Pointless plantage" by Lemuel.
# Three separate decorative bubbles mixed into one vault.
NAME:   lemuel_fruit_fields
TAGS:   allow_dup extra transparent decor
DEPTH:  Lair
ORIENT: float
WEIGHT: 9
MONS:   plant, butterfly
ITEM:   gold
KFEAT:  ^~ = .
: local size = crawl.random2(9)
: if size > 7 then
SUBST:  '`"- = ^:20 ~:15 ., . = d:3 .:150 1:10 2:4
: elseif size > 3 then
SUBST:  "` = ^:20 ~:15 ., . = d:5 .:50 1:8 2:4
: else
SUBST:  '" = ^:20 ~:15 ., . = d:5 .:50 1:8 2:4
: end
CLEAR:  `'"-
COLOUR: ^ = green
FTILE:  ^ = floor_grass
COLOUR: ~ = brown
FTILE:  ~ = floor_dirt
MAP
       -------
     -----------
   -``------------
 -``````-----------
 ```````"'''-------
-`````"""'''''-``----
--```"""'''''""````--
----`"''''''"""`````-
-----'''''''"""`````-
-----'""'''''""````--
----``""""''''-``----
 --`````"""'--------
 --````````---------
   -``````--------
     -``--------
       -------
ENDMAP

# Two separate small decorative vaults mixed into one:
NAME:   lemuel_fruit_tree
TAGS:   extra transparent decor
DEPTH:  Lair, Workshop
ORIENT: float
MONS:   plant, fungus
KITEM:  a : stone
: if crawl.one_chance_in(5) then
SUBST:  '"~ = . 1
SUBST:  0`-. = .:90 a:2 2:1
SUBST:  _ = .:90 a:4
: else
CLEAR:  '-_
SUBST:  `" = . a 2:1
SUBST:  ~. = .:90 a:2 2:1
SUBST:  0 = 1
: end
MAP
   ________
  _--------_
 _---..~..--_
_--'.......--_
_---..`"`.~--_
_---..`0`..--_
_---..`"`..--_
 _--~....~.-_
  _--.....-_
   ________
ENDMAP

####

# A trio of precisely-mirrored pools.
NAME:    nicolae_quartered_pool_little
TAGS:    transparent extra decor uniq_qpool
DEPTH:   Lair
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm, Nn, Oo, Pp, Qq
SUBST:   abcdefghijklmnopq = ., ABCDEFGHIJKLMNOPQ = W
NSUBST:  7 = 1:@ / * = t@@@
MAP
    777777
   7qolLOQ7
  7pnkiIKNP7
 7pmjhfFHJMP7
7qnjgedDEGJNQ7
7okhecbBCEHKO7
7lifdbaABDFIL7
7LIFDBAabdfil7
7OKHECBbcehko7
7QNJGEDdegjnq7
 7PMJHFfhjmp7
  7PNKIiknp7
   7QOLloq7
    777777
ENDMAP

NAME:    nicolae_quartered_pool_medium
TAGS:    transparent extra decor uniq_qpool
DEPTH:   Lair
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu
SUBST:   abcdefghijklmnopqrstu = ., ABCDEFGHIJKLMNOPQRSTU = W
NSUBST:  7 = 1:@ / * = t@@
MAP
      7777
    77spPS77
   7urolLORU7
  7tqnkiIKNQT7
 7uqmjhfFHJMQU7
 7rnjgedDEGJNR7
7sokhecbBCEHKOS7
7plifdbaABDFILP7
7PLIFDBAabdfilp7
7SOKHECBbcehkos7
 7RNJGEDdegjnr7
 7UQMJHFfhjmqu7
  7TQNKIiknqt7
   7UROLloru7
    77SPps77
      7777
ENDMAP

NAME:    nicolae_quartered_pool_super
TAGS:    transparent extra decor uniq_qpool
DEPTH:   Lair
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu, Vv, Ww, Xx, Yy, Zz
SHUFFLE: 12, 34, 56
SUBST:   abcdefghijklmnopqrstuvwxyz135 = ., ABCDEFGHIJKLMNOPQRSTUVWXYZ246 = W
NSUBST:  7 = 1:@ / * = t@
MAP
      777777
    771xtTX277
   75zwspPSWZ67
  73yvrolLORVY47
 75yuqnkiIKNQUY67
 7zvqmjhfFHJMQVZ7
71wrnjgedDEGJNRW27
7xsokhecbBCEHKOSX7
7tplifdbaABDFILPT7
7TPLIFDBAabdfilpt7
7XSOKHECBbcehkosx7
72WRNJGEDdegjnrw17
 7ZVQMJHFfhjmqvz7
 76YUQNKIiknquy57
  74YVROLlorvy37
   76ZWSPpswz57
    772XTtx177
      777777
ENDMAP

NAME:    nicolae_quartered_pool_impasse
TAGS:    transparent extra decor no_pool_fixup uniq_qpool
DEPTH:   Lair
SHUFFLE: DN, EO, FP, HQ, JR, KS, LZ, M-
SUBST:   DEFHJKLM = w, NOPQRSZ- = l
MAP
  ......
 ..LHQZ..
..MKFPS-..
.LKJEORSZ.
.HFEDNOPQ.
.QPONDEFH.
.ZSROEJKL.
..-SPFKM..
 ..ZQHL..
  ......
ENDMAP

NAME:    nicolae_lair_grove_arrangements
TAGS:    transparent decor extra
DEPTH:   Lair
SHUFFLE: abcdefghi/''''T''''/T'''''''T/T'''T'''T/T'T'''T'T/T'T'T'T'T/\
         TTT'''TTT/'T'T'T'T'/T'T''''T'/'T'TTT'T'/'T'''''T'/TTTT'TTTT/\
         T'TTTTT'T/TT'TTT'TT/'''TTT'''/T'TT'TTTT/TT'T'T'TT/TT'''''TT/\
         TT''T''TT/TT''TT'T'/T''''T'T'
SUBST:   abcdefghiT = ttttt., ' = ., _ = ._
CLEAR:   _
MAP
__________________
_................_
_..aa...bb...cc.._
_.aaaa.bbbb.cccc._
_.aaaa.bbbb.cccc._
_..aa...bb...cc.._
_................_
_..dd...ee...ff.._
_.dddd.eeee.ffff._
_.dddd.eeee.ffff._
_..dd...ee...ff.._
_................_
_..gg...hh...ii.._
_.gggg.hhhh.iiii._
_.gggg.hhhh.iiii._
_..gg...hh...ii.._
_................_
__________________
ENDMAP

####

NAME:    bh_autumn_forest_stairs
TAGS:    extra luniq_staircase decor
DEPTH:   Lair
WEIGHT:  5
COLOUR:  t = lightred / red w:3
SHUFFLE: Aaq / Bbr / Ccs
SUBST:   q = @
SUBST:   A = ., bBcCdDrs = t
COLOUR:  t = lightred / red w:3 / green w:3 / lightgreen w:1
TILE:    t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
COLOUR:  a = lightred / red / green / lightgreen
TILE:    a = dngn_tree_lightred / dngn_tree_red / dngn_tree_yellow / dngn_tree
SUBST:   a = t
MAP
  aaaa qqq aaaa
 caaaaaAAAaaaaab
cccaaaaAAAaaaabbb
ccc...........bbb
ccc...........bbb
 cc..xxxxxxx..bb
sCC..x.....x..BBr
sCC..x..{..x..BBr
sCC..x.....x..BBr
 cc..xV...Vx..bb
ccc..xx+++xx..bbb
ccc...........bbb
ccc...........bbb
ccctttttttttttbbb
 cctttttttttttbb
  ctttt    tttb
ENDMAP

# Not very randomized, but the unusual shape, wall-plant use,
# and location should be variable enough. Intentionally lacks
# the transparent tag, to minimize niches in walls behind plants.
NAME:    hangedman_lair_unloving_vines
TAGS:    extra decor
DEPTH:   Lair
KMONS:   P = plant
KMASK:   ` = no_monster_gen
SHUFFLE: xP'. / xP'. / .'xP
SUBST:   '` = .
MAP
           xPx
         xxPxPxx    ````..@
        xPP'.'PPx   ....'''
      xxP''.'.''Px...'''PPP
   xxxPP'```.``'Px.''PPPxxx
xxxPPP''.xP'``.```'PPxxx
PPP'''...xP''.'.''Pxx
'''....   xPP'.'PPx
@..````    xxPxPxx
             xPx
ENDMAP

NAME:    cheibrodos_lair_caveside_slide
TAGS:    extra decor
DEPTH:   Lair, !Lair:$
MONS:    fungus w:3 / nothing, plant w:3 / nothing
SUBST:   1 : 12, " = x.
MAP
    ...xx
  ....xxx
 xx..1Wxxx
 xxx.1Wxxx
 xxx".WWxxx
  xxx.1Wxxx
  xxx.1WWxxx
  xxx".1WWxx
 xxxx"".1Wxx
xxxx""".1Wxxx
xxx"""..1WWxx
xxW1....1WWxx
xxWW(111WWxxx
xxWWWWWWWxxx
xxxWWWWWxxx
 xxxxxxxxx
  xxxxxx
ENDMAP

# Someone hasn't taken care of their garden, but occasionally it takes care of itself.
NAME:    pdpol_lair_neglected_garden
TAGS:    extra decor transparent no_trap_gen
DEPTH:   Lair
SHUFFLE: ac"` / pbbp
SUBST:   ab = D, cp = P
: if crawl.one_chance_in(5) then
MONS:    plant, bush
SUBST:   V = T, D = 2, P = 1
FTILE:   ' = floor_dirt, 12`" = floor_grass
:else
MONS:    withered plant / plant w:1, fungus
SUBST:   P = 1, D = 122
:end
SUBST:   '`" = .
MAP
.....@.....
..`P"'"P`..
.`Pac'caP`.
.``c'''c``.
.`"''D''"`.
@`"'DVD'"`@
.`"''D''"`.
.``c'''c``.
.`Pac'caP`.
..`P"'"P`..
.....@.....
ENDMAP

###############################################################################
# Remaining non-grouped regular Lair vaults.

NAME:   kennysheep_basilisk_garden
TAGS:   transparent
DEPTH:  Lair:2-
NSUBST: . = 3 = ...G / *:.
MAP
   .....
 ....G....
 .G.....G.
.....G.....
...........
.G.G.*.G.G.
...........
.....G.....
 .G.....G.
 ....G....
   .....
ENDMAP

NAME:   nicolae_lair_lil_pond
TAGS:   transparent ruin decor extra
DEPTH:  Lair
NSUBST: D = . / x
SUBST:  - = . / W
MAP
............
....xxxx....
..DDDxxxxx..
..x------x..
.xx-wwww-xx.
.xx-wwww-xx.
.xx-wwww-xx.
.xx-wwww-xx.
..x------x..
..xxxxxxxx..
....xxxx....
............
ENDMAP
