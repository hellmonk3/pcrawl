###############################################################################
# shoals.des: All maps and branch endings relating to the Shoals.
#
# <<1>>      Entries
# <<2>>      Decorative vaults
# <<3>>      Monster-placing vaults
# <<4>>      End vaults
#
###############################################################################
###############################################################################

: crawl_require("dlua/ghost.lua")

{{

function shoals_finale_setup(e, qualifier, decoy_count)
  qualifier = "_" .. qualifier
  if not decoy_count then
    decoy_count = crawl.random_range(6, 7)
  end
  e.hook('post_place', function ()
    -- one rune hut, one ilsuiw hut, maybe a mimic hut, and a bunch of decoys.
    dgn.place_maps{tag=("shoal_rune" .. qualifier), count=1, die_on_error=true}
    if crawl.one_chance_in(5) then
        dgn.place_maps{tag="shoal_guardian", count=1, die_on_error=true}
    end
    if crawl.one_chance_in(10) then
        dgn.place_maps{tag=("shoal_mimic" .. qualifier), count=1,
                       die_on_error=true}
        -- place fewer decoys if you have a fake rune hut, to compensate for
        -- increased threat
        decoy_count = decoy_count - 2
    end
    dgn.place_maps{tag=("shoal_hut" .. qualifier), count=decoy_count,
                   die_on_error=true}
  end)
end

function shoals_vault_is_mimic(e)
  local params = dgn.map_parameters()
  if params == nil then
    return false
  end

  local tag, tag2 = unpack(params)
  return tag2 ~= nil and string.find(tag2, "mimic")
end

-- When the kraken dies, the tides will control the water, so the items will
-- be accessible!
function shoals_atoll_kraken_death(data, triggerable, triggerer, marker, ev)
    crawl.mpr("The surface of the water froths suddenly, and then rushes away.",
              "warning")

    local points = dgn.find_marker_positions_by_prop("water_marker", 1)
    for _, point in ipairs(points) do
        dgn.fprop_changed(point.x, point.y, "no_tide")
    end
    -- Force tide effects to be applied immediately.
    dgn.apply_tide()
end

function shoals_atoll_place_items(data, triggerable, triggerer, marker, ev)
    local points = dgn.find_marker_positions_by_prop("tide_seed", 1)
    for _, point in ipairs(points) do
        for i = 1, 5 do
            dgn.create_item(point.x, point.y, "star_item")
        end
    end
end

function shoals_ice_setup(e)
  e.subst("- = xxxW.")
  e.rtile("x = wall_zot_cyan")
  e.colour(".x@01eO| = white")
  e.ftile(".x@+01eO| = floor_ice")
end
}}

###############################################################################
#
# <<1>> Shoals entries.
#       All must have the shoals_entry tag and place an enter_shoals feature.
#
###############################################################################

NAME:  shoals_dummy_entry
TAGS:  shoals_entry transparent
KFEAT: O = enter_shoals
MAP
O
ENDMAP

NAME:  minmay_shoals_entry_hut
TAGS:  shoals_entry transparent
SUBST: w = wwwW
TILE:  x = wall_pebble_lightgray
KFEAT: O = enter_shoals
MAP
  WWWWWWW
 WWwwwwwWW
WWwxxxxxwWW
Wwxx...xxwW
Wwx.....xwW
Wwx..O..+.@
Wwx.....xwW
Wwxx...xxwW
WWwxxxxxwWW
 WWwwwwwWW
  WWWWWWW
ENDMAP


NAME:   elwin_shoals_entry_rocks
TAGS:   shoals_entry transparent
KFEAT:  O = enter_shoals
MAP
.WW...W..
WxxW..xWW
WWxxWxxx.
.WW..WW..
.WW.O.xW.
W..W...WW
.xWW.WWW.
Wxx.WxxW.
.x....xx.
ENDMAP

###############################################################################
#
# <<2>> Decorative vaults.
#
###############################################################################

default-depth: Shoals

NAME:   overgrown_tower
TAGS:   transparent extra
WEIGHT: 2
MONS:   plant, fungus
SUBST:  x = x:60 . '
SUBST:  ' = ' 1:5
SUBST:  . = . 2:1
SUBST:  ' = .
MAP
 '''''''''
'''xx+xx'''
''xx...xx''
'xx.....xx'
'x.......x'
'x.......x'
'x.......x'
'xx.....xx'
''xx...xx''
'''xxxxx'''
 '''''''''
ENDMAP

NAME: nicolae_shoals_seven_spires
TAGS: transparent water_ok extra
MAP
     .....
   ....c....
  ....ccc....
 ..c...c...c..
 .ccc.....ccc.
...c..   ..c...
.....     .....
..c..     ..c..
.ccc.     .ccc.
..c...   ...c..
 .............
 ....c...c....
  ..ccc.ccc..
   ..c...c..
     .....
ENDMAP

NAME: nicolae_shoals_square_sea_wall
TAGS: water_ok transparent extra
MAP
..............
.cc..cccc..cc.
.c..........c.
...        ...
...        ...
.c.        .c.
.c.        .c.
.c.        .c.
.c.        .c.
...        ...
...        ...
.c..........c.
.cc..cccc..cc.
..............
ENDMAP

NAME: nicolae_shoals_circle_sea_wall
TAGS: water_ok transparent extra
MAP
      .....
    ..cc.cc..
   .cc.....cc.
  ....     ....
 .c..       ..c.
 .c.         .c.
.c.           .c.
.c.           .c.
...           ...
.c.           .c.
.c.           .c.
 .c.         .c.
 .c..       ..c.
  ....     ....
   .cc.....cc.
    ..cc.cc..
      .....
ENDMAP

NAME:    nicolae_shoals_stone_sculpture_thing
TAGS:    water_ok transparent extra
SHUFFLE: abde
SUBST:   ab = @, de = ., C : .c, y : .wc, z : .wc
MAP
    CbbC
   cc..cc
  ccz..zcc
 ccyc..cycc
Cczc....czcC
a....ww....d
a....ww....d
Cczc....czcC
 ccyc..cycc
  ccz..zcc
   cc..cc
    CeeC
ENDMAP

NAME:  nicolae_shoals_nautilus_by_nature
TAGS:  extra transparent water_ok
: if you.depth() == 1 then
SUBST: S = )}]
: elseif you.depth_fraction() < 1 then
SUBST: S = (){}[]
: else
SUBST: S = ({[
: end
MAP
       ccccccc
     ccc.....ccc
     c.........c
 @..cc.........cc
 ...c...ccccc...c
 ...c...c...c...c
c...c...c.S.c...c
c...c.......c...c
c...c.......c...c
cc..cc.....cc..cc
 c...ccccccc...c
 cc...........cc
  cc.........cc
   ccc.....ccc
     ccccccc
ENDMAP

NAME:    nicolae_shoals_gull_way
TAGS:    transparent water_ok
DEPTH:   Shoals
SHUFFLE: DEFHJKLMN
SUBST:   H : H`, J : J`, K : K`
NSUBST:  D = + / c, E = + / c, F = + / c, H = + / c, J = + / c, K = + / c, \
         P = 2=+ / 2=n / +c / nc / c, Q = + / n / +c / nc / c
SUBST:   _ = _w, LMN = `
KPROP:   +` = no_tide
CLEAR:   _
MAP
cccPccccPcccPccc+cc
c`````F```E```D```c
c`````F```E```D```c
P`````F```E```D```c
c`````F```E```D```c
c`````F```E```D```c
cHHHHHcc+cccQcccQcc
P`````c............ _______
c`````Q.wwwwwwwwwwwwwwwwwww____
cJJJJJc.wwwwwwwwwwwwwwwwwwwwwww____
c`````Q.wwwwwwwwwwwwwwwwwww____
P`````c............ _______
cKKKKKcc+cccQcccQcc
c`````L```M```N```c
c`````L```M```N```c
P`````L```M```N```c
c`````L```M```N```c
c`````L```M```N```c
cccPccccPcccPccc+cc
ENDMAP

###############################################################################
#
# <<4>> Shoals ending vaults
#
# Note that the Shoals usually have a randomised end map, by placing many
# copies of the same cave structures. Pre-made, non-encompass maps could also
# work, though they're not used yet.
#
###############################################################################

default-depth:

NAME: shoals_end_simple
ORIENT: float
PLACE: Shoals:$
: shoals_finale_setup(_G, 'simple')

# HangedMan's alternative Shoals ending
NAME: shoals_end_hangedman
ORIENT: float
PLACE: Shoals:$
WEIGHT: 8
: shoals_finale_setup(_G, 'hangedman', crawl.random_range(5,6))

# Grunt's icy Shoals ending
NAME: shoals_end_grunt_ice
ORIENT: float
PLACE: Shoals:$
WEIGHT: 8
: shoals_finale_setup(_G, 'grunt_ice', crawl.random_range(5,6))

# Grunt's garden Shoals ending
# Weighted lower on account of being more open and nasty.
NAME: shoals_end_grunt_garden
ORIENT: float
PLACE: Shoals:$
WEIGHT: 4
: shoals_finale_setup(_G, 'grunt_garden', crawl.random_range(5,6))

################################################################################
# Shoal huts.

NAME:      shoalhut_rune
TAGS:      shoal_rune_simple shoal_mimic_simple water_ok no_dump transparent \
           allow_dup
NSUBST:    A = 1:+ / 1 = x:19 +:1 / *:x
SUBST:     0 = 0.
SUBST:     e = e.
KMONS:     ge = merfolk impaler / merfolk javelineer / merfolk aquamancer
: if shoals_vault_is_mimic() then
KITEM:     O = barnacled rune of Zot mimic
: else
KITEM:     O = barnacled rune of Zot
: end
KPROP:     O = no_tide
LROCKTILE: wall_pebble_lightgray
MAP
 xxAxx
xx.0.xx
xe0g0ex
A0gOg0A
xe0g0ex
xx.0.xx
 xxAxx
ENDMAP

# Shoal decoy hut
NAME:   shoalhut
TAGS:   shoal_hut_simple water_ok no_dump allow_dup transparent
NSUBST: A = 1 = x:14 +:1 / *:x
NSUBST: 0 = 2:. / 2:0 / * = 0.
MAP
 xxAxx
xx.0.xx
x.0.0.x
A0.|.0A
x.0.0.x
xx.0.xx
 xx+xx
ENDMAP

# Shoal hangout of Ilsuiw.
# The hut itself may not contain Ilsuiw if she had other engagements.
NAME:    shoal_ilsuiw
TAGS:    shoal_guardian
KMONS:   1 = Ilsuiw, merfolk avatar
KMONS:   2 = merfolk impaler / merfolk javelineer
KMONS:   3 = merfolk aquamancer / merfolk impaler / merfolk javelineer
KMONS:   A = Ilsuiw band
KMONS:   B = merfolk aquamancer / merfolk javelineer
KFEAT:   123ABC = w
SHUFFLE: 123 / 123 / 123 / ABC / ABC
MAP
 wxwxw
wxw2wxw
xwwwwwx
w3w1w3w
xwwwwwx
wxw2wxw
 wxwxw
ENDMAP

################################################################################
# HangedMan's alternative Shoals:$

NAME:      shoals_rune_alternative
TAGS:      shoal_rune_hangedman shoal_mimic_hangedman water_ok allow_dup \
           transparent no_dump
KMONS:     12 = merfolk impaler / merfolk javelineer / merfolk aquamancer
KMONS:     3 = kraken / harpy band w:15
KMONS:     d = merfolk avatar / water nymph w:5
: if shoals_vault_is_mimic() then
KITEM:     d = barnacled rune of zot mimic
: else
KITEM:     d = barnacled rune of zot
: end
KFEAT:     123 = w
KPROP:     d = no_tide
SHUFFLE:   AB / AB / BA, CD / CD / DC
NSUBST:    2 = 2 = 22w / * = 22www, 0 = 2:0 / 1:. / * = 00...
SUBST:     AC = x, B = w, D = .
LROCKTILE: wall_pebble_lightgray
FHEIGHT: .d0@ = 15
MAP
  xxxxxxx
 xx12wwwxx
xx2x..00Wxx
x1xd02w..Wx
x2.03Awx..x
xw.2AxBw..x
xw0wwBw.D.x
xw0.xw..C.x
xxW...DC.x@
 xxW....x..
  xxxxxx@.
ENDMAP

NAME:    shoals_rune_alternative_decoy
TAGS:    shoal_hut_hangedman water_ok allow_dup transparent no_dump
SHUFFLE: AB / AB / BA, CD / CD / DC
NSUBST:  0 = 2:0 / 2:. / * = 00...
SUBST:   AC = x, B = w, D = .
FHEIGHT: .d0@ = 15
MAP
  xxxxxxx
 xxwwwwwxx
xxwx00.0Wxx
xwx|0ww..Wx
xw00wAwx..x
xw0wAxBw..x
xw.wwBw.D.x
xw0.xw..C.x
xxW...DC.x@
 xxW....x..
  xxxxxx@.
ENDMAP

# Deliberately separated from the styles of branch end;
# it's good randomization and balancing material.
NAME:    shoals_ilsuiw_alternative
TAGS:    shoal_guardian water_ok
WEIGHT:  8
KMONS:   1 = Ilsuiw, merfolk avatar
KMONS:   2 = merfolk impaler / merfolk javelineer
KMONS:   3 = merfolk aquamancer / merfolk impaler / merfolk javelineer
KMONS:   4 = Ilsuiw band
KMONS:   5 = merfolk aquamancer / merfolk javelineer
KFEAT:   123456 = w
SHUFFLE: 123 / 123 / 123 / 456 / 456
SHUFFLE: AB / AB / BA, CD / CD / DC
SUBST:   AC = x, B = w, D = .
MAP
  .@xxxx
 .x....xx
.x.CD..2xx
@.C..wxw3xx
x.D.wAwx..x
x..wA1Bw..x
x..xwBw.D.x
xx3wxw..C.@
 xx2..DC.x.
  xx....x.
   xxxx@.
ENDMAP

################################################################################
# Shoal icebergs (Grunt).

NAME:   shoals_iceberg_rune
TAGS:   shoal_rune_grunt_ice shoal_mimic_grunt_ice water_ok no_dump
TAGS:   transparent allow_dup
MONS:   merfolk impaler w:2 / merfolk javelineer / merfolk aquamancer
KMONS:  K = kraken simulacrum / frost giant w:20
: if shoals_vault_is_mimic() then
KITEM:  O = barnacled rune of Zot mimic
: else
KITEM:  O = barnacled rune of Zot
: end
KFEAT:  K = deep_water
NSUBST: A = 1:. / 1:xxxx. / *:x
NSUBST: 1 = 4:1 / 4 = 1., 0 = 2:0 / 2:. / * = 00...
NSUBST: w = 1:K / *:w
: shoals_ice_setup(_G)
KPROP:  O = no_tide
MAP
   .......
  .-------.
 ..xxxxxxx..
.-xA.---.Ax-.
.-x.10w01.x-.
.-x-0x1x0-x-.
.-x-w1O1w-x-.
.-x-0x1x0-x-.
.-x.10w01.x-.
.-xA.---.Ax-.
 ..xxxxxxx..
  .-------.
   .......
ENDMAP

# Shoal iceberg
NAME:   shoals_iceberg
TAGS:   shoal_hut_grunt_ice water_ok no_dump allow_dup transparent
NSUBST: A = 1:. / 1:xx. / *:x
NSUBST: 0 = 2:. / 2:0 / * = 0.
: shoals_ice_setup(_G)
MAP
   .......
  .-------.
 ..xxxxxxx..
.-xA.---.Ax-.
.-x..0w0..x-.
.-x-0x.x0-x-.
.-x-w.|.w-x-.
.-x-0x.x0-x-.
.-x..0w0..x-.
.-xA.---.Ax-.
 ..xxxxxxx..
  .-------.
   .......
ENDMAP

# Ilsuiw's winter home.
NAME:   shoals_ilsuiw_ice
TAGS:   shoal_guardian
KMONS:  e = Ilsuiw band, merfolk avatar
MONS:   merfolk aquamancer / merfolk javelineer
NSUBST: 1 = 2:1 / *:.
NSUBST: A = 1:. / 1:x. / *:x
: shoals_ice_setup(_G)
MAP
   .......
  ..-----..
 ...xxxxx...
...A.---.A...
.-x..1w1..x-.
.-x-1x.x1-x-.
.-x-w.e.w-x-.
.-x-1x.x1-x-.
.-x..1w1..x-.
...A.---.A...
 ...xxxxx...
  ..-----..
   .......
ENDMAP

################################################################################
# Shoal gardens (Grunt).
# This theme doesn't have a guardian vault - Ilsuiw doesn't hang around in
# gardens. If we get another satyr unique at some point, that would be a
# good candidate for this.

NAME:    shoals_garden_rune
TAGS:    shoal_rune_grunt_garden shoal_mimic_grunt_garden water_ok
TAGS:    no_dump allow_dup transparent
MONS:    satyr w:18 / merfolk impaler / merfolk javelineer / merfolk aquamancer
MONS:    faun w:6 / 0
KMONS:   P = plant
: if shoals_vault_is_mimic() then
KITEM:   O = barnacled rune of Zot mimic
: else
KITEM:   O = barnacled rune of Zot
: end
NSUBST:  1 = 4:1 / 2:1. / 2:1. / 2:2 / 2:2. / 2:2. / *:.
SHUFFLE: abcdefghijk, ABCDEFGH
SUBST:   abAB = x, cdeCDE = P, g : xP, h : P., ijkGH = .
KPROP:   O = no_tide
MAP
   abcbd
 kkaBAAdee
 jCCB1HHGf
ijD11111Gfg
iED11111FFg
hE111O111Eh
gFF11111DEi
gfG11111Dji
 fGHH1BCCj
 eedAABakk
   dbcba
ENDMAP

NAME:    shoals_garden
TAGS:    shoal_hut_grunt_garden water_ok no_dump allow_dup transparent
MONS:    faun w:6 / 0
KMONS:   P = plant
NSUBST:  1 = 2:1 / 2:1. / 2:1. / *:.
SHUFFLE: abcdefghijk, ABCDEFGH
SUBST:   abAB = x, cdeCDE = P, g : xP, h : P., ijkGH = .
MAP
   abcbd
 kkaBAAdee
 jCCB1HHGf
ijD11111Gfg
iED11111FFg
hE111|111Eh
gFF11111DEi
gfG11111Dji
 fGHH1BCCj
 eedAABakk
   dbcba
ENDMAP

###############################################################################
#
# Encompass endings
# These do not place huts in favor of a more standard vault structure and
# more themed/unusual monsters. Please keep the total weight of these
# endings low relative to the traditional endings.
#
###############################################################################

NAME:   shoals_end_hellmonk_lost_city
: set_border_fill_type("open_sea")
ORIENT: encompass
PLACE:  Shoals:$
WEIGHT: 3
KITEM:  O = barnacled rune of Zot
MONS:   faun w:15 / manticore / harpy, satyr, minotaur, sphinx
KMONS:  O = sphinx
KMONS:  P = plant
SUBST:  1 = 1.., 2 = 2.., 3 = 334, G = G.
SUBST:  ; : W.., p : W.., q : W.., r : W.., s : W.., y : W.., z : W..
NSUBST: | = 1:O / *:|, . = 16 = 0. / *=.........W
SUBST:  D = x., c = ccc.
NSUBST: ' = 1:( / 1:[ / 1:{ / 2 = <.. / *=P......
MAP
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxDDDxxwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwxDx1112xxxxwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwxDx2.....rrrxwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwxxD...xxxrrrccccccwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwxxDpppDDxrrrccc1..1cccwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwxDDpppxDDrrrrcc.......|ccDDDwwwwwwwwwwwwww
wwwwwwwwwwWWWWWWWWWDDpppxDD...xrr+...2...3.cc1.xxwwwwwwwwwwwww
wwwwwwwwwWWWWWWWWWDxpppxD...xxxrrcc.......|cc...xxwwwwwwwwwwww
wwwwwwwwWWWWWWWWWDxpppxx...xxxxrrrccc1..1ccc.....xDwwwwwwwwwww
wwwwwwwWWWWWWWWWDxpppxx...xxxxxxxrrrcccccc....2..2Dwwwwwwwwwww
wwwwwwWWW'''WWWDxpppxx.....yyyy2DDDrrrr......2GD..Dwwwwwwwwwww
wwwwwwWW''''''WDxpppDxxxDDDxxyyyDxrrr1.....1DxxD..Dwwwwwwwwwww
wwwwwwW'''''''WDD..pDxD1..1DxyyyyxrrxxDDxx..xxxx..Dwwwwwwwwwww
wwwwwwW''cc''.WDD..pDx......Dyyyxxxxx2..2x..DxxD..Dwwwwwwwwwww
wwwwwwW''cc''.WDD..pDx1....1+yy...2xD....D...2.2.1Dwwwwwwwwwww
wwwwwwW''''''.W+...pDx......Dyy.....D....D...ccccccccwwwwwwwww
wwwwwwW'''''..W+...ppxD1..1DxyyDD1..x1..2x.ccc1....1cccwwwwwww
wwwwwwW''''..ccccc..pxDDDDxxxxxDD...xD++Dxcc1.........ccwwwwww
wwwwwwW'''..cc..1cc..G....G.....G....G....Gc.....2.....|cwwwww
wwwwwwW'''..+....2+;;;;;;;;;;;;;;;;;;;;;;;;+..2......3.|cwwwww
wwwwwwW'''..cc..1cc..G....G.....G....G....Gc.....2.....|cwwwww
wwwwwwW''''..ccccc..qxDDDDxxxxxDD...xD++Dxcc1.........ccwwwwww
wwwwwwW'''''..W+...qqxD1..1DxzzDD1..x1..2x.ccc1....1cccwwwwwww
wwwwwwW''''''.W+...qDx......Dzz.....D....D...ccccccccwwwwwwwww
wwwwwwW''cc''.WDD..qDx1....1+zz...2xD....D...2.2.1Dwwwwwwwwwww
wwwwwwW''cc''.WDD..qDx......Dzzzxxxxx2..2x..DxxD..Dwwwwwwwwwww
wwwwwwW'''''''WDD..qDxD1..1DxzzzzxssxxDDxx..xxxx..Dwwwwwwwwwww
wwwwwwWW''''''WDxqqqDxxxDDDxxzzzxxsss......1DxxD..Dwwwwwwwwwww
wwwwwwWWW'''WWWDxqqqxx.....zzzz2DDDssss1.....2GD..Dwwwwwwwwwww
wwwwwwWWWWWWWWWWDxqqqxx...xxxxxxxssscccccc....2..2Dwwwwwwwwwww
wwwwwwwWWWWWWWWWWDxqqqxx...xxxxsssccc1..1ccc.....xDwwwwwwwwwww
wwwwwwwwWWWWWWWWWWDxqqqxD...xxxsscc.......|cc...xxwwwwwwwwwwww
wwwwwwwwwWWWWWWWWWWDDqqqxDD...xss+...2...3.cc1.xxwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwxDDqqqxDDsssscc.......|ccDDDwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwxxD...DDxsssccc1..1cccwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwxxD2..xxxsssccccccwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwxDx......sssxwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwxDx1112xxxxwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxDDDxxwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
ENDMAP

NAME: shoals_end_hellmonk_storm_palace
: set_border_fill_type("open_sea")
ORIENT: encompass
PLACE:  Shoals:$
WEIGHT: 3
MONS:   water elemental / air elemental w:5, wind drake / water nymph
MONS:   merfolk impaler / merfolk javelineer w:5 / merfolk aquamancer w:5
SUBST:  + : ++., p : ccc+., q : ccc+., r : ccc+., s = W.c
SUBST:  1 = 011..., 2 = 2., 3 = 33.
NSUBST: ; = 1:{ / 1:[ / 1:( / * = <.
SUBVAULT: D : storm_palace_rune
SUBVAULT: E : storm_palace_corner
SUBVAULT: F : storm_palace_corner
SUBVAULT: H : storm_palace_corner
SUBVAULT: J : storm_palace_corner
MAP
cccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccc
ccEEEEEEEEEEE.....1...1..1...2.....FFFFFFFFFFFcc
ccEEEEEEEEEEE.....2...1..1...1.....FFFFFFFFFFFcc
ccEEEEEEEEEEEcccccccccc22ccccccccccFFFFFFFFFFFcc
ccEEEEEEEEEEEcG1................1GcFFFFFFFFFFFcc
ccEEEEEEEEEEEc1....ssWWWWWWss....1cFFFFFFFFFFFcc
ccEEEEE;EEEEEc..sWWWWWsWWsWWWWWs..cFFFFF;FFFFFcc
ccEEEEEEEEEEEc1sWWWssssssssssWWWs1cFFFFFFFFFFFcc
ccEEEEEEEEEEEc..sWWWWWssssWWWWWs..cFFFFFFFFFFFcc
ccEEEEEEEEEEEc1....ssWWWWWWss....1cFFFFFFFFFFFcc
ccEEEEEEEEEEEcG3.......23.......3GcFFFFFFFFFFFcc
ccEEEEEEEEEEcccccccccccppcccccccccccFFFFFFFFFFcc
cc..cccccccccDDDDDDDDDDDDDDDDDDDDDDccccccccc..cc
cc..cG1.1.1GcDDDDDDDDDDDDDDDDDDDDDDcG1.1.1Gc..cc
cc..c1..s..3cDDDDDDDDDDDDDDDDDDDDDDc3..s..1c..cc
cc..c..sWs..cDDDDDDDDDDDDDDDDDDDDDDc..sWs..c..cc
cc..c..WWW..cDDDDDDDDDDDDDDDDDDDDDDc..WWW..c..cc
cc21c..WWW..cDDDDDDDDDDDDDDDDDDDDDDc..WWW..c21cc
cc..c.sWsWs.cDDDDDDDDDDDDDDDDDDDDDDc.sWsWs.c..cc
cc..c.sWsWs.cDDDDDDDDDDDDDDDDDDDDDDc.sWsWs.c..cc
cc..c.WWsWW.cDDDDDDDDDDDDDDDDDDDDDDc.WWsWW.c..cc
cc11c.WsssW.cDDDDDDDDDDDDDDDDDDDDDDc.WsssW.c11cc
cc..2.WWssW3rDDDDDDDDDDDDDDDDDDDDDDq2WssWW.2..cc
cc..2.WWssW2rDDDDDDDDDDDDDDDDDDDDDDq3WssWW.2..cc
cc11c.WsssW.cDDDDDDDDDDDDDDDDDDDDDDc.WsssW.c11cc
cc..c.WWsWW.cDDDDDDDDDDDDDDDDDDDDDDc.WWsWW.c..cc
cc..c.sWsWs.cDDDDDDDDDDDDDDDDDDDDDDc.sWsWs.c..cc
cc..c.sWsWs.cDDDDDDDDDDDDDDDDDDDDDDc.sWsWs.c..cc
cc12c..WWW..cDDDDDDDDDDDDDDDDDDDDDDc..WWW..c12cc
cc..c..WWW..cDDDDDDDDDDDDDDDDDDDDDDc..WWW..c..cc
cc..c..sWs..cDDDDDDDDDDDDDDDDDDDDDDc..sWs..c..cc
cc..c1..s..3cDDDDDDDDDDDDDDDDDDDDDDc3..s..1c..cc
cc..cG1.1.1GcDDDDDDDDDDDDDDDDDDDDDDcG1.1.1Gc..cc
cc..cccccccccDDDDDDDDDDDDDDDDDDDDDDccccccccc..cc
ccHHHHHHHHHHccccccccccc++cccccccccccJJJJJJJJJJcc
ccHHHHHHHHHHHcG3.......32.......3GcJJJJJJJJJJJcc
ccHHHHHHHHHHHc1....ssWWWWWWss....1cJJJJJJJJJJJcc
ccHHHHHHHHHHHc..sWWWWWssssWWWWWs..cJJJJJJJJJJJcc
ccHHHHHHHHHHHc1sWWWssssssssssWWWs1cJJJJJJJJJJJcc
ccHHHHH;HHHHHc..sWWWWWsWWsWWWWWs..cJJJJJ;JJJJJcc
ccHHHHHHHHHHHc1....ssWWWWWWss....1cJJJJJJJJJJJcc
ccHHHHHHHHHHHcG1................1GcJJJJJJJJJJJcc
ccHHHHHHHHHHHcccccccccc22ccccccccccJJJJJJJJJJJcc
ccHHHHHHHHHHH.....1...1..1...2.....JJJJJJJJJJJcc
ccHHHHHHHHHHH.....2...1..1...1.....JJJJJJJJJJJcc
cccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

NAME:   storm_palace_center_square
TAGS:   storm_palace_rune
KITEM:  O = barnacled rune of Zot
KITEM:  | = superb_item
MONS:   merfolk aquamancer / merfolk javelineer / merfolk impaler
KMONS:  O = patrolling storm dragon / patrolling titan
KFEAT:  |O = shallow_water
NSUBST: | = 1:O / *:|, 1 = 4 = 8. / * = 1..
SUBST:  p : W., q : W., r : W., S : W.
MAP
cccc..pppppppppp..cccc
cWW..ppppccccpppp..WWc
cW1...ppppccpppp...1Wc
c..ccccppppppppcccc..c
...cWW..pppppp..WWc...
.r.cW.1...pp...1.Wc.s.
rrrc.1ccccWWcccc1.csss
rrrr..ccWWWWWWcc.sssss
rrrrr.cWWWWWWWWc.sssss
rcrrr1cWWc||cWWc1ssscs
rccrrrWWWW||WWWWsssccs
rccrrrWWWW||WWWWsssccs
rcrrr1cWWc||cWWc1ssscs
rrrrr.cWWWWWWWWc.sssss
rrrr..ccWWWWWWcc..ssss
rrrc.1ccccWWcccc1.csss
.r.cW.1...qq...1.Wc.s.
...cWW..qqqqqq..WWc...
c..ccccqqqqqqqqcccc..c
cW1...qqqqccqqqq...1Wc
cWW..qqqqccccqqqq..WWc
cccc..qqqqqqqqqq..cccc
ENDMAP

NAME:   storm_palace_center_circular
TAGS:   storm_palace_rune no_pool_fixup
KITEM:  O = barnacled rune of Zot
MONS:   merfolk aquamancer / merfolk javelineer / merfolk impaler
KMONS:  O = patrolling storm dragon / patrolling titan
KMONS:  P = plant
NSUBST: | = 1:O / *:|, 1 = 4 = 8. / * = 1..
SUBST:  . = P......
MAP
c....................c
1.cccccccccccccccccc.1
.cc1......1.......1cc.
....WWWWWWWWWWWWWW....
...WWccccccccccccWW...
...Wcc.....1....ccW...
...Ww............wW...
.c.Ww...cccccc...wW.cW
...Wc..cc|..|cc..cW.WW
...Wc..1....1ww..cWWWW
.c.Ww.....||.wwWWWWWcW
.c.Ww.....||.wwWWWWWcW
...Wc..1....1ww..cWWWW
...Wc..cc|..|cc..cW.WW
.c.Ww...cccccc...wW.cW
...Ww............wW...
...Wcc....1.....ccW...
...WWccccccccccccWW...
....WWWWWWWWWWWWWW....
.cc1.......1......1cc.
1.cccccccccccccccccc.1
c....................c
ENDMAP

NAME:   storm_palace_center_ruins
TAGS:   storm_palace_rune
KITEM:  O = barnacled rune of Zot
MONS:   merfolk aquamancer / merfolk javelineer / merfolk impaler
KMONS:  O = patrolling storm dragon / patrolling titan
NSUBST: | = 1:O / *:|, 1 = 4 = 8. / * = 1..
SUBST:  p : W., q: W., . = W..., c = cc.
MAP
c....................c
1.cccccccccccccccccc.1
.cc................cc.
..cccc.cc1cc1cc.cccc..
.....cccqqccqqccc.....
....ccqqqqccqqqqcc....
.c....GqqccccqqG....c.
.cccc1..cccccc..1cccc.
....ccccc||||cccccppc.
.cppc.......1cc..cppc.
.cppc.ccccc.cccc.1ppc.
.cpp1.cccc.ccccc.cppc.
.cppc..cc1.......cppc.
.cppccccc||||ccccc....
.cccc1..cccccc..1cccc.
.c....GqqccccqqG....c.
....ccqqqqccqqqqcc....
.....cccqqccqqccc.....
..cccc.cc1cc1cc.cccc..
.cc................cc.
1.cccccccccccccccccc.1
c....................c
ENDMAP

NAME:   storm_palace_corner_pillars
TAGS:   storm_palace_corner allow_dup no_pool_fixup
SUBST:  ; = AGG..., W : wWW
MAP
...........
.WWWW;WWWW.
.WccW.WccW.
.WccW.WccW.
.WWWW.WWWW.
.;... ...;.
.WWWW.WWWW.
.WccW.WccW.
.WccW.WccW.
.WWWW.WWWW.
..........
ENDMAP

NAME:   storm_palace_corner_reverse_hut
TAGS:   storm_palace_corner allow_dup
KMONS:  P = plant
SUBST:  ; : W., . = P.......
MAP
cccccc.....
c..........
c..ccccc...
c.cc;;;cc..
c.c;;;;;c..
c.c;; ;;c..
..c;;;;;c..
..cc;;;cc..
...cc+cc...
...........
..........
ENDMAP

NAME:   storm_palace_corner_grid
TAGS:   storm_palace_corner allow_dup
SUBST:  . = WW., c = cc., c : ccbG
MAP
...........
.c.c.c.c.c.
...........
.c.c.c.c.c.
...........
.c.c. .c.c.
...........
.c.c.c.c.c.
...........
.c.c.c.c...
..........
ENDMAP

NAME:   storm_palace_corner_altar
TAGS:   storm_palace_corner allow_dup no_pool_fixup
KFEAT:  _ = altar_qazlal / altar_fedhas w:5
SUBST:  _ = __C, t : ttG
MAP
cccccccW...
cccccccW...
ccccWWWW...
ccccW......
ccWWW......
ccW.. ..t..
ccW........
WWW....WWW.
....t..W_W.
.......WWW.
..........
ENDMAP

NAME:   storm_palace_corner_narrow
TAGS:   storm_palace_corner allow_dup no_pool_fixup
SUBST:  . = W.., ; : wWW., + : ++.
MAP
G.........+
.W........+
..WWccc...c
..WWWWcc++c
..cWWWWc;;c
..cWW Wc;;c
..ccWWcc;;c
...ccccc;;c
...+;;;;;;c
...+;;;;;;c
++cccccccc
ENDMAP

#don't allow_dup this one to avoid disconnects
NAME:   storm_palace_corner_blocked
TAGS:   storm_palace_corner no_pool_fixup
KMONS:  P = plant
SUBST:  . = P......., G = GG., W : wW
MAP
WWWW.......
WWW........
WW........c
W..G...G..c
..........c
..... ....c
..........c
...G...G..c
..........c
.........Gc
cccccccccc
ENDMAP
