###############################################################################
# spider.des: All maps and branch endings relating to the Spider.
#
# <<1>>      Entries
# <<2>>      Decorative vaults
# <<3>>      Monster-placing vaults
# <<4>>      Spider end vaults
#
###############################################################################
###############################################################################

# Functions for the spider trap and endvault loot
{{
function setup_endvault_loot(e)
  e.item("potion of ambrosia ident:all")
  e.item(dgn.loot_potions)
  e.item("hat good_item w:20 / pair of gloves good_item / " ..
         "pair of boots good_item / scarf w:40 / orb w:40")
end
}}

###############################################################################
#
# <<1>> Spider entries.
#       All must have the spider_entry tag and place an enter_spider feature.
#
###############################################################################

NAME:   dummy_spider_entry
TAGS:   spider_entry transparent
KFEAT:  O = enter_spider_nest
MAP
O
ENDMAP

###############################################################################
#
# <<2>> Decorative vaults.
#
###############################################################################

default-depth: Spider

NAME:   hangedman_spider_mandibles
TAGS:   extra transparent
# 59 inside blocks, 59 outside blocks.
NSUBST: x = 6:. / 9 = xx. / *:x
NSUBST: z = 4:. / 4:' / 9 = xxx''. / *:x
CLEAR:  '
MAP
      zzzzzzz
     zz.....zz
    zz...x...zz
    z...xxx...z
   zzx...x...xzz
  @z.x.......x.z@
 @z...xx...xx...z@
 z.....xx.xx.....z
 zx..xxx...xxx..xz
zz.x.x.......x.x.zz
z..xxx.......xxx..z
z..xx....x....xx..z
 z.x....xxx....x.z
z..x....xxx....x..z
z..xx....x....xx..z
zz..xx.......xx..zz
 zz.............zz
  zzzz..@.@..zzzz
ENDMAP

NAME: nicolae_spider_circle_web
TAGS: transparent extra
MAP
       ......x
     ...xxxxxx..
    ..xx.....xx..
   .xx......x..xx.
  .x....xxxxx....x.
 ..x...x....xx...x..
 .x...x....x..x...x.
xxx..x...xxx...x..x..
.x.xxx..x..xx...x..x.
.x..x.xxx.x..x..x..x.
.x..x..x.xxx.x..x..x.
.x..x..x..x.xxx.x..x.
.x..x...xx..x..xxx.x.
..x..x...xxx...x..xxx
 .x...x..x....x...x.
 ..x...xx....x...x..
  .x....xxxxx....x.
   .xx..x......xx.
    ..xx.....xx..
     ..xxxxxx...
       x......
ENDMAP

NAME:  nicolae_spider_webs_and_nodes
TAGS:  transparent extra
SUBST: D : .E, E : x .:5, F : .H, H : x .:5
MAP
             DDD
          ..DEEED
       .....DxxED
    ......xxxExED
 DDD...xxx...DDx..
DEEExxx.x......x..
DExxD....x.....x..
DExED....x......x..
 DDx.....xFF...xx..
 ..x....FHxHxxx.x..
 ..x....FxxxF....x..
  ..x.xxxHxHF....x..
  ..xx...FFx.....xDD
  ..x......x....DExED
   ..x.....x....DxxED
   ..x......x.xxxEEED
   ..xDD...xxx...DDD
    DExExxx......
    DExxD.....
    DEEED..
     DDD
ENDMAP

NAME:    nicolae_spider_stair_cavern
TAGS:
DEPTH:   Spider:2-
SHUFFLE: {[(
SUBST:   D = .x, . = .:100 ^:5
KFEAT:   ^ = web trap
MAP
  xxxxxxxxxx
 xxDDDDDDDDxxx
xxD.D...D.DDDxx
xD.D.DDD.D..DDxx
xDD.DDxDDDDD.DDxx
xD.DDDxxxxxDD.DDxx
xD.D.DDDDxxD.D.DDx
xDD.D...DDxDD.D..@
xxDDD.{..DxxDDDD.@
xDD.D...DDxDD.D..@
xD.D.DDDDxxD.D.DDx
xD.DDDxxxxxDD.DDxx
xDD.DDxDDDDD.DDxx
xD.D.DDD.D..DDxx
xxD.D...D.DDDxx
 xxDDDDDDDDxxx
  xxxxxxxxxx
ENDMAP

NAME:  nicolae_spider_checkerboard
TAGS:  transparent
SUBST: D = x:30 ., E = x .:30
MAP
.E.xxx.E.xxx.E.
E.ExxxE.ExxxE.E
.E.DxD.E.DxD.E.
xxD.E.DxD.E.Dxx
xxxE.ExxxE.Exxx
xxD.E.DxD.E.Dxx
.E.DxD.E.DxD.E.
E.ExxxE.ExxxE.E
.E.DxD.E.DxD.E.
xxD.E.DxD.E.Dxx
xxxE.ExxxE.Exxx
xxD.E.DxD.E.Dxx
.E.DxD.E.DxD.E.
E.ExxxE.ExxxE.E
.E.xxx.E.xxx.E.
ENDMAP

NAME:    nicolae_spider_spiderdrome
TAGS:    transparent
SHUFFLE: pqrsz/PQRSZ/FHJay/KLMNO
SUBST:   przs = ., q = x., PQKLFH = x, RMJD = xw, EZOy = w., SNa_ = x_
CLEAR:   _
MAP
__________sss__________
_xxxxxxxxxqpqxxxxxxxxx_
_xDDDDDDDDDrDDDDDDDDDx_
_xDEEEEEEEEzEEEEEEEEDx_
_xDE...............EDx_
_xDE...............EDx_
_xDE..EEEEEEEEEEE..EDx_
_xDE..EDDDDDDDDDE..EDQS
_xDE..EDxxxxxxxDE..ZRPS
_xDE..EDDDDDDDDDE..EDQS
_xDE..EEEEEEEEEEE..EDx_
_xDE...............EDx_
_xDE...............EDx_
_xDOEEEEEEEEEEEEEyEEDx_
_LMDDDDDDDDDDDDDDJDDDx_
NKLxxxxxxxxxxxxxHFHxxx_
NN______________aaa____
ENDMAP

NAME:  nicolae_spider_twisty_arc_passages
TAGS:  transparent
SUBST: D = x..
MAP
     ......   ........
    D...DD....D...DD......
   xxD.DxxD.DDxD.DxxDD.DDD.
  xxD..DxD.DxxxD..Dxxx.DxD.
 .DD..DxxD.DxDD....DDxDxxD.
 ...DDxxD..xxD..DDD.DxxxD..
 .DDxxxD..DxD..DxxxD.DxD..
.DxxxD....DxD.DxxDD..DxD.D
..DDD.xxD.DxD.DxD.....D..D
..DD..DxxDDxD..DD..DDDD...
.DxxD..DxxDxxD.DxDDxxxxDD..
 .DD..D.DD.xD..DxxxxDDDDxD.
 .DD.D.....D.DD.DxxD....D..
.DxxD..DDD.DDxxD.DxD.D..D..
..DxxDDxxxDxxxD........DxD.
.DxxxxxxDxxxDD.D..DDDDDxxD.
..DDxxD.DxD......DxxxxxxxD.
....DxxD.xxD.D..DxxDDDDDD.
.DDD.DxD.DxD..DDxxD....Dx
.DxD.DxD..D..DxxxD...Dxxx
 DxD..D.D..D..DDD..D..D
     .. ....  .......
ENDMAP

###############################################################################
#
# <<3>> Monster-placing vaults.
#
###############################################################################

# Misc minivaults
NAME:   guppyfry_spider_mini_1
MAP
x@.xxxxxxxx.
xx..xxxxx..@
xxx..xxx..xx
.xxx..x..xx.
@..xx...xx.@
xx.........x
xxx........@
xx..x...x.xx
x..xx..xx..x
@.xxx..xxx.@
xxxx..xxxxx.
xxx.@xxxxxxx
ENDMAP

NAME:   guppyfry_spider_mini_2
MAP
xx..xxxx..xx
x....xx....x
xx..xxxx..xx
xxx..xx..xxx
xWWxxWWxxWWx
@WWWWWWWWWW@
xWWxxWWxxWWx
xxx..xx..xxx
xx..xxxx..xx
x....xx....x
xx..xxxx..xx
ENDMAP

NAME:   guppyfry_spider_mini_3
MAP
W....WW....W
xW...WW...Wx
.xWWWWWWWWx.
.WxW.WW.WxW.
.W..WWWW..W.
WWWW....WWWW
.W..WWWW..W.
.WxW.WW.WxW.
.xWWWWWWWWx.
xW...WW...Wx
W....WW....W
ENDMAP

NAME:  nicolae_spider_jagged_tunnels
TAGS:  transparent
SUBST: D = x:75 .:20
MAP
..xxxxxxxxxx
...DDDDDDDDx
x.........Dx
xD..DDDDD.Dxxxx
xD.D.DxxD.DDDDx
xD.DDDxDD....Dx
xD.DxxxD.DDD.Dxxxx
xD.DxDDDDDxD.DDDDx
xD.DDD.DxxDD....Dx
xD....DDxxD.DDD.Dx
xDDDD.DxDDDDDxD.Dx
xxxxD.DDD.DxxxD.Dx
   xD....DDxDDD.Dx
   xDDDD.DxxD.D.Dx
   xxxxD.DDDDD..Dx
      xD.........x
      xDDDDDDDD...
      xxxxxxxxxx..
ENDMAP

NAME:  nicolae_spider_nooks_and_crannies
TAGS:  transparent
SUBST: D = x.
MAP
   ...........
  ...DDD.D.....
 ..DxDxDx.xD.D..
..Dxxxxxx.xxx...
.xxx..xxD..xxD...
...x..xxxx.DxxD..
.xxxx.x....xx....
.Dxx..x..xxxxxD..
.Dx..xxxxxD..xDD.
....xx..x....x.D.
.Dxxx...x.xxxx.D.
..DxD.xxx.....D..
..Dxx...xxxxxD..
 ..Dxxx.xDxDD..
  ..DDD.D.D...
   ..........
ENDMAP

NAME:   hellmonk_test_site
TAGS:   no_monster_gen no_trap_gen ruin transparent
SUBST:  ' = x:4 .
SUBST:  G = G..
FTILE:  x.G1 = floor_sand
TILE:   x = wall_sandstone
TILE:   G = dngn_crumbled_column
MAP
     @
  xxx.xxx
 xxx...xxx
 xG.....Gx
 x.'''''.x
 x.'xxx'.x
 x.'xxx'.x
 x.'''''.x
 xG.....Gx
 xxx...xxx
  xxx.xxx
     @
ENDMAP

###############################################################################
#
# <<4>> Spider ending vaults
#
###############################################################################

default-depth:

NAME:   spider_rune_water
PLACE:  Spider:$
WEIGHT: 20
ORIENT: northwest
KITEM:  O = potion of ambrosia ident:type, gossamer rune of Zot
MONS:   ghost moth, hornet, emperor scorpion / w:5 spark wasp
MONS:   wolf spider / redback / tarantella / jumping spider / culicivora
MONS:   redback
MONS:   orb spider
MONS:   moth of wrath / ghost moth / sun moth
NSUBST: 1 = 2:1 / 1:41 / *:4
SUBST:  4 = 444.., 5 = 55..., 6 = 6.
COLOUR: c = white
COLOUR: 18O+'def = lightgrey
FTILE:  18cO+'def = floor_pebble_lightgray
NSUBST: ' = 3:3 / 4:2 / 4=32'' / 2:d / 4 = dee / 4 = ff' / *:'
TILE:   c = wall_brick_gray
SUBST:  x : cccx
SUBST:  X = x
: if crawl.coinflip() then
SUBST:  W = WW.
: else
SUBST:  W = W..
: end
KFEAT:  Ww = shallow_water
SUBST:  = : .+
: setup_endvault_loot(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxX
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxX
xxxxxxxxxxxxxxxxxxxxxxWWWxxxxxxxxxxxxxxxxxxxxxX
xxxxxxxxxxxwwxxxxxxx..5.WxxxxxxxxxxxxxxxxxxxxxX
xxxxxxxxx144wwwxWW6.W..W.WWWWxxxxxxxxxxxxxxxxxX
xxxxxxxxxx44wwwW..WxxW.xx.56.WWxxxxxxxxxxxxxxxX
xxxxxxxxxxx4xxW..Wxxx.WxxWWxW..WxwwwxxxxxxxxxxX
xxxxxxxxxxxxxW.5WxxxxW..xxxxxW..Wwwwww41xxxxxxX
xxxxxxxxxxxxxW..Wxcccc..ccccxW.5.Wxxw444xxxxxxX
xxxxxxxxxxxxxW.Wxxc1'c++c'7cxxWW..Wxxxx4xxxxxxX
xxxxxxxxxxxxW.Wxxxc'''cc'''cxxxxW.WxxxxxxxxxxxX
xxxxxxxxxxxW5Wxxxxc''''''''ccccxxW.WWxxxxxxxxxX
xxxxxxxxxxxW.Wx44xc''''''''+'OcxxWW5.WxxxxxxxxX
xxxxxxxxxxx.Wwww4xcccccccccccccxxxwW..WxxxxxxxX
xxxxxxxxxWW.Wwww4xxxxxxxxxxxxxx44wwwW.WxxxxxxxX
xxxxxxxxW..Wxx41xxxWWWWxxWWWxxx444xxxW.WWxxxxxX
xxxxxxxxW5.WxxxxxWW...4WW4..WWxx1xxxxxW..WxxxxX
xxxxxxxxxW.WxxxxxW.WxW44444W..WxxxxxxxW5.WxxxxX
xxxxxxxxW..WxxxW.5WxxW.4.WxxW5WxxxxxW...WxxxxxX
xxxxxxxxxW..WxW..WxxxxW..WxxW..WxxW..WWWxxxxxxX
xxxxxxxxxxW.5.W..WxxxxxW...WxxW..W..WWxxxxxxXXX
xxxxxxxxxxxWW...Wxxxxxxx...xxxxWW5WWxxxxxXXXX
xxxxxxxxxxxxxWWWxxxxxxxx===xxxxxxWxxxxxXXX
xxxxxxxxxxxxxxxxxxxxxxxx+++xxxxxxxxxxxXX
XXXXXXXXXXXXXXXXXXXXX66.....66XXXXXXXXX
XXXXXXXXXXXXXXXXXX@@@@@@@@@@@@@@@XXXXXX
ENDMAP

NAME:   grunt_spider_rune_circles
TAGS:   no_pool_fixup
WEIGHT: 20
PLACE:  Spider:$
ORIENT: float
KITEM:  O = gossamer rune of Zot
MONS:   ghost moth, moth of wrath / ghost moth, emperor scorpion
MONS:   wolf spider / redback / tarantella / jumping spider / culicivora
MONS:   orb spider / steelbarb worm, redback
NSUBST: 4 = 3:3 / * = 44.
NSUBST: 7 = 3 = 111.. / 1:2
NSUBST: d = 3:d / 3:e / *:f
SUBST:  5 = 5., 6 = 66...
SUBST:  W = W..
SUBST:  - = ....WWW^
KFEAT:  ^ = web trap
TILE:   c = wall_brick_gray
: setup_endvault_loot(_G)
MAP
    4 xxx        xx       x
  ..4@xcxxxxxxxxxxxxxxx xxx xx
 W4W.4Wcccxxccccxxccccxxxcxxxxx
 @..4W6W6ccccWWccccWWcccccccccxxxx
W54.x6W6WcWWW.4..........444.ccccxx
wW.xcccW666....W5.W....WWWWW..cccxx
 4Wxcc66..wwwwcccccccc..444....cccxx
@WxxcW-..wwcccc--575dccccc..-.-.cccxx
  xxcW-4.wcc-d575dwww4d--ccc.-..cccxx
  WxcW-..5cd4wwwww3Oww474-3c..W5cccxxx
  xxccW.4.c-74.wwwwwww4ddccc6-.Wcccxx
   xxccW4Wcccc...4.dd4---6-6-6ccccxx
    xxcW45..-cccc-3cc-66-6-ccccxxxx
     xcccW-...44cccc6-6-6-ccxxxxxx
     xxxcccccc4.....-6-ccccxx  xx
       xxxxxxccc-----cccxxxx
            xxxcccccccxxx
              xxxxxxxxx
                   xxx
ENDMAP

NAME:   guppyfry_spider_rune
PLACE:  Spider:$
TAGS:   no_pool_fixup
ORIENT: southwest
KITEM:  O = gossamer rune of zot
KMONS:  O = ghost moth
KITEM:  * = potion of ambrosia ident:type
KMONS:  * = emperor scorpion
MONS:   ghost moth / sun moth, ghost moth / moth of wrath
MONS:   demonic crawler / steelbarb worm / wolf spider
MONS:   w:2 broodmother / w:4 emperor scorpion / w:1 spark wasp
MONS:   tarantella / wolf spider / jumping spider / culicivora
MONS:   redback
MONS:   orb spider
NSUBST: 8 = 2:c / *:7, " = 1:. / 1:c. / *:c
NSUBST: d = 1:e / * = def
SUBST:  3 = 3., 4 = 4., 5 = 55., 6 = 66..., 7 = 777.
SUBST:  - = .....WWW^
KFEAT:  ^ = web trap
SHUFFLE: O12*
TILE:   C = wall_brick_gray
SUBST:  C = c
: setup_endvault_loot(_G)
MAP
xxxxxxxxxxxxxxxxxxx@@@@@@@@@@xxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx......xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx....xxxxxxxxxxxxxxxxx
xxxxxxxxxxccccccccccccc..cccccccccccccxxxxxx
xxxxxxxxxxcccccccccccc-..-ccccccccccccxxxxxxx
xxxxxxxxxxcdWccccccccc-x.-cccccccccWdcxxxxxxxx
xxxxxxxxxxc66.c..4CCCcx..xcCCC4..c.66cxxxxxxxx
xxxxxxxxxxcW.-..c.45Oc6..6c154.c..-.Wcxxxxxxxx
xxxxxxxxxxcc..-c"c55Ccx..xcC55c"c-..ccxxxxxxxx
xxxxxxxxxxcccW.-ccCCCc-..-cCCCcc-.Wcccxxxxxxxx
xxxxxxxxxxcccc..-cccccx.-xccccc-..ccccxxxxxxxx
xxxxxxxxxxccccxW.-c8cc--.-cc8c-.Wxccccxxxxxxxx
xxxxxxxxxxcccc-.-xx--x-xx.x--xx-.-ccccxxxxxxxx
xxxxxxxxxxccc--3-x--.--55--.--x-3--cccxxxxxxxx
xxxxxxxxxxccc.333.-.-.5445.-.-.333.cccxxxxxxxx
xxxxxxxxxxccc--3-x--.--55--.--x-3--cccxxxxxxxx
xxxxxxxxxxcccc-.-xx--x.xx-x--xx-.-ccccxxxxxxxx
xxxxxxxxxxccccxW.-c8cc-.--cc8c-.Wxccccxxxxxxxx
xxxxxxxxxxcccc..-cccccx-.xccccc-..ccccxxxxxxxx
xxxxxxxxxxcccW.-ccCCCc-4.4cCCCcc-.Wcccxxxxxxxx
xxxxxxxxxxcc..-c"c55Ccx-.xcC55c"c-..ccxxxxxxxx
xxxxxxxxxxcW.-..c.45*c7--7c254.c..-.Wcxxxxxxxx
xxxxxxxxxxc66.c..4CCCccffccCCC4..c.66cxxxxxxxx
xxxxxxxxxxcdWccccccccccccccccccccccWdcxxxxxxxx
xxxxxxxxxxccccccccccccccccccccccccccccxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   grunt_spider_rune_parallel
PLACE:  Spider:$
ORIENT: east
NSUBST: O = 1:O / *:d
KITEM:  O = gossamer rune of Zot
MONS:   ghost moth, moth of wrath
MONS:   emperor scorpion / w:5 spark wasp / w:5 radroach / w:5 broodmother
MONS:   wolf spider / redback / tarantella / jumping spider / culicivora
MONS:   meliai / steelbarb worm / orb spider, redback
NSUBST: 1 = 2:1 / 1:1.. / *:4
NSUBST: d = 3:d / 3:e / *:f
SUBST:  2 = 12, 4 = 44..., 5 = 55. " = ^^...6, - = .....w^^
SUBST:  6 = 6.
KFEAT:  ^ = web trap
SUBST:  C : cx
TILE:   c = wall_brick_gray
KMASK:  w = no_monster_gen
: setup_endvault_loot(_G)
MAP
                      xxxxxxxxxxx
              xxxxxxxxxxxxxxxxxxxxxxxx
   xxxxxxxxxxxxxxxxxxxCCCCCCCCCCxxxxxxxx
xxxxxxxxxxxxxxxCCCCCCCC--------CCCCCcxxxxxx
xxxxxCCCCCCCCCCC--C-----...4..------cccccxxxxx
CCCCCC-----------.C......5.4.4.4...----cccxxxxx
-------...........C.""""...1.4.4...."""---cccxxxx
."""........4.4...CCP"""..5.4.4.4.."""...---ccxxxx
@"""5......4.4.....C"""".-------.4.""".....--ccxxx
@..........4..4....CC..---ccccc-.----...3.4.--ccxx
@-------------......CC--ccc---cc--cc-..4..4..--cxx
@-ccccccccccc--------cccc---.--cc----.4..44.dd-cxx
@--------------cccccccc---.4.4----.......4..dOdcxx
@"".......4...----------....4.4........3.4.412Ocxx
@""5.....4.4....""""".5x.4.4.4.5.........4..dO-cxx
@"".....4.4.....""""".1...4...4...----4..44.dd-cxx
@---------.4..."""""..5x...4-------cc-.4..4.--ccxx
@-ccccccc-----..""""".-------ccccc----..3..--ccxxx
@-------ccccc---------cccccccc-----."""""---ccxxx
@"""5..--------ccccccccc-------4....."""--cccxxxx
.""".....4....--------C--..4.4.4....."--ccxxxxxx
------....4.4.4......CC...5..4.4.....---ccxxxxxx
CCCCC-----.4.4......CC""""..4.4....---cccxxxxxxx
xxxxCCCCC-----.....CCP"""".1.4.4..--CCCxxxxxxxx
xxxxxxxxCCCCC---...C.."""".......--CCxxxxxxxxx
 xxxxxxxxxxxCCC---.C---....-------CCxxxxxxxxx
    xxxxxxxxxxCCCCCCC-------CCCCCCCxxxxxxx
         xxxxxxxxxxxCCCCCCCCCxxxxxxxxx
             xxxxxxxxxxxxxxxxxxxxxxx
                xxxxxxxxxxxxxxxxx
ENDMAP

NAME:    grunt_spider_rune_island
TAGS:    no_monster_gen
PLACE:   Spider:$
ORIENT:  west
NSUBST:  d = 1:O / 3:d / 3:e / *:f
KITEM:   O = gossamer rune of Zot
MONS:    ghost moth, moth of wrath, sun moth
MONS:    emperor scorpion / w:5 spark wasp / w:5 jorogumo
MONS:    orb spider
MONS:    wolf spider / redback / tarantella / culicivora / jumping spider
MONS:    demonic crawler / steelbarb worm / torpor snail / meliai
NSUBST:  h = 2:9 / 2 = 4. / *= 6667....
NSUBST:  9 = 3:13 / 1:2 / * = 6..
SUBST:   5 = 5.., g = 6667....
SUBST:   0 = 667...
SUBST:   + = ^^^+
SUBST:   " = ...xxW^
SUBST:   - = ...cWW^^^
SUBST:   . = .:20 x:2 ^:1
KFEAT:   ^ = web trap
KFEAT:   _ = .
SUBST:   c : cx
SUBST:   C : c
TILE:    c = wall_brick_gray
: setup_endvault_loot(_G)
MAP
  xxxxc--"""""@"""x""@5""""""--ccc
 xxxccc-.""".....g.g...."""...--"c
 xxxc--...""".....g....."".....-""
xxccc-...5.""...5......."".....-"-
xcc---......""...g.g..."".5....g"-
xc--h........WWWWWWWWWWW"....g.-"-
xcc..h....5.WWwwwwwwwwwWWW....5-"-
xcc...h.h..WWwwwwwwwwwwwwWW..g.-"@
xcc....h..WWwwwCCCCCCwwwwwW...g-"-
xcch.5...hWwwwCC----CCCwwwW...g-"-
xc-......_WwwCC--0.---CwwwW....-"-
xc-..h.._-WwwC--409__-CCwwW..0.-"-
xc-.h.h_-"-"wC-0ddd0_--CwwW.5..-"-
xc-..._-"---_+49dddd9-CCwwW...g-"-
xc-.h.h_-"-"wC-0ddd0--CwwwW...g-"-
xc-..h.._"WwwC--409--CCwwwW..g.-"@
xcc..5..._WwwCC--0_-CCwwwWW....-"-
xcc.......WwwwCC----CwwwwW....5-"-
xcc......hWwwwwCCCCCCwwwWW...g.-"-
xcc.......WWwwwwwwwwwwwWW......g"-
xc-h....h...WwwwwwwwwwWW.......-"-
xc-.5..h....WWWWWWWWWWW........-"-
xc--..h......".5g.g..."....5...-""
xcc--h......""........"".......-"c
xxcc-.h.....""...g...."".......-"c
 xxc"""""5""""""g"g""5""""""""""cc
  xc----@-"-----x------""-@----ccx
ENDMAP

##########################################
#Arachne Lair Rune Vault
NAME:   johnstein_spider_rune_arachne_lair
TAGS:   no_monster_gen
KMONS:  j = emperor scorpion / spark wasp w:5 / jorogumo
KMONS:  m = moth of wrath / ghost moth w:40 / sun moth
KMONS:  o = orb spider w:40 / demonic crawler
KMONS:  s = torpor snail / entropy weaver
ORIENT: float
PLACE:  Spider:$
# '"-_!?^
# ABCDEFG
#pick 1 of 3 different flavors
: if crawl.one_chance_in(3) then
    SUBST:  "-? = .
    SUBST:  '_!^ = Y
    FTILE:  Y = FLOOR_HALL
    COLOUR: Y = blue
    KMONS:  Z = Arachne, jorogumo
    KMONS:  1 = tarantella / wolf spider / redback w:20 / jumping spider w:30 /\
                culicivora
    KMONS:  2 = torpor snail / pharaoh ant
: elseif crawl.one_chance_in(2) then
    SUBST:  '-! = .
    SUBST:  "_?^ = Y
    FTILE:  Y = FLOOR_ROUGH_RED
    COLOUR: Y = red
    KMONS:  Z = Arachne, jorogumo
    KMONS:  1 = redback w:20 / wolf spider / tarantella w:30 / jumping spider /\
                culicivora
    KMONS:  2 = entropy weaver / steelbarb worm
: else
    SUBST:  '"_ = .
    SUBST:  -!?^ = Y
    FTILE:  Y = FLOOR_SANDSTONE
    COLOUR: Y = brown
    KMONS:  Z = Arachne, jorogumo
    KMONS:  1 = tarantella / wolf spider w:30 / redback w:20 / jumping spider /\
                culicivora
    KMONS:  2 = meliai / spark wasp w:5
: end

NSUBST: 3 = 4:j / 1:s / *=2.
NSUBST: 2 = 6=o. / 2:m / 6=2. / *=1

KITEM:  O = gossamer rune of Zot
NSUBST: O = 1:O / *=%

#thin out monsters and set up loot
NSUBST: % = 2:d / 2:e / 2:f / *=def...
SUBST: 1 = 1..

KFEAT: Y = web / floor w:40
: setup_endvault_loot(_G)
MAP
    .......@@@@@.......
   ..cccccco...occcccc..
   .cc.Occcc+++ccccO.cc.
   cc.m.c..2.2.2..c.m.cc
  cc..3cc.2.2Z2.2.cc3..cc
 xc...cc.2'''''''2.cc...cx
 xc.2cc..1c''!''c1..cc2.cx
 xc..c.'1ccc!!!ccc1'.c..cx
xxc.1.'''1c--!--c1'''.1.cxx
xcc.1''''_"?????"_''''1.ccx
xc.1..'''"???^???"'''..1.cx
xc..c..'1c??^^^??c1'..c..cx
xc.2c..1ccc?^^^?ccc1..c2.cx
xc1.cc..1c'!^^^!'c1..cc.1cx
xc.3Oc..1''!^^^!''1..cO3.cx
xc1.cc..1c'!^^^!'c1..cc.1cx
xc.2c..1ccc?^^^?ccc1..c2.cx
xc..c..'1c"?^^^?"c1'..c..cx
xc....'''"""?^?"""'''....cx
xcc.1''''_""???""_''''1.ccx
 xcc.1'''1c1-!-1c1'''1.ccx
 xcc.1.'1ccc'!'ccc1'.1.ccx
 xcc..c..1c'''''c1..c..ccx
 xcc..cc.2'''''''2.cc..ccx
 xc2.ccc...2.Z.2...ccc.2cx
 xc..cccc1..222..1cccc..cx
 xc..cccc.1..2..1.cccc..cx
 xc2.ccc...........ccc.2cx
  cc..c1.1cc+++cc1.1c..cc
   c..cc1.cc.1.cc.1cc..c
   cc1.cc..c111c..cc.1cc
    cc.3cc%c.1.c%cc3.cc
     cc.mccc...cccm.cc
     .cc.Occo.occO.cc.
     ..ccccc...ccccc..
      .....@@@@@.....
ENDMAP

#Tomb flavoring, death scarabs
NAME:   floodkiller_spider_rune_tomb
PLACE:  Spider:$
TAGS:   no_monster_gen
ORIENT: east
NSUBST: N = 4:W / c
TILE:   M = wall_tomb
COLOUR: M = yellow
KFEAT:  M = stone_wall
KFEAT:  P = clear_rock_wall
TILE:   c = wall_sandstone
TILE:   P = dngn_sarcophagus_pedestal_left
FTILE:  23+$OEGHNPS. = floor_sandstone:20 / none
: setup_endvault_loot(_G)
ITEM:   gold good_item
KITEM:  O = potion of ambrosia ident:type, gossamer rune of Zot
MONS:   death scarab
MONS:   wolf spider spectre / wolf spider zombie w:5 / demonic crawler w:5 \
        / emperor scorpion spectre w:2 / pharaoh ant w:3 \
        / broodmother zombie w:3 / jorogumo w:2 / jorogumo spectre w:3
MONS:   jumping spider spectre w:20 / tarantella spectre w:20 \
        / redback spectre w:20 / culicivora spectre / jumping spider zombie \
        / tarantella zombie / redback zombie / culicivora zombie w:5
MONS:   jumping spider / tarantella / redback / wolf spider w:5 \
        / steelbarb worm w:5 / orb spider w:3 / spark wasp w:2 \
        / pharaoh ant w:5 / culicivora
MONS:   emperor scorpion / jorogumo w:5, ghost moth, moth of wrath
NSUBST: S = 4:1 / 6 = 1. /*:., H = 2:5 / 1:6 / 1:7 / K
SUBST:  K = 2 5:4 6:2 7:2 E:20, E = 3 4:30 .:20
SUBST:  Q = W:15 .
NSUBST: d = 2:d / 2:e / 3:f / *=gggge
: set_feature_name("clear_rock_wall", "sarcophagus")
MAP
  .....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  .G.G.cccccccccccccccccccccccccccccccx
  .....c.....WWWWWWWW..HE...x..WWH.x.cx
  .....c..x...WWxWW..........WWWW....cx
  .G.G.cWWW....WWW.x.E.x....WWWxW....cx
  .....cWWWW..E...H......WWWWWWW....Ecx
  .....cWWWWx...E......WWWxWWW......xcx
  .G.G.c.WW..ccccccccccccccccccc.....cx
  .....cE....c....x............c...E.cx
@......cx....cx...........H.xH.c....Hcx
@..G.G.cE....c........x...EE..Ec.x...cx
@......c..x..c..HMMMMMMMMMM.E.Wc....Ecx
@E.ccc.c.....c...MS..2SdSdM..WWcWW...cx
@QccccccW....c...M.3S3.SMSMxWWWNWWW..cx
@QWcccccWx..Ec.E.+..3..dMdMNNNNNWWx..cx
@QWWWccWWW...c...+.S.SdSMdMWWWWWWWWE.cx
@QWWWWWWWW..Ec..H+..S3.dPOM..WWWWWW..cx
@QWccWWWWW..Hc.E.+.S.SdSMdM.WWWWWWWW.cx
@QcccccWWW..Ecx..+..3..dMdMNNNNNWxWW.cx
@.ccccccWW...c...M.3S3.SMSMWWWWNWWWWEcx
@E.cc.ccW.x.Ec.E.MS..2SdSdM..WWcWWW..cx
@......c..W..c..HMMMMMMMMMM.E.Wc.WW.xcx
@..G.G.c.....c.......x....EE...c.WWE.cx
@......c.....c.x..........H.HxEc.WW.Hcx
  .....cx....c...........x.....c.WW..cx
  .G.G.c.....ccccccccccccccccccc.xWW.cx
  .....c........WxWWWW.x......x...WWEcx
  .....cExE..xWWWWWWWWW...........WWWcx
  .G.G.c...WWWWW...WWWWWWWW.x.....WxWcx
  .....cWWWWWWW...Hx.EWWWxWW....H..WWcx
  .....cWWWx..E.E......HEWWWW......WWcx
  .G.G.cccccccccccccccccccccccccccccccx
  .....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# Broodmother-heavy map
NAME:   hellmonk_spider_rune_endless_swarm
PLACE:  Spider:$
ORIENT: southeast
MONS:   wolf spider / jumping spider / redback / tarantella / culicivora
MONS:   orb spider
MONS:   steelbarb worm / torpor snail / meliai / pharaoh ant / entropy weaver
MONS:   emperor scorpion w:30 / ghost moth w:20 / moth of wrath / sun moth
MONS:   jorogumo w:15 / radroach / spark wasp w:5
MONS:   broodmother
KITEM:  O = potion of ambrosia ident:type, gossamer rune of Zot
KFEAT:  ^ = web
NSUBST: . = 96 = 1... / *:., 2 = 2:2 / *=2...
NSUBST: 6 = 4:6 / 4 = 456. / *= 4..
NSUBST: d = 1:O / 3:d / 3:e / 3:f
SUBST:  3 = 3....
SUBST:  ; : ;;;;;c
SUBST:  : = ...xxW^, - = ....W^, Q : xWW, R = ...W^, ; = ...^
TILE:   c = wall_brick_gray
: setup_endvault_loot(_G)
MAP
            xxxxxxxxx
           xxxxxxxxxxxxx
         @xxxx:::::::xxxxx
        @2'''--x.x--.:::xxxx
       @'''.-xxx.xxx-..2:xxxx
      @'''..-xx...xx-....::xxx
     @'''...xx..3..xx......:xxx
    @'''........63..........:xxx
   @'''.....xx..3..xx........:xx
  x2'''.....-xx...xx-.........:xx
  xx''......-xxx.xxx-........2:xx
 xx'--x.x--..--x.x--...........:xx
 xx-xxx.xxx-.........3..--x.x--:xx
 x:-xx...xx-...........-xxx.xxx-:xx
 x:xx.....xx...........-x.....x-:xx
 x:...363..............xx..3..xx:xx
xx:xx..3..xx..--x.x--.....363..x-:xx
xx:-xx...xx-.-xxx.xxx-.xx..3..xx-:xx
xx:-xxx.xxx-.-xx....x-.-x.....x-.:xx
xx:.--x.x--..xx..3..xx.-xxx.xxx-2:xx
xx:.............363.....--x.x--..:xx
xx:...RR.....xx..3..xx...........:xx
xx:..RQQR....-xx...xx-....3.....:xxx
xx:.RQQQQR...-xxx.xxx-........6.:xxx
xx:..RQQR.....--x.x--...........:xxx
xx:...RR...3...........ccc+ccc..:xxx
 xx:.6...............ccc.....ccc:xx
 xx:.....--x.x--.....c6........c:xx
 xxx:...-xxx.xxx-...cc...;;;..3ccxx
  xx:2..-xx...xx-...c...;;c;;.6dcx
  xxx:..xx.....xx...+..;;ccc;;6dcx
   xx:.....363......c...;;c;;33dc
   xxx:.xx..3..xx...cc...;;;36dcc
    xx:.-xx...xx-....c3...633ddc
    xxx:-xxx.xxx-....ccc3ddddccc
     xxx:--x.x--..6....cccccccx
      xx::...........2..:xxxxx
       xxx::.2......::::xxxxx
        xxxx:::..:::xxxxxxxx
          xxxxx::xxxxxxxxx
            xxxxxxxxxxxx
ENDMAP

# A spider-free end vault. Lower density to compensate for tougher monsters.
# No webs (because there are no spiders, you see)
NAME:   hellmonk_spider_rune_bug_friends
PLACE:  Spider:$
ORIENT: float
TAGS:   no_monster_gen no_trap_gen
MONS:   boulder beetle / meliai
MONS:   steelbarb worm w:15 / demonic crawler
MONS:   pharaoh ant / entropy weaver / torpor snail
MONS:   ghost moth w:15 / sun moth / moth of wrath w:5
MONS:   emperor scorpion
MONS:   radroach / spark wasp
KITEM:  O = potion of ambrosia ident:type, gossamer rune of Zot
NSUBST: 7 = 4:7 / *=7..
# bias the big monsters, randomly
: if crawl.one_chance_in(3) then
    SUBST:  7 = 44456
: elseif crawl.one_chance_in(2) then
    SUBST:  7 = 45556
: else
    SUBST:  7 = 45666
: end
NSUBST: . = 16 = 3... / 16 = 2... / 16 = 1... / *:.
NSUBST: ' = 12 = 1... / 16 = 2... / 1:3 / 1:3. / *:.
# bias the ruination level of the terrain
: if crawl.one_chance_in(3) then
    SUBST:  A = cc.
: elseif crawl.one_chance_in(2) then
    SUBST:  A = c.
: else
    SUBST:  A = c..
: end
NSUBST: d = 3:d / 3:e / *:f
TILE:   c = wall_brick_gray
: setup_endvault_loot(_G)
MAP
        cccc     cccc
       ccddcccccccddcc
       cd7..ccOcc..7dc
       cc..AcdddcA..cc
        cc..cc7cc..cc
         c....7....c
        cc....A....cc  cccc
   cccccc7...AcA...7cccc..ccc
  cc....A..AcccccA..AA......cc
  c....cc..Ac.7.cA..AAcA....cc
  cc..cc..cc.....cc..Ac...ccc
   c.AA....A.....A......ccc
  cc7..................7cc
 cc...cA....A'''A....cc..cc
cc...Accc.ccc'''ccc.AA....cc
ccA..AcccAcA'''''Accc....cc
 cc..ccA'''''''''''Acc.ccc
  c...cA'''''''''''''ccc
  c.ccc''''A'''''A''''Ac
  cccc''''Acc'''ccA''''c
     c''''Acc@@@ccA''''c
     cc''ccc     ccA''cc
      cc'cc       cc'cc
       ccc         ccc
ENDMAP
