###############################################################################
# swamp.des: All maps and branch endings relating to the Swamp.
#
# <<1>>      Entries
# <<2>>      Decorative vaults
# <<3>>      Monster-placing vaults
# <<4>>      Swamp end vaults
#
###############################################################################
###############################################################################

: crawl_require("dlua/dungeon.lua")

# Functionality for one of Grunt's vaults.
{{
function callback.grunt_lady_of_the_lake_trigger(data, triggerable, triggerer,
                                                 marker, ev)
  if data.triggered then
    return
  end

  local b = dgn.find_marker_positions_by_prop("merfolk_avatar_spawn", 1)[1]

  if dgn.create_monster(b.x, b.y, "generate_awake merfolk avatar") then

    data.triggered = true
    if you.see_cell(b.x, b.y) then
      crawl.mpr("A figure emerges from the depths of the water!", "warning");
    else
      crawl.mpr("The water ripples visibly.", "visual");
    end
  else
    crawl.mpr("The water ripples visibly.", "visual");
  end
end
}}

{{
function swamp_ruins(e)
    e.tags('serial_swamp_ruin extra transparent unrand')
    e.depth('Swamp')
    e.weight('6')
    e.tile('x = wall_brick_vines')
    e.ftile('- = floor_sandstone')
    e.subst('- = .')
end
}}

###############################################################################
#
# <<1>> Swamp entries.
#       All must have the swamp_entry tag and place an enter_swamp feature.
#
###############################################################################

# This is basically the dummy balancer.
NAME:   swamp_mist
TAGS:   swamp_entry transparent
KFEAT:  O = enter_swamp
MARKER: O = lua:fog_machine { \
             pow_min = 8, pow_max = 12, delay = 25, \
             size = 2, walk_dist = 1, spread_rate= 33 }
MAP
O
ENDMAP

# Scenery, no threat.
NAME:   swamp_mist_pool
TAGS:   swamp_entry no_pool_fixup transparent
KFEAT:  O = enter_swamp
SHUFFLE: XYZ
SUBST: Y : YX, Z : ZX, X = W, Y = w, Z = w
MARKER: O = lua:fog_machine { \
             pow_min = 8, pow_max = 12, delay = 25, \
             size = 2, walk_dist = 1, spread_rate= 33 }
SUBST:  '= t.., "= tw, t=ttw, A=Ww
MAP
       @
   't"Ywt@
  't"wYwXt'
 't"wAYXw"t'
 t"wwAOAZw"t
 't"wAAZw"t'
  't"wwZ"t'
   't"wt@
ENDMAP

###############################################################################
#
# <<2>> Decorative vaults.
#
###############################################################################

default-depth: Swamp

###############################################################################
# A series of pools by Jude.
NAME:   swamp_pool1
TAGS:   no_pool_fixup allow_dup extra luniq_pool
WEIGHT: 1000
{{ smear_map({iterations=5, smear='w', onto='wwt"', boxy=false}) }}
SUBST:  "=ttw
MAP
     ""
   "www"
  "wwwww"
  wwwwwww"
  "wwwww"
   "www"
    "w"
ENDMAP

NAME:   swamp_pool2
TAGS:   no_pool_fixup allow_dup extra luniq_pool
WEIGHT: 500
SUBST:  "=ttttw
MAP
     ""
   "www"
  "wwwww"
  wwwwwww"
  "wwwww"
   "www"
    "w"
ENDMAP

NAME:   swamp_pool3
TAGS:   no_pool_fixup allow_dup extra luniq_pool no_exits
WEIGHT: 500
SUBST:  '=Wt, "=wwt
MAP
"'ttt'
'""w"tt
twwww"t
'""wwtt
 ttw"t'
  'tt'
ENDMAP

NAME:   swamp_pool4
TAGS:   no_pool_fixup allow_dup extra luniq_pool
WEIGHT: 500
SUBST:  "=ttttw
MAP
  t""tt
 ""www"t
"twwwwwtt
 wwwww"tt
"twww"tt"
 ttwww"tt
  ttw"tt
ENDMAP

NAME:   swamp_pool5
TAGS:   no_pool_fixup allow_dup extra luniq_pool no_exits
KMONS:  1 = patrolling swamp drake
KFEAT:  1 = deep_water
WEIGHT: 500
SUBST:  "=ttttw, '=ttW
MAP
  't't'
 tt"""t'
 t"w1w"tt
 ww1ww"t'
 t"w1t"t
 tt"w"ttt
  tt"t'
ENDMAP

###############################################################################
# Other Swamp decorative vaults

# Swamp's layout is chaotic and samey; fill it with decorative vaults.
NAME:   swamp_ruins_default
TAGS:   luniq_extradecor chance_extradecor allow_dup extra no_dump
PLACE:  Swamp, !Swamp:$
{{
  local n = 1
  hook("post_place", function()
    dgn.place_maps{tag="serial_swamp_ruin", count=n}
  end)
}}

NAME:   serial_swamp_ruin
TAGS:   luniq_serial luniq_extradecor chance_extradecor extra
PLACE:  Swamp, !Swamp:$
WEIGHT: 2
{{
  local n = 2 + crawl.random2(2)
  hook("post_place", function()
    dgn.place_maps{tag="serial_swamp_ruin", count=n}
  end)
}}

NAME:   swamp_other_decor
TAGS:   luniq_extradecor chance_extradecor allow_dup extra no_dump
PLACE:  Swamp, !Swamp:$
WEIGHT: 8
{{
  local n = 1
  hook("post_place", function()
    dgn.place_maps{tag="decor", count=n}
  end)
}}

#####
NAME: nicolae_swamp_fairy_circle
TAGS: transparent extra decor
SUBST: W = .W, w = wwwW, q = w
MONS: fungus / nothing w:7
MAP
    ...
  ..111..
 .11WWW11.
 .1WWwWW1.
.1WWwwwWW1.
.1WwwqwwW1.
.1WWwwwWW1.
 .1WWwWW1.
 .11WWW11.
  ..111..
    ...
ENDMAP

NAME: nicolae_swamp_treefoil
TAGS: transparent extra decor
SUBST: T = t.
KFEAT: . = . w:30 / W
MAP
tt       @     tt
 ttt....T.T...Tttt
  TtttT.TT..TtttT
  ..TttT...tttT.
   T..ttttttT..
    T..TTtT..T.
     @..TtT.T..
      T.TtT.TT.@
      ..TtT.TT
       .TtT.
        Tt.
        ttT
         t
ENDMAP

NAME: nicolae_swamp_feature_on_the_river_bank
TAGS: transparent no_pool_fixup decor
SUBST: T = t., W = .Ww t:5, Q = tw, f = .GTV U:5 C:5
MAP
     tttQ..@..Qttt
    ttQQQ.....QQQtt
tttttQQwW.....WwQQttttt
  ttQQwwW.....WwwQQtt
  ttQwwwW..f..WwwwQtt
   ttwwwW.....Wwwwtt
   @TwwwWW...WWwwwT@
   ..WwwwWWWWWwwwW..
   T..WwwwwwwwwwW..T
   tT..WwwwwwwwW..Tt
   ttT..WWWwWWW..Ttt
    ttT...WWW...Ttt
     ttT.......Ttt
      ttTTTTTTTtt
ENDMAP

NAME: nicolae_swamp_stair_fork
TAGS: extra decor
DEPTH: Swamp:2-, !Swamp:$
SHUFFLE: {}[]()
SUBST: W = w., T = t.
MAP
tttttt
t...TTtt
t.{...TTtt
t.......TTtt
tT..WWW...Ttt
tT..WwwW...Ttt
 tT.WwwwWW..Tt
 tT..WwwwwW..@
  tT..WwwwwW.
  tT..Wwwwww
   tT..Wwwww
   ttT..Www
    ttT..
     tt@
ENDMAP

NAME: nicolae_swamp_round_clearing
TAGS: transparent luniq_swclearing extra decor
SUBST: T = ttt.., E = .e, e = W.., t = ttttt.
MAP
    eeeee
   eEEEEEee
  eE.....EEe
 eE........Ee
eE..........Ee
eE...TTT.....Ee
eE...TtTT....Ee
eE...TTtT....Ee
eE....TTT...Ee
 eE........Ee
 eE........Ee
 eE.......Ee
  eE.....Ee
   eEEEEEe
    eeeee
ENDMAP

NAME: nicolae_swamp_rocky_clearing
TAGS: transparent luniq_swclearing extra decor
WEIGHT: 3
SUBST: X = xxxtt.., E = .e, e = W..
MAP
       eeeee
  eeeeeEEEEEeeeee
 eEEEEE....XEEEEEe
 eE.......XXX....Ee
eE...XXX...X......Ee
eE..XxxxX.....XX...Ee
eE..XxxxX....XxxX..Ee
eE...XXX......XX...Ee
 eE..............EEe
  eEEE...XX..EEEEee
   eeeEEEEEEEeeee
      eeeeeee
ENDMAP

NAME: nicolae_swamp_trees_clearing
TAGS: transparent luniq_swclearing extra decor
SUBST: w = Te, T = ttt.., E = .e, e = W..
MAP
      tttweeee
    wtttTwEEEEeee
  eeTtttT.....EEEe
eeEE.TttT........Ee
eE....TtT....T...Ee
eE....TtT...TtT...Ee
eE.....T....TtT...Ee
 eE........TttTT.Ee
  eE........TtttTEe
  eeEEE.....Ttttww
    eeeEEEEEwtttt
       eeeeewttt
ENDMAP

NAME:    hangedman_swamp_blocks
TAGS:    transparent extra no_pool_fixup decor
SHUFFLE: BC, DEF
SUBST:   A : ttw, B : tttww, D : w., E : w..., C = t, F = .
KMASK:   wW = no_monster_gen
MAP
  ...   ..    ..
 .WWW....WW....w.
.Wtttt..ttt.D.tAw
.WttttW.ttt.D.tt.
.WttttW.tBt......
 .ttttW.....ttt..
 ..WWW..FE..CttW.
 ......FE...tttW.
.Wttt.FE.WWW....
.WttB.E.Wtttt..
 .ttt...WttttW.
 .......WttttW.
 .DD.tCt.ttttW.
 ....ttt..WWW..
..tt.ttt......
.WAt..WW.
 .W.....
ENDMAP

NAME:    hangedman_swamp_diagram
TAGS:    transparent extra no_pool_fixup decor
MONS:    bush
SHUFFLE: AB, CD, EF, GH, IJ / IJ / JI
SUBST:   A : tww.., C : tww.., E : t., G = t.
SUBST:   I = wt1GT., BDF = t, H = .
KMASK:   wW = no_monster_gen
MAP
   tt.tt@tt@tt.tt
   tt.tt.tt.tt.tt
   tttWWttttWWttt
ttttttWWttttWWttt
tttttttA.tt.Btttt
..ttttttCWWDttttt
ttWWtttttWWtttttt
ttWWAttG.ttEtt
@.tt.Ct.I.Fttt
tttttWWt.JWttt
tttttWWtFWWttt
@.tt.DtEtttHtt
ttWWBttttttttt
ttWWtttttttttt
..ttttt
ttttttt
ttttttt
ENDMAP

NAME:   wheals_swamp_sinkhole
TAGS:   extra no_monster_gen decor
DEPTH:  Swamp:1-2
WEIGHT: 5
KFEAT: ^ = shaft trap
SUBST: u = Ww
KPROP: wW^ = no_tele_into
MAP
ttttttttt
twwwwwwwt
twuuuuuwt
twuWWWuwt
twuW^Wuwt
twuWWWuwt
twuuuuuwt
@wwwwwwwt
ttttttttt
ENDMAP

NAME: nicolae_swamp_triple_paths
TAGS: transparent extra decor
SUBST: T = t_
CLEAR: _
MAP
         Tt@tT
         Tt.tT
     @TTTTt.tTTTT@
    T..tttt.tttt..T
    TT....t.t....TT
     Tttt.t.t.tttT
    TTttt.t.t.tttTT
TTTTTtttt.t.t.ttttTTTTT
ttttttttt.t.t.ttttttttt
TTTTTtttt.t.t.ttttTTTTT
    TTttt.t.t.tttTT
     Tttt.t.t.tttT
    TT....t.t....TT
    T..tttt.tttt..T
     @TTTtt.ttTTT@
        TTt.tTT
         Tt@tT
ENDMAP

NAME: nicolae_swamp_godswood
TAGS: transparent extra decor
MAP
ttt     ttt
..ttttttt..
@...ttt...@
.tt..t..tt.
tttt...tttt
 tt..t..tt
 t.......t
 tt..w..tt
  t.www.t
 tt..w..tt
 t...C...t
 tt..t..tt
  tt...tt
   ttttt
ENDMAP

NAME:    hangedman_swamp_cone
TAGS:    transparent extra no_pool_fixup decor
SHUFFLE: ABCDEF
SUBST:   AB : w, C : ww., D : w.., EF = ., G : ttw., H : wtt``, I : wtt``
CLEAR:   `
KMASK:   wW = no_monster_gen
MAP
t@......
t.ttttG.H
t.A.........
t....BttttG.H
t.tttttC.........
t.A.......DttttG.H
t....BttttttE.........
t.tttttC.......FttttG@
t.A.......Dtttttt.....
t....Btttttt......I
t.ttttt......I
t@......I
ENDMAP

NAME:    hangedman_swamp_lotus
TAGS:    transparent extra no_pool_fixup decor
MONS:    plant
SHUFFLE: ABCDE
SUBST:   AB : w, C : wwW1, D : wWW1, E : W1
KMASK:   wW = no_monster_gen
MAP
        ... ...
     .............
    ..ttttAAAtttt..
  .......ttttt.......
 ..ttttB.......Btttt..
@.....tttt.t.tttt.....@
.tttC....t...t....Cttt.
 ..ttttD.......Dtttt..
   ...tttttEttttt...
      ....ttt....
         .ttt.
         .ttt.
         .ttt.
          .t.
          .t.
           @
ENDMAP

NAME:    hangedman_swamp_pad
TAGS:    transparent extra no_pool_fixup decor
KMONS:   P = plant
KFEAT:   P = shallow_water
SHUFFLE: AB / AB / BA, CD / CD / DC, EF / EF / FE
SUBST:   A = t, B = ., C : Pt~.., D = ., E = t, F = P
KMASK:   ~ = no_monster_gen
SUBST:   ~ = w
MAP
   .www.www.
  .w...w...w.
 .w.B.t.t.B.w.
.w.A.ttttt.A.w.
w.B.t..C..t.B.w
w..t...D...t..w
w.tt..EFE..tt.w
.w.tCDFtFDCt.w.
w.tt..EFE..tt.w
w..t...D.wwt..w
w.B.t..C.wPw..w
.w.A.tttttwPw.w
 .w.B.t.t..wPw.
  .w...w....w.
   .www.wwww.
ENDMAP

#####
# The ruins of a town... all that is left are a few buildings
# and skeletons. These vaults are just to give swamp some variety...
# There are no monsters or loot placed here.
NAME:   evilmike_swamp_ruin_house_1
NSUBST: x = 1:+ / *:x
SUBST:  yx = xxxxx-, - = ----.
: swamp_ruins(_G)
MAP
  ........
 ..........
..yxxyxxxy..
..x--y---x..
..x------x..
..yy-y---x..
..x--yy-yy..
..x--y---x..
..x--y---x..
..yxxyxxxy..
 ..........
  ........
ENDMAP

NAME:   evilmike_swamp_ruin_house_2
SUBST:  x = xxxxx-, - = ----.
: swamp_ruins(_G)
MAP
 ..
 .xxx
 .x----
..x-----
..x---
..x--- ---x
 .x-------x
 .xxxxxxxxx.
 ...........
     ...
ENDMAP

NAME:   evilmike_swamp_ruin_house_3
NSUBST: x = 1:+ / *:x
SUBST:  y = x, x = xxxxx-, - = ----.
: swamp_ruins(_G)
MAP
    .....
   .......
  ..yxxxy..
 ..yy---yy..
..yy-----yy..
..x---y---x..
..x--yyy--x..
..x---y---x..
..yy-----yy..
 ..yy---yy..
  ..yxxxy..
   .......
    .....
ENDMAP

NAME:   evilmike_swamp_ruin_house_4
SUBST:  x = xxxxx-, - = ----.
: swamp_ruins(_G)
MAP
  .......
 ..xxxxx..
..xx---xx..
.xx-----x
.x--   --
.x---
.xx----
..xx--
 ..xx
  ...
ENDMAP

NAME:   evilmike_swamp_ruin_house_5
NSUBST: x = 1:+ / *:x
SUBST:  yx = xxxxx-, - = ----.
: swamp_ruins(_G)
MAP
  ............
 ..............
..yxxxxxyxxxxy..
..x-----y----x..
..x-----y----x..
..x-----y----x..
..yyyy-yy---
..x---y----
..x---yxxxxx
..x---x...
..x---x...
..yxxxy..
 .......
  .....
ENDMAP

NAME:   evilmike_swamp_ruin_house_6
SUBST:  x = xxxxx-, - = ----.
: swamp_ruins(_G)
MAP
.....
.xxxxxxx x
.x-----
.x----   -     x
 x----
 xxxx  x      -x
  ...        --x
          x----x.
         .x----x.
         .x----x.
         .xxxxxx
         ...
ENDMAP

NAME:   evilmike_swamp_ruin_rubble_1
SUBST:  x = xxxx-, - = -----.
: swamp_ruins(_G)
MAP
   .......
  ......x..
 ..xx.......
...xx...xxx..
........x--..
...xx...xxx..
.............
.x-x..xx.....
.x-x..xx...x.
.xxx.........
 .....xxxx..
  ....xxxx.
   .......
ENDMAP

NAME:   evilmike_swamp_ruin_rubble_2
SUBST:  x = xxxx-, - = -----.
: swamp_ruins(_G)
MAP
   .........
  ..xx..xxx..
 ...xx..x--...
..x.....xxx....
.....x......
.........
.xxx...
.--x..
.x-x...
.xxx.....
.....xx.....
..x..xx..xxx...
 ....xx..--x..
  .......xxx.
   .........
ENDMAP

NAME:   evilmike_swamp_ruin_wall
SUBST:  x = xxxx-, - = -----.
: swamp_ruins(_G)
WEIGHT: 4
MAP
        x
       xx
       xx
      .xx.
     .-xx-.
    .--xx--.
   .---xx---.
xxxxxxxxxxxxxxx
 xxxxxxxxxxxxxxx
   .---xx---.
    .--xx--.
     .-xx-.
      .xx.
       xx
       xx
       x
ENDMAP

NAME:   hangedman_swamp_ruin_wall
SUBST:  x = xxxx-, - = -----.
: swamp_ruins(_G)
WEIGHT: 4
MAP
  x
.-xx-....
.-xx-----.
 -xxxxxx-.
.-xxxxxx-.
.-----xx-....
 ....-xx-----.
    .-xxxxxx-.
    .-xxxxxx-
    .-----xx-.
     ....-xx-.
           x
ENDMAP

NAME:    hangedman_swamp_ruin_hall
SHUFFLE: yYzZ / yYzZ / zZyY
NSUBST:  - = 2:d- / *:-, z = 1:+ / *:X, Z = 1:. / *:X
SUBST:   XyY = xxxx-, - = -----.
: swamp_ruins(_G)
WEIGHT:  2
MAP
  ....  ....
 ..XZZZZZZX..
 ..y------Y..
  .y-x--x-Y.
 ..y------Y..
 ..XzzzzzzX..
  ..........
  XXXX--XXXX
.XxxxX--XxxxX.
.XxxxX--XxxxX.
.XxxxX--XxxxX.
  XXXX--XXXX
  ..........
ENDMAP

NAME:   nicolae_swamp_bean_lake
TAGS:   transparent decor
NSUBST: Q = 2=@ / _
SUBST:  D = tt., L : w., R : w., E = w., . = ...W
CLEAR:  _
MAP
Q..  ....Q
..........
.....EwwE...
 ..EwwwwwwE..Q
 ..wwwwwwwwR..
..EwwwwwwwwR..
..wwwwwwwwE..
..wwwwwwE...Dt
..EwwwwE...Dttt
Q..wwww....DDtt
  .EwwE.....Dt
  ..LL..DD...
   ....DtDD.Q
   Q.. tttt
        tt
ENDMAP

NAME:  nicolae_swamp_big_swirl
TAGS:  decor
SUBST: M = ..wt, D = t..
MAP
   tttttt   .@.ttt
 tttDDDDtttD...DDttt
 tDD....DDtD.....DDt
ttD......MwM......Dtt
tD...DD.MwwwM.DD...Dt
tD..DDDDMwwwMDDDD..Dt
tD..DDDDMwwwMDDDD..Dt
tD...DD.MwwwM.DD...Dt
ttD......MwM......Dtt
 tDD.....DtDD....DDt
 tttDD...DtttDDDDttt
   ttt.@.   tttttt
ENDMAP

NAME:   nicolae_swamp_oxbows
TAGS:   decor
NSUBST: D = w / ', E = w / ', F = w / ', H = w / ', J = w / ', K = w / '
NSUBST: L = w / ', M = w / ', N = w / ', O = w / ', P = w / ', Q = w / '
NSUBST: R = w / ', S = w / ', Z = w / ', a = w / ', p = w / ', q = w / '
NSUBST: r = w / ', s = w / ', y = w / ', z = w / ', - = w / ', " = w / '
NSUBST: ` = w / '
SUBST:  ' = W.
MAP
 ''  .@.   '''''
'ww'.....''awwwp''
'www'...'Zwwa'pwwq'
 'wwD'.'SwZ''.''qwr
  'Dw'.'wS'.....'rw
   'w'.'w'.......'w
  'Ew'.'wR'.....'sw
 'FwE'.'RwQ'...'-ws
 HwF'...'QwP'.'"w-'
 wH'.....'Pw'.'w"'
 w'.......'w'.'w'
 wJ'.....'Ow'.'w`'
 JwK''.''NwO'.'`ww'
 'KwwL'MwwN'...'www'
  ''LwwwM''.....'ww'
    '''''   .@.  ''
ENDMAP

NAME:    nicolae_swamp_small_spring
TAGS:    transparent decor
SHUFFLE: DEFHJKLMNOPRS / QQQ___QQ_____/ QQQ___Q___QQQ / __QQQQQ______ \
         / _____QQQ_____ / _____QQ___QQQ / __QQQ_Q_QQQ__ / ______QQQQQ__ \
         / ________QQQQQ / QQQQ___Q_____ / _QQQQQ_______ / _______QQQQQ_ \
         / _____Q___QQQQ / _QQQ_QQQ_QQQ_
SUBST:   DEFHJQ = w, KLMNOPRS = _, a = Ww, ' = .W
CLEAR:   _
MAP
    .'''''......
tttt.     ..wwa..
txxx.DEFHJ.wwwwa.
txwwwwKLMwwwwwwa.
txxx.NOPRS.wwwwa.
tttt.     ..wwa..
    .'''''......
ENDMAP

NAME:    nicolae_swamp_some_circles
TAGS:    transparent decor
# Shuffle guide:
# PQR - sides (Q) guaranteed traversible, center (P)/corners (R) whatever
# DER - sides/center guaranteed blocked, corners whatever
# PFH - corners guaranteed traversible, sides/center whatever
SHUFFLE: PQR/DER/PFH
SUBST:   P : twWpM., Q : WpM., D : tE, E : wt, R : twWM, F : twWpM., H : WM
KMONS:   pM = plant
KFEAT:   M = W
MAP
............
..RR....RR..
.RRRRQQRRRR.
.RRRRQQRRRR.
..RRPPPPRR..
..QQPPPPQQ..
..QQPPPPQQ..
..RRPPPPRR..
.RRRRQQRRRR.
.RRRRQQRRRR.
..RR....RR..
............
ENDMAP

NAME:    nicolae_swamp_tree_designs
TAGS:    transparent decor
SHUFFLE: JKLMNO / ------ / -----q / ----q- / ----qq / ---q-- / ---q-q / \
         ---qqq / --q--- / --q--q / --q-q- / --q-qq / --qq-- / --qq-q / \
         --qqqq / -q---- / -q---q / -q--q- / -q--qq / -q-q-- / -q-q-q / \
         -q-qqq / -qq--- / -qq--q / -qq-q- / -qq-qq / -qqqqq / q----- / \
         q----q / q---qq / q--q-- / q--q-q / q--qqq / q-q--- / q-q--q / \
         q-q-q- / q-q-qq / q-qq-- / q-qq-q / q-qqqq / qq---- / qq---q / \
         qq--q- / qq--qq / qq-q-- / qq-q-q / qq-qqq
SUBST:   JKLMNOq = t, - = ., D = t_, p : t., P : t.
CLEAR:   _
MAP
   DDDDDDDDD
 DDtttttttttDD
Dttt.......tttD
tt...JKLKJ...tt
...p.KMNMK.p...
@..P.LNONL.P..@
...p.KMNMK.p...
tt...JKLKJ...tt
Dttt.......tttD
 DDtttttttttDD
   DDDDDDDDD
ENDMAP

NAME:   nicolae_swamp_tree_isthmus
TAGS:   transparent decor
ORIENT: float
SUBST:  E = .Wtw
MAP
       wwwwwww
     wwwwwwwwwww
   wwwwEEEEEEwwwww
ttwwwEEE....EEwwwwwwwtt
.ttwEE..EEEE.Ewwwwwwtt.
..ttE.EEEwwE.EwwwEEtt..
@....EEwwwE.EwwwEE....@
..ttEEwwwE.EwwEEE.Ett..
.ttwwwwwwE.EEEE..EEwtt.
ttwwwwwwwEE....EEEwwwtt
     wwwwwEEEEEEwwww
       wwwwwwwwwww
         wwwwwww
ENDMAP

NAME:   nicolae_swamp_well_well_well
TAGS:   transparent decor
NSUBST: x = . / xxxx.
SUBST:  p = p..., x : xxxxt, . = ...W
KMONS:  p = plant
KFEAT:  p = . w:30 / W
MAP
 ........
..pppppp..
.ppxxxxpp.
.pxxwwxxp.
.pxwwwwxp.
.pxwwwwxp.
.pxxwwxxp.
.ppxxxxpp.
..pppppp..
 ........
ENDMAP

NAME:   nicolae_swamp_decorative_isthmus
TAGS:   no_pool_fixup transparent decor
ORIENT: float
SUBST:  _ = t_, ' = w:50 ., D = t:50 w, Q = W:50 w
CLEAR:  _
MAP
________________
tttttttttttttttt
tDD''QtDw..'Dttt
t'ww.wtQQWwww'Qt
wQQWwwwW.WwwwwW@
wW.WQQWQQQWQQWQ.
WQQQW.WwwQ...WQQ
.QwQ..QwwQQQQQww
@QwQQWQWwwwwwwDw
QQDt'wWt...'tDtt
tttttttttttttttt
________________
ENDMAP

# Just a simple gag vault: A few people got their boots stuck in the mud here
# and just left them behind. Sometimes generates bardings. I guess those get
# stuck in the mud too.
NAME:    nicolae_swamp_stuck_in_the_mud
TAGS:    transparent
SHUFFLE: Ss, Rr
SUBST:   D = W., E = t..., SR = @, sr = t, M = tttW
NSUBST:  W = 3=p / 3=pWW
KITEM:   p = pair of boots
KFEAT:   p = W
MAP
tttt    tttt
S.EttttttE.R
S..EtMMtE..R
tE.DDWWDD.Et
ttEDDWWDDEtt
tE.DDWWDD.Et
s..EtMMtE..r
s.EttttttE.r
tttt    tttt
ENDMAP

NAME:  nicolae_swamp_little_swirl
TAGS:  decor
SUBST: M = ..wt, D = t..
MAP
  ttttt
 ttDDDttD.@.Dt
ttD...MwM...Dtt
tD...MwwwM...Dt
tD...MwwwM...Dt
tD...MwwwM...Dt
ttD...MwM...Dtt
 tD.@.DttDDDtt
        ttttt
ENDMAP

NAME:    nicolae_swamp_long_stream
TAGS:    decor
ORIENT:  float
SHUFFLE: Pp/Qq/Rr
SUBST:   p = w, P = w_, QqRr = _
CLEAR:   _
MAP
tttt
t...@        RrrrrrR
t.wwww  RrrrrrrRRRrrrrR       ttt
t.wwwwRrrrR qqqqQ   RrrrrrR  ...t
t..wwwwwqqqQq Qqqqq   qqqrrRwww..t
tt... PppPqqq    QqqQQqQqqwwwwww.t
 ttt   PpppP  PppppqqqqppppPwww..t
         PppppppPPppppppP    @..tt
           PPPP    PPPP       ttt
ENDMAP

# http://i.imgur.com/H7nvQFo.jpg
NAME:   nicolae_swamp_little_tower
TAGS:   decor no_pool_fixup
KMONS:  P = plant
NSUBST: X = - / x
SUBST:  T = t-, - = ....P, z = WWw, _ = __w
CLEAR: _
MAP
   ttt___
  ttTwwww___
  tTWwwwwwww___
  tTWwwwwwwwwww___
 tt.Wwwwzzzzzwwwzz
 t.Tzwwzx---xzwwz.
 .tzwwzx-xXx-xzzz.
.Ttwwwz-xx-xx-zz-.
@Ttwwwz-X---X-WW-@
.Ttwwwz-xx-xx-zz-.
 .tzwwzx-xXx-xzzz.
 t.Tzwwzx---xzwwz.
 tt.Wwwwzzzzzwwwzz
  tTWwwwwwwwwww___
  tTWwwwwwww___
  ttTwwww___
   ttt___
ENDMAP

NAME:  nicolae_swamp_dense_foliage
TAGS:  decor transparent
SUBST: _ = ._, - = t_, t = tttttt., . = ......W
CLEAR: _
MAP
         -_-
      -_-.t.---
   -_-.t.ttt.t.-
  -.t.ttt.t.t.t.-
 -.ttt.t.t.t.ttt_
 -t.t.t.t.ttt.t.-
 -.t.t.ttt.t.t.t.-
-.t.ttt.t.t.t.ttt_
_ttt.t.t.t.ttt.t.-
-.t.t.t.ttt.t.t.t.-
 -.t.ttt.t.t.t.ttt_
 _ttt.t.t.t.ttt.t.-
 -.t.t.t.ttt.t.t.-
  -.t.ttt.t.t.t.t-
  _ttt.t.t.t.ttt.-
  -.t.t.t.ttt.t.-
   -.t.ttt.t.-_-
    ---.t.-_-
       -_-
ENDMAP

NAME:  nicolae_swamp_very_dense_foliage
TAGS:  decor transparent
SUBST: _ = ._, - = t_, t = tttttt., . = ......W
CLEAR: _
MAP
       -_-
      tt.----_
   --tttt.--.-_-
   -.ttttt..tt.-----
   --.tt..ttttt.--.-
   --.t.tt.ttttt..tt
  --.t.tttt.tt..ttttt
  _.tt.tttt.t.tt.tttt-
 _-tttt.tt.t.tttt.tt._
 -.ttttt..tt.tttt.t.--
 --.tt..ttttt.tt.t.--
 --.t.tt.ttttt..tt.--
--.t.tttt.tt..ttttt.-
_.tt.tttt.t.tt.ttttt_
-tttt.tt.t.tttt.tt._
 ttttt..tt.tttt.t.--
  tt..ttttt.tt.t.--
  -.--.ttttt..tt.--
  -----.tt..ttttt.-
      -_-.--.ttttt-
        _----.tt
            -_-
ENDMAP

###############################################################################
#
# <<4>> Swamp ending vaults
#
# They come in two flavours: normal and themed. The first three are normal and
# use regular swamp monsters. The other five have special themes, and are less
# common.
#
###############################################################################


default-depth:

###############################################################################
# Swamp:4 standard
#
NAME:    swamp
TAGS:    transparent no_monster_gen
PLACE:   Swamp:$
WEIGHT:  20
ORIENT:  float
TAGS:    no_rotate
MONS:    swamp dragon, swamp drake, hydra
MONS:    thorn hunter / shambling mangrove / spriggan druid
KMONS:   O = fire dragon w:1 / ice dragon w:1 / hydra w:18
KMONS:   & = the lernaean hydra
KITEM:   O& = decaying rune of Zot
SHUFFLE: 12
SUBST:   - = WW.
NSUBST:  13 = 5:., 1 = 5:1 / 2:. / * = 1..
NSUBST:  2 = 5:2 / 2:. / * = 2.., 3 = 7:3 / 3:. / * = 3..
: if crawl.one_chance_in(11) then
SUBST:   4 = .:999 4:1, O = &
: end
MAP
      -t-t-
     2-2-2-2
   ---cc.cc---
   -tcc3.2cct-
  --cc3.2.3cc---
 --tc11.3322ctt-
 -tcc...21.1ccc--
--tc413321.14cct-
-tcc31333.113cct-
-tcc22113.223cc--
--tcc3333113cct-
 -tcccc3O3ccct--
 --tcccccccct--
  --ttccccct--
   ---ttcct--
     ---t---
       ---
ENDMAP

###############################################################################
# Swamp:4 alternative (dp)
#
NAME:    swamp_alternative
TAGS:    transparent no_monster_gen no_pool_fixup no_rotate
PLACE:   Swamp:$
WEIGHT:  20
ORIENT:  float
MONS:    swamp dragon, swamp drake, hydra, lindwurm, rime drake, bog body
MONS:    thorn hunter / shambling mangrove / spriggan druid
KMONS:   O = fire dragon w:2 / hydra w:18
KMONS:   P = ice dragon w:2 / hydra w:18
KMONS:   & = the lernaean hydra
KITEM:   OP& = decaying rune of Zot
SHUFFLE: Od, 123,  lLO4 / wWP5, 56
SUBST:   1 = 111., 2 = 2., 3 = 333.
SUBST:   l = l `:2, L~ = ., ' = .t, " = tc
SUBST:   - = WW.
KPROP:   ` = no_tele_into
KMASK:   ` = opaque
: if crawl.one_chance_in(11) then
SUBST:   6 = .:999 6:1, OP = &
: end
MAP
    1- -1---
   2.3.2''3-2--
   -."c..1-"""---
  --""c".ccccc""-..'
 --"cc_.2ll4ccc""2-
--"cc..33lL..27"cc'.-
-"cc1122lLl...1.2""---
-"c7...llllll21..1.-3-
-"cc33l4llllll2.cc1-'3
--"ccll.llll34lcc"----
 --"cccO.lld3ccc"--
  --""cccccccc""--
   --""""""""""--
    ---""-"""---
      -----"--
          ---
ENDMAP

###############################################################################
# "Old school" swamp end, by Jude
#
NAME:    swamp_old_school
TAGS:    no_pool_fixup
PLACE:   Swamp:$
WEIGHT:  20
ORIENT:  southeast
MONS:    swamp dragon, hydra, swamp drake
MONS:    the lernaean hydra
SHUFFLE: ab, 12
SUBST:   a = c, b = ', " = ttw, ` = tt`
SUBST:   u = wwwwwWWWWxxx., ' = W....
NSUBST:  1 = 2:. / 2:1 / * = 1111.
NSUBST:  2 = 2:. / 2:2 / * = 2222.
SUBST:   3 = 3333.
KITEM:   O = decaying rune of Zot
CLEAR:   `
MAP
               t
@@''x   "t     t`   x        t
@'''   "ttt`  tt`    `t`    tt   ttt
'x'"  "ttttt`tttt`  tttt`  tttt ttttt
''''""tttttttttttttttttttttttttttttttt
'''""ttt3wtcccccttcccttccttttttccctttt
'''"tt.wcccccwwccccwcccccccttcccOccctt
'''""'ccccuuwuuwuuwuwuuwccccccwu1ucctt
'3'''1''cuu3uuuuuu2uuu3uuwcccwu2uwcctt
''''''''''''''''''''''''uuwwccw3cccctt
''""'2'cc'uuuuu'uu'uuuu'u1uuwccabccttt
'"tt.ccccccuuuu'uuuuuuu'uuuwwccabctttt
 W"tt.wcccccuuu'uuuuuuu'uuu2ccabcctttt
  W"tt.wccccwuu'u1uuuuu'uuwccccabccttt
  W.""ttccwwuuu'uuuuu1u'u2uccccc'ccctt
   .xw"tccwuuuu'uuuuuuu'uucccc2u'ccctt
x  .ww"ttccwu2u'u1uuu2u'u2uccuu'uucctt
  WxW"ttccwuuuu'uuuuuuu'uuuuuu'u1ccttt
  ww"tttcwuu''''''''''''''''''uucctttt
   ww""tccwuuuu2uuuuuuu'uuuuu2uuuccttt
 3wWwwW"tccccuuuuu''''''uuuuuuuucctttt
   xwwWxttcccuuuuuuuuuu''''''u2ucctttt
   Wwww"ttccwuuu3uuuu3u''uuuu21ucctttt
  @wWW"ttccwu1wuuccuuu2'''ccuccucctttt
   ""tttccwuuwccccccc1cccw4ccccccttttt
  `ttttttccwwcccttttcccccccctttttttttt
 ``tttttttcccctttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttt
ENDMAP

###############################################################################
# Swamp:4 alternative (due)
NAME:    swamp_fiery
TAGS:    transparent no_pool_fixup no_rotate
PLACE:   Swamp:$
ORIENT:  float
MONS:    fire dragon, lindwurm / fire crab w:8, swamp dragon / hydra w:12
MONS:    swamp drake / steam dragon w:4
SUBST:   L = ll., ' = t., " = t
MARKER:  R = lua:fog_machine { cloud_type = "flame", pow_min = 8, \
             pow_max = 12, delay_min = 40, delay_max = 70, \
             size = 2, walk_dist = 1, spread_rate= 3, excl_rad = 0 }
KPROP:   lR = no_cloud_gen
KFEAT:   XR = lava
KITEM:   O = decaying rune of Zot
NSUBST:  O = 1:O / *:l
SUBST:   - = WW.
MAP
     --'--
   ------4-     ...3
  --tcct-'- - -..ll.    4' -
 --tccc4c.---4-.lRl.-4-------
 -tccc3c.t-ttt-.ll..-tcc--t--
--tc.L.3cctcccc3...ttccL.ct-
-tccL4ll.ccc12cccccccc3Lc.t--
--tcc.LLl.43Ll344cc.c.L4.cct-
-tcccc4llLLlllLLl..L3Lll.Lct-
--tcc.LllllclllllLlllllL.cct-
 --tcc3LllRccllllRll.443cct--
  -tccL.Lllllccclll3lcccct--
  --tccc3L2lcctccO12ccttt--
   --ttcc1Occt-tccccct----
    ---tcccct---tttt---
      --tttt-- -------
       ------
ENDMAP

###############################################################################
# The way to icy death! (due)
#
NAME:    swamp_icy
TAGS:    no_pool_fixup no_rotate transparent
PLACE:   Swamp:$
ORIENT:  southeast
MONS:    ice dragon, hydra simulacrum, freezing wraith, azure jelly
KMONS:   ? = swamp worm simulacrum / swamp drake simulacrum
KFEAT:   Q? = deep_water
NSUBST:  O = 1:O / *:Y
SUBST:   X = tt., Y = Wwwww., 4 = 1:15 4
KITEM:   O = decaying rune of Zot
NSUBST:  2 = 4 = 2. / * = 2, 0 = 1:412 / 3:2 / 2:. / * = 2., 3 = 1:. / *:3
MARKER:  Q = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 8, pow_max = 12, delay_min = 30, delay_max = 40, \
             size = 2, walk_dist = 1, excl_rad = 0 }
MAP
@@             wwttttttttt
@.  ..        wwttcccccctt
@ ..  ..  .....wwcc.c3cctt
    ..  .....w?.wcXc3.Xctt
      ..    ?ww......cXctt
           www.wwXc3c3Xctt
            w?.wXcXc.3cctt
             ww.wccc..cctt
              ..ttcc..cctt
             ..wwtcc2.cctt
            ..wwttc..ccctt
           .twwwttcc0Xcctt
          .tttwwwtcc.0.ctt
         .tttttwwtc..ccctt
         .ttttttttcc0.cctt
          .wwtttttccX0.ctt
          ..?wttttccc0.cct
     @@  2.wwwwttttcc.0cct
    ?@. ..ww?wttttccc.0cct
   ttww.wwwwwtttttcc..cctt
   ttw?ww?wwtttcccc.0ccttt
  ttttwwwwtttcccccc0Xctttt
  tttttttttcccwwwY0.Yccttt
  tttttttcccwwQww.YY.wcctt
   tttttccwwwww22Yww2wwctt
     ttccwwww22YwwwwY2wctt
    ttccwww.2YwwQwwwY2wctt
   ttccww1.YwwwwwwwY2wwctt
  ttcccwOYwWWcccWY1.wwcctt
 ttcccccwwwwcW4WwOYYcccttt
tttccccccccccccccccccttttt
tttttttttttttttttttttttttt
ENDMAP

###############################################################################
# Alternative swamp end, by Lemuel
#
NAME:    swamp_pestilence
TAGS:    no_pool_fixup
PLACE:   Swamp:$
ORIENT:  southeast
MONS:    spectral swamp dragon / spectral hydra
MONS:    hydra zombie / swamp dragon zombie
MONS:    spectral swamp drake / swamp drake zombie
MONS:    death drake / vampire mosquito band / w:5 spectral bunyip
MONS:    bloated husk, phantom, bog body
KMONS:   8 = phantasmal warrior
KMONS:   9 = ghost crab
KMONS:   ? = swamp worm zombie / alligator zombie
KMONS:   O = bone dragon
KITEM:   O = decaying rune of Zot
KITEM:   * = *
KFEAT:   ?Y = deep_water
KFEAT:   O* = shallow_water
SHUFFLE: O*
SUBST:   1 = 113, ? = ??w, " = tc, ' = .w
NSUBST:  6 = 1:5 / 3 = 89 / 10 = 5666. / *:., y = 1:Y / *:W
MARKER:  Y = lua:fog_machine { cloud_type = "foul pestilence", \
             pow_min = 3, pow_max = 4, delay_min = 35, delay_max = 45, \
             size = 1, walk_dist = 0, excl_rad = 0}
MARKER:  O = lua:fog_machine { cloud_type = "foul pestilence", \
             pow_min = 3, pow_max = 4, delay_min = 35, delay_max = 45, \
             size = 3, walk_dist = 1, excl_rad = 0}
COLOUR:  YO = green
MAP
@@
@....
   .....
    ?w7.....w?w"tt     tt
    w?ww.7ww?wwc"tt   ttt
    "wWWwww?w?cc"ttt tttt
   '""cWw?w?ccc"ttttttttt
   't"c7.wwcccc"""ttttttt
   tttc...ccccccccccc"ttt
  ttt"cc...Ycccc6666c"ttt
  ttt"ccY...cccO6666cc"tt
 ttttcccc...cc6666666c"tt
  t""ccccY.Yccc666666cctt
   "cc1*ccY.Ycc6666666ctt
   tc211ccc.Ycccc6.666c"t
   ccc2.1ccY.Ycccc..6cc"t
   ""c.1.cccY.ycccWW.cc"t
   "tcc.1.cccy..ccWW..ctt
   "t"c2.21ccc..ccc6wcctt
   tttcc..2ccc..c..wwc"tt
   ttt"cc.1WWc...6wcccttt
   tttt"c2.WW...?wcc""ttt
  ttttttccc2.wwwwcc""tttt
  ttttttt"cc?w?wcc""ttttt
  tttttttt"cc4wcc"ttttttt
 tttttttttt"cccc"tttttttt
ttttttttttttt""tttttttttt
ttttttttttttttttttttttttt
ENDMAP

###############################################################################
# HangedMan's hellswamp: an abandoned, overgrown, flooded temple,
# occupied by demons, beasts, messes of flesh, and other vile tricks.
NAME:    swamp_vile
TAGS:    no_pool_fixup no_monster_gen
PLACE:   Swamp:$
ORIENT:  southeast
MONS:    bog body / ugly thing, shadow / orange demon / small abomination w:2
MONS:    large abomination / very ugly thing / thrashing horror w:6
MONS:    green death, hell beast, great orb of eyes, titanic slime creature
KMONS:   8 = hydra / swamp dragon
KMONS:   9 = tentacled monstrosity
KMONS:   & = the Lernaean hydra
KITEM:   O = decaying rune of zot
KFEAT:   8 = shallow_water
SHUFFLE: yz, '"
SUBST:   yX = x:20 t:4 W:1, z = ..wwwWWW, ' = ....W, " = wWWW, 5 : 59
NSUBST:  7 = 1:4 / 1:8 / *:7, 0 = 6:1 / 3:. / *:2
NSUBST:  5 = 1:5 / 1:6 / * = 56 &:2, 9 = 1 = 9 &:1 / *:.
MAP
@@ "' Xxccxcxcct
@'"'XWXW""'..Wcct
  'XWXWx''"zyW.xct
   WXWx"wwwyzwxWcct
  XXWx.x1wwzyw.x.xct
 txWx"x2wwwyzwx0..cctt
 xt"'w1w28wzywx30xWcctt
 ct"'www88wyxwcxx.xWxttt
  x'"wwwww.cXWwwwwWwWcttt
  c.yzyzyzcX..zyzyzWx7xct
 ct.zyzyzxX.W.yzyzy.WxWcct
 xt"'wWWWwW..wWW.WWw.WwWcct
  x"'WxxxWwzyWwcwc.WyzWwWxct
  c'"wx0x.wyzWc...cWzywWx.cct
 xt"'wx3.xwzy.w.7.w.yzwx0..xct
 ct'"wx0x.wyzWc...cWzywc30xWcct
  x"'WxxWwWzyW.cwcWWyzwxcxWwWcct
  c"'wWWwWW..wWW.WW..wWwWWwWwWcct
 ctWzyzyzWW.w.yzyzy.W.WWzyzyzWttt
 xt.yzyzyzxw..zyzyzw..xzyzyzy.tct
  c"'wWWw.WXWWwwwwwwWXc.WW.w'"ctt
  c"'WxxWW.yWwW.x.wWWy.WWxx."'xtt
 ct'"xx.xWwzyWxx.xWwyzWWc0cx'"cxt
 xt'"x04.xWyzWx030xWzyWx055t"'tct
 ctt8xx0xxWyzWxxxxxWyz.xc5Oc'"ctt
  ct"'xxxWwzywWwwwWwzyw.xtcc8tXct
  cc'"8""''yz""'7'""yz'"'"'87tcct
  tccct''""W.''"'"''W."'"'"tttcct
 tttttccxcccttcxctcxcXctcXXxtccct
ttttttttttttttttttttttttttttttttt
ENDMAP

###############################################################################
# "Everyone said I was daft to build a castle on a swamp..."
NAME:    grunt_swamp_rune_castle
TAGS:    no_monster_gen no_trap_gen transparent
PLACE:   Swamp:$
ORIENT:  float
MONS:    swamp dragon, swamp drake, hydra, bog body
MONS:    fungus w:12 / plant w:5 / bush w:1
KMONS:   d = patrolling the lernaean hydra / hydra w:1, hydra
KMONS:   ef = patrolling hydra
KITEM:   d = decaying rune of Zot
KITEM:   ef = any good_item
KITEM:   g = stone w:40 / large rock q:1
NSUBST:  @ = 1:@ / *:.
SUBST:   C = cc., = = +.
SHUFFLE: def, 123
NSUBST:  : = 6:1 / 6:2 / 6:3 / 9:4 / 3 = 0.... / 20:5 / * = '
SUBST:   ' = .....WW, G = Gg
MAP
 cccccWWW'''''WWWccccc
cc%..ccCC=C=C=CCcc..%cc
c%1...c'::'''::'c...2%c
c..d..c'G:::::G'c..e..c
c.....c':::C::''c.....c
cc....+.G:'C':G.+....cc
Wcccc+c.::CCC:'.c+ccccW
WC'''..'G:'C':G'..'''CW
WC:G:G:G'''C'''G'G'G:CW
'=::::::'':C:''::::::='
'C'::'C'':VcV:''C'::'C.
'=''CCCCCCcccCCCCCC''=@
'C'::'C'':VcU:''C'::'C.
'=::::::'':C:''::::::='
WC:G'G'G'''C'''G'G'G:CW
WC'''..'G:'C':G''''''CW
Wcccc+c.':CCC:''C=CCCCW
cc....+.G:'C':G'=''':CC
c.....c''::C::''C'':::C
c..f..c'G:::::G'C':::'C
c%3...c'::'''::'C:::''C
cc%..ccCC=C-C=CCCC:'''C
 cccccWWW'.@.'WWWCCCCC@
ENDMAP

###############################################################################
# Another Basic swamp end, by johnstein
# about 27 basic swamp enemies & 1/11 chance of L. Hydra
NAME:    swamp_basic_johnstein
PLACE:   Swamp:$
ORIENT:  float
WEIGHT:  20
SUBST:   1 = 111''
SUBST:   % = %'
KMONS:   1 =  swamp dragon / swamp drake / hydra w:15 / bog body w:5
SUBST:   ' = .....WWW, - = ..wwwWW, T = .wWtt, C = ccct
KFEAT:   1 =  floor / floor / shallow_water
KITEM:   O = decaying rune of Zot
NSUBST:  O = 1:O / *:*
: if crawl.one_chance_in(11) then
     KMONS: O = the lernaean hydra
: else
     KMONS: O* = thorn hunter / shambling mangrove / spriggan berserker
     KITEM: * = *
: end
MAP
   tt  tt
  ttttttttt @@@   tt  tt
tttccccccttt'-'ttttttttt
ttcc%O%Ccccc'-'ccccccccctt
tccT1-1-Cccc1-1ccccC%O%cctt
tcT1'1'1'ccc'-'cccC-1-1Tcctt
tccT1-1-ccc-1T1-cc'1'1'1Tcct
ttcC'-'ccc''TTT''cc-1-1Tcctt
 tcC-1ccc1-ccccc-1cc'-'Ccctt
ttcC-1cc'-cc%O%cc-'cc1-Ccctt
ttc'-'c1-cc-1-1-cc-1c1-Ccct
ttc-1Cc'-c'1'1'1'c-'c''-cctt
 tc-''c1-cc-1-1-cc-1cC1-cct
ttcC1'1'''cC'-'Cc-''c''-cttt
ttccccC'''cC'-'Cc'''1'1cctt
 ttttccC-1'1'T'1'1-CCccctt
  ttttccC-1'TTT'1-Cccccttt
    tttcccccccccccccttttt
     ttttttttttttttttt t
       tt   ttt ttt t
ENDMAP

###############################################################################
# Growth, Death and Undeath, by byrel
# Presided over by an undead Lernaean hydra
NAME:    byrel_swamp_growth_death_and_undeath
TAGS:    no_monster_gen
PLACE:   Swamp:$
ORIENT:  float
TAGS:    no_rotate
MONS:    swamp dragon / swamp drake / hydra
MONS:    freezing wraith / flayed ghost / phantasmal warrior
MONS:    hydra skeleton / swamp dragon skeleton / death drake skeleton
MONS:    hydra zombie / swamp dragon zombie / death drake zombie
MONS:    spectral hydra / hydra simulacrum
MONS:    patrolling thorn hunter / patrolling shambling mangrove
MONS:    patrolling spriggan druid god:fedhas
KITEM:   O = decaying rune of Zot
KFEAT:   _ = altar_fedhas
KFEAT:   - = altar_yredelemnul
NSUBST:  O = 1:O / *:%
SUBST:   x = xxxxxxttT
NSUBST:  1 = 8=1 / 8=1. / .W
NSUBST:  3 = 6=5 / 8=3 / 8=3. / .
SUBST:   3 = 2344
NSUBST:  6 = 3=6 / 3=6. / .
SUBST:   . = ...W
NSUBST:  P = 3=' / ppW
KMONS:   p = plant
KMONS:   ' = oklob plant
KMONS:   q = vampire knight
KMONS:   L = patrolling the lernaean hydra zombie / patrolling the lernaean hydra skeleton
KPROP:   q = bloody
TILE:    E = WALL_UNDEAD, T = DNGN_TREE_DEAD, G = DNGN_GRAVESTONE, x = WALL_BRICK_VINES
COLOUR:  T = brown
SUBST:   T = t
SUBST:   E = c
{{
dgn.delayed_decay_extra(_G, 'q', 'hydra corpse')
}}
MAP
         xxx@@@xx
        xxxx11xxxx
        xxx1111xxxx
       xxxx11T11xxxx
      xxxx1111t11xxxx
     xxxt111t1111T1Txx
    ccct.11111T1111ccx
   cccct..ttTxLxT..ccccc
  ccccct..txxxxxxE3EEEEcc
 ccccP....TccccccE333sEcccc
cccc..t6...ccccEEEE333TEEEcc
ccc..66....cccE3T3333EE3Eccc
ccP...6P.t.PccEE333G3333Eccc
cct66.P_P66tcccE3T33q3T3Eccc
cc66P.7P7.P6cccE3333-333Eccc
ccPppPpppPppcccE33G3qG3EEccc
ccccppppppcccccEEEE333EEcccc
cccccccOcccccccccE333333Eccc
  cccccccccccccccEEOEEEEEcc
    ccccccc    cccEEEcccccc
      cc        ccccccc cc
ENDMAP

###############################################################################
# Witches Coven by nikheizen/nzn
# 13 witches (or wizards, or druids, or magi, or sorcerers; they're rather
# inclusive) gather around the rune of power to enact a ritual most foul.
# Also features five tier 4 demons and two tier 2 demons.
# Averages nineteen extra monsters, of which 1/3 are tier 4 demons, 1/3 are
# random 0s, and 1/3 are random 9s
NAME:   nzn_swamp_witches_coven
PLACE:  Swamp:$
ORIENT: float
TAGS:   no_monster_gen no_rotate no_vmirror
KITEM:  O = decaying rune of zot
MONS:   fenstrider witch, green death / reaper / blizzard demon
MONS:   neqoxec / soul eater / sixfirhy, orange demon / hellwing / chaos spawn
MONS:   spriggan druid / orc sorcerer / ogre mage / deep elf pyromancer w:5 / \
        deep elf zephyrmancer w:5 / wizard
SUBST:  ' = W:55 .:15 4:5, 4 = 049
KFEAT:  B = stone_arch
KFEAT:  a = c
MARKER: B = lua:fog_machine { cloud_type = "flame", pow_min = 10, \
                              pow_max = 10, delay = 10, size = 1, \
                              walk_dist = 0, excl_rad = 0 }
TILE:   a = wall_stone_dark
COLOUR: a = magenta
TILE:   G = dngn_statue_demonic_bust / dngn_statue_imp / dngn_statue_cerebov \
            / dngn_statue_ancient_evil
MAP
         ccccccccc
       ccc''5t5''ccc
     ccct'''ttt'''tccc
    cc'''''''t'''''''cc
   cc'aa'''''t'''''aa'cc
  cc''aAaat''t''taaAa''cc
  ct'''a'aa''t''aa'a'''tc
 cc''''a'3aat'taa3'a''''cc
 ct''''aa'5aataa5'aa''''tc
@c'''''ta'''tat'''at'''''c@
c'''''''aat'''''taa'''''''c
c''t''''5aa'''''aa5'''t'''c
c'''''''aat''2''taa'''''''c
c''''taaaat''G''taaaat''''c
c''taaa''a'''t'''a''aaat''c
cttaa53'''attttta'''35aattc
ccaaaaaaa'aaaaaaa'aaaaaaaac
 ct'''''''a'''''a'''''''tc
 ct''''tttaA'''Aattt''''tc
 cct''t''''a'''a''''t''tcc
  ct5'1'''ta'1'at'''1'5tc
  cct'''''ta'2'at'''''tcc
   cct''''taaOaat''''tcc
    cct'''''aBa'''''tcc
     ccct'''aaa'''tccc
       ccc'''a'''ccc
         ccccccccc
ENDMAP
