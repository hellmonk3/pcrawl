##############################################################################
# zot.des: The ultimate goal. The Orb must be placed (Z). These maps can and
#          should be serious challenges! Players dying on Zot:5 are okay :)
#
# Content: Zot entry vaults (these are on Depths:$ and contain the portal)
#          Zot vaults, mini and not so mini
#          Zot hall (on Zot:5)
#          Vaults that define the floor/wall colours for Zot:1-5
#
##############################################################################

{{
function zot_entry_setup(e)
    e.place("Depths:$")
    e.orient("float")
end
}}

{{
-- Create colourful Sanctum glass
function coloured_glass(e)
  e.shuffle('roygbiv')
  e.kfeat('roygbiv = m')
  e.tile('r = dngn_transparent_wall_red')
  e.tile('o = dngn_transparent_wall_brown')
  e.tile('y = dngn_transparent_wall_yellow')
  e.tile('g = dngn_transparent_wall_green')
  e.tile('b = dngn_transparent_wall_blue')
  e.tile('i = dngn_transparent_wall')
  e.tile('v = dngn_transparent_wall_magenta')
  e.colour('r = red')
  e.colour('o = brown')
  e.colour('y = yellow')
  e.colour('g = green')
  e.colour('b = blue')
  e.colour('i = cyan')
  e.colour('v = magenta')
end
}}

##############################################################################
##############################################################################
#
# Sanctum entries (D side)
#
##############################################################################
##############################################################################

NAME:   zot_entry_dummy
TAGS:   zot_entry
: zot_entry_setup(_G)
MAP
...
...
...
ENDMAP

##############################################################################
##############################################################################
#
# Sanctum petite vaults
#
##############################################################################
##############################################################################

# There's few vaults on Sanctum:5, tone down their chances.
NAME: dummy_balancer_zot
DEPTH: Sanctum:$
TAGS: dummy
ORIENT: float
WEIGHT: 110
MAP
x
ENDMAP

################################################################################
# Space-filling minivaults by bh.

NAME:   bh_zot_ikea_curve
TAGS:   extra transparent
DEPTH:  Sanctum
MAP
xxxxxxxxxxxxx
@.....x.....@
@.....x.....@
xxxx..x..xxxx
x.....x.....x
x.....x.....x
x..xxxxxxx..x
x..x.....x..x
x..x.....x..x
x.....x.....x
x.....x.....x
xxxxxxxxxxxxx
ENDMAP

NAME:   bh_zot_lava_cross
TAGS:   extra
DEPTH:  Sanctum
MAP
     @@
    x..x
    x..x
    x..x
 xxxxllxxxx
@...l..l...@
@...l..l...@
 xxxxllxxxx
    x..x
    x..x
    x..x
     @@
ENDMAP

NAME:   bh_zot_levy_curve
TAGS:   extra
DEPTH:  Sanctum
SUBST:  W = Ww
MAP
          WWW.
          W.W..
          W.WWW
          ....W
         .....W
        ....WWW
       .......W
      ..www...W
     ...w.w.WWW
    ....wwwWW..
WWW.......W...
W........WW..
WWW..W..WW..
..W..W..W..
 .WWWWWWW.
ENDMAP

#########################
# Minivaults based on the old orb chambers.
#
NAME:    zot_minivault_1
TAGS:    transparent
WEIGHT:  20
DEPTH:   Sanctum, !Sanctum:$
MAP
 xxxxxxxxxxx
xxx...G...xxx
xx..G...G..xx
xG.........Gx
@..G..G..G..@
xG.........Gx
xx..G...G..xx
xxx...G...xxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_monky_inset
NAME:    zot_minivault_2
TAGS:    transparent
WEIGHT:  5
DEPTH:   Sanctum, !Sanctum:$
MAP
 xxxxx@xxxxx
xxxG.....Gxxx
xx....G....xx
xG...xxx...Gx
@...xxxxx...@
xG...xxx...Gx
xx....G....xx
xxxG.....Gxxx
 xxxxx.xxxxx
xx...G.G...xx
x...........x
x...........x
xx.........xx
 xxxxx@xxxxx
ENDMAP

# Based on orb_two_ways
NAME:    zot_minivault_3
TAGS:    transparent
WEIGHT:  5
DEPTH:   Sanctum, !Sanctum:$
MAP
 xxxxxxxxxxx
xxxx.....xxxx
xx....x...xxx
xx...xxx..xxx
xx...xx..xxxx
xxxxxx..xxxxx
xxxx.....xxxx
xxx..GxG..xxx
xG...xxx...Gx
@..GxxxxxG..@
xG...xxx...Gx
xxx..GxG..xxx
xxxx.....xxxx
xxxxx..xxxxxx
xxxx..xx...xx
xxx..xxx...xx
xxx...x....xx
xxxx.....xxxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_monky_within_orb
NAME:    zot_minivault_4
TAGS:    transparent
DEPTH:   Sanctum, !Sanctum:$
COLOUR:  n = magenta
TILE:    n = dngn_transparent_stone_magenta
MAP
 xxxxxxxxxxx
xxxG.....Gxxx
xx..nnGnn..xx
x..n.....n..x
@..n.G.G.n..@
x..n.....n..x
xx..nnGnn..xx
xxxG.....Gxxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_monky_twist
NAME:    zot_minivault_5
TAGS:    transparent
DEPTH:   Sanctum, !Sanctum:$
COLOUR:  n = magenta
TILE:    n = dngn_transparent_stone_magenta
MAP
 xxxxxxxxxxx
xxxnn.....xxx
xxnnG.G.n..xx
xnnn....nn.Gx
@.nnG.G.Gnn.@
xG.nn....nnnx
xx..n.G.Gnnxx
xxx.....nnxxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_grunt_hallway
NAME:    zot_minivault_8
TAGS:    transparent
WEIGHT:  5
DEPTH:   Sanctum, !Sanctum:$
SUBST:   > = }]}
MAP
 xxxxxxxxx
xxxx...xxxx
xxxG...Gxxx
xxx.....xxx
xxxG...Gxxx
xxxx...xxxx
xxxxG.Gxxxx
xGGx...xGGx
x..x...x..x
@..xG.Gx..@
x..x...x..x
x..x...x..x
x..xG.Gx..x
x.Gx...xG.x
x.Gx...xG.x
x..xG.Gx..x
x..x...x..x
x.........x
xx.......xx
 xxxxxxxxx
ENDMAP

####
NAME: nicolae_moth_party
TAGS: no_pool_fixup no_monster_gen
DEPTH: Sanctum, !Sanctum:$
ORIENT: north
SUBST: l : lw
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx......xxxxxxxxx......xxx
xx........xxx.xxx........xx
x...llll...x...x...llll...x
x..llllll.........llllll..x
x..llllll.........llllll..x
x...llll...........llll...x
xx.......................xx
xxx.....................xxx
xxxx...................xxxx
xxxxx.................xxxxx
xxxx...................xxxx
xxx.....................xxx
xx...lll...........lll...xx
xx..lllll..x...x..lllll..xx
xx...lll...x...x...lll...xx
xxx.......xx...xx.......xxx
xxxx.....xxx...xxx.....xxxx
xxxxxxxxxxx@@@@@xxxxxxxxxxx
ENDMAP

NAME:    hangedman_zot_wheel_spokes
TAGS:    transparent extra decor
DEPTH:   Sanctum, !Sanctum:$
# Swap positions in unit's potential spots
# with the spokes and / or the center, maybe.
SHUFFLE: AGMBHN / AHMBGN / AGNBJM
SHUFFLE: CIOFLR / CLOFIR / CIRFLO
SHUFFLE: DJPEKQ / DJQEKP / DJQEKP
SHUFFLE: AGM / BHN / CIO / DJP / EKQ / FLR
# Make those slots random walls or floor.
SUBST:   AGM : xxxxcccbbv, BHN : xxxxcccbbv
SUBST:   CIO : xxxxcccbbv .:100
SUBST:   DJP : xxxxcccbbv .:100, EFKLQR = .
MAP
      ...CDEF...
      ..F.AB.C..
........EB..AD..
........DA..BE..
.IJKL...C.BA.F..
L.GH.I...FEDC...
KH..GJ.OPQR.......
JG..HKR.MN.O......
I.HG.LQN..MP.IJKL.
.LKJI.PM..NQL.GH.I
......O.NM.RKH..GJ
.......RQPO.JG..HK
  ...CEDF...I.HG.L
  ..F.AB.C...LKJI.
  ..EB..AE........
  ..DA..BD........
  ..C.BA.F..
  ...FEDC...
ENDMAP

##########################################################################
# Some decorative multi-coloured glass columns
NAME:   skrybe_zot_glass_1
TAGS:   transparent decor no_pool_fixup
DEPTH:  Sanctum
SUBST: W : Wwl
KMASK:  Wwl = no_monster_gen
: coloured_glass(_G)
MAP
  .....    .....
 ..WWW..  ..WWW..
..WWrWW....WWyWW..
.WWrrrWW..WWyyyWW.
.WrrrrrW..WyyyyyW.
.WWrrrWW..WWyyyWW.
..WWrWW....WWyWW..
 ..WWW......WWW..
  ..............
  ..............
 ..WWW......WWW..
..WWoWW....WWgWW..
.WWoooWW..WWgggWW.
.WoooooW..WgggggW.
.WWoooWW..WWgggWW.
..WWoWW....WWgWW..
 ..WWW..  ..WWW..
  .....    .....
ENDMAP

NAME:   skrybe_zot_glass_2
TAGS:   transparent decor no_pool_fixup
DEPTH:  Sanctum
SUBST:  l : Wwl
KPROP:  l = no_cloud_gen
KMASK:  Wwl = no_monster_gen
: coloured_glass(_G)
MAP
      .....
 ......lll......
..lll.lloll.lll..
.llrlllooolllyll.
.lrrrlooooolyyyl.
.llrlllooolllyll.
..lll.lloll.lll..
 ......lll......
      .....
ENDMAP

NAME:   skrybe_zot_glass_3
TAGS:   transparent decor no_pool_fixup
DEPTH:  Sanctum
SUBST:  l : Wwl
SUBST:  ( : ([{.
KMASK:  Wwl = no_monster_gen
KPROP:  l = no_cloud_gen
: coloured_glass(_G)
MAP
xxxxxx@@@xxxxxx
xlllll...lllllx
xlrrrl...looolx
xlrrll...lloolx
xlrll.....llolx
xlll.......lllx
@.............@
@......(......@
@.............@
xlll.......lllx
xlyll.....llglx
xlyyll...llgglx
xlyyyl...lggglx
xlllll...lllllx
xxxxxx@@@xxxxxx
ENDMAP

##############################################################################
##############################################################################
#
# hall_of_Zot_5
#
##############################################################################
##############################################################################

NAME:    hall_of_Zot
PLACE:   Sanctum:$
ORIENT:  north
TAGS:    no_dump no_trap_gen
KITEM:   Z = Orb of Zot
MONS:    patrolling orb guardian, orange crystal statue / w:20 obsidian statue
MONS:    electric golem, killer klown, ancient lich / dread lich, orb of fire
# Traps to spread throughout the vault. Fewer alarm traps as those have a big
# effect on the vault as a whole.
KFEAT:   ~ = zot trap / dispersal trap / permanent teleport trap / net trap \
             / alarm trap w:5
# Flat distribution of traps for specific sections.
KFEAT:   ^ = zot trap / dispersal trap / permanent teleport trap / alarm trap \
             / net trap
KMASK:   ^~ = no_item_gen
# Place a minimum number of traps and some extra liches in each lower wing.
NSUBST:  d = 2=5. / 4=~ / ., h = 2=5. / 4=~ / .
# In a each upper wing, place a few monsters along with locations for the main
# monster set on '?'. Use more central areas in the upper wings to do this.
NSUBST:  f = 3 / 4 / 6 / 9 = ? .:8 / e, k = 3 / 4 / 6 / 9 = ? .:8 / j
# Minimum number of traps in each upper wing.
NSUBST:  e = 4=~ / ., j = 4=~ / .
# Each upper wing has equal chance to get one of three sets that most heavily
# weight a specific monster: killer klown, ancient lich, or orb of fire.
SUBST:   ? : KLM
SUBST:   K = 0 4:40 5 6 ., L = 0 4 5:40 6 ., M = 0 4 5 6:40 .
# Make a final sprinkling of traps throughout the vault.
SUBST:   . = ~ .:600
# Put traps in wing entrances with decreased odds on one side to make double
# trapped entrances rarer.
SUBST:   D = ^..., H = ^.
# For upper-wing entrances, sometimes put statue monsters, traps, or statues.
SUBST:   E = 2 ^:20 G .:80, J = 2 ^:20 G .:80
# For the orb chamber, some guaranteed and random traps. About 4 on-average.
NSUBST:  - = 2=^ / * = ^ .:240
# Colours
COLOUR:     cX = lightmagenta
LFLOORCOL:  magenta
LROCKCOL:   lightmagenta
LFLOORTILE: floor_rough_magenta
LROCKTILE:  wall_zot_lightmagenta
validate {{ return has_exit_from_glyph('Z') }}
MAP
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXeeeeeeeeeeeeeXXXXXXXXXXXXXXXXXXXXXXXXXjjjjjjjjjjjjjXXXXXXXXXXXXXX
XXXXXXXXXXXXeeeeeeeeeeeeeeeeeeeXXXXXXXXXXXXXXXXXXXjjjjjjjjjjjjjjjjjjjXXXXXXXXXXX
XXXXXXXXXXXeeeeeeeeeeeeeeeeeeeeeeXXXXXXXXXXXXXXXjjjjjjjjjjjjjjjjjjjjjjXXXXXXXXXX
XXXXXXXXXXeeeeeeeeeeeeeeeeeeeeeeeeXXXXXXXXXXXXXjjjjjjjjjjjjjjjjjjjjjjjjXXXXXXXXX
XXXXXXXXXXeeeeeeefffffffffffffeeeeXXX---1---XXXjjjjkkkkkkkkkkkkkjjjjjjjXXXXXXXXX
XXXXXXXXXXXeeeeeefffffffffffffeeeeXX--1---1--XXjjjjkkkkkkkkkkkkkjjjjjjXXXXXXXXXX
XXXXXXXXXXXXeeeeefffffffffffffeeeeX1---------1XjjjjkkkkkkkkkkkkkjjjjjXXXXXXXXXXX
XXXXXXXXXXXXXeeeefffffffffffffeeeee--1--Z--1--jjjjjkkkkkkkkkkkkkjjjjXXXXXXXXXXXX
XXXXXXXXXXXXeeeeefffffffffffffeeeeX1---------1XjjjjkkkkkkkkkkkkkjjjjjXXXXXXXXXXX
XXXXXXXXXXXeeeeeefffffffffffffeeeeXX--1---1--XXjjjjkkkkkkkkkkkkkjjjjjjXXXXXXXXXX
XXXXXXXXXXeeeeeeefffffffffffffeeeeXXX---1---XXXjjjjkkkkkkkkkkkkkjjjjjjjXXXXXXXXX
XXXXXXXXXXeeeeeeefffffffffffffeeeecXXXXXXXXXXXcjjjjkkkkkkkkkkkkkjjjjjjjXXXXXXXXX
XXXXXXXXXXXeeeeeeeeeeeeeeeeeeeeeecccccccccccccccjjjjjjjjjjjjjjjjjjjjjjXXXXXXXXXX
XXXXXXXXXXXXeeeeeeeeeeeeeeeeeeeXcccccccccccccccccXjjjjjjjjjjjjjjjjjjjXXXXXXXXXXX
XXXXXXXXXXXXXeeeeeeeeeeeeeeeXXXXcccccccccccccccccXXXXjjjjjjjjjjjjjjjXXXXXXXXXXXX
XXXXXXXXXXXXXXE111EccXXXXXXXXXXXcccccccccccccccccXXXXXXXXXXXccJ111JXXXXXXXXXXXXX
XXXXXXXXXXXccdddddddddddddddDD1.ccccccccccccccccc.1HHhhhhhhhhhhhhhhhccXXXXXXXXXX
XXXXXXXXXXccddddddddddddddddcc1...ccccccccccccc...1cchhhhhhhhhhhhhhhhccXXXXXXXXX
XXXXXXXXXccddddddddddddddddddccc...ccccccccccc...ccchhhhhhhhhhhhhhhhhhccXXXXXXXX
XXXXXXXXXcddddddddddddddddddddccc...............ccchhhhhhhhhhhhhhhhhhhhcXXXXXXXX
XXXXXXXXXcddddddddddddddddddddcXcc.............ccXchhhhhhhhhhhhhhhhhhhhcXXXXXXXX
XXXXXXXXXcddddddddddddddddddddcXXc.............cXXchhhhhhhhhhhhhhhhhhhhcXXXXXXXX
XXXXXXXXXcddddddddddddddddddddcXXcc.1...1...1.ccXXchhhhhhhhhhhhhhhhhhhhcXXXXXXXX
XXXXXXXXXcddddddddddddddddddddcXXXc...........cXXXchhhhhhhhhhhhhhhhhhhhcXXXXXXXX
XXXXXXXXXcddddddddddddddddddddcXXcc...........ccXXchhhhhhhhhhhhhhhhhhhhcXXXXXXXX
XXXXXXXXXccddddddddddddddddddccXXc.............cXXcchhhhhhhhhhhhhhhhhhccXXXXXXXX
XXXXXXXXXXccddddddddddddddddccXXcc.............ccXXcchhhhhhhhhhhhhhhhccXXXXXXXXX
XXXXXXXXXXXccddddddddddddddccXXXc...............cXXXcchhhhhhhhhhhhhhccXXXXXXXXXX
XXXXXXXXXXXXccccccccccccccccXXXXcccccccc@ccccccccXXXXccccccccccccccccXXXXXXXXXXX
ENDMAP

##############################################################################
##############################################################################
#
# For colouring the walls and floors of the first four levels
#
##############################################################################
##############################################################################

NAME:       hall_of_Zot_1
DEPTH:      Sanctum:1
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  lightgrey
LROCKCOL:   white
LFLOORTILE: floor_rough_lightgray
LROCKTILE:  wall_zot_white
MAP
.
ENDMAP

NAME:       hall_of_Zot_2
DEPTH:      Sanctum:2
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  white
LROCKCOL:   blue
LFLOORTILE: floor_rough_white
LROCKTILE:  wall_zot_blue
MAP
.
ENDMAP

NAME:       hall_of_Zot_3
DEPTH:      Sanctum:3
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  blue
LROCKCOL:   lightblue
LFLOORTILE: floor_rough_blue
LROCKTILE:  wall_zot_lightblue
MAP
.
ENDMAP

NAME:       hall_of_Zot_4
DEPTH:      Sanctum:4
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  lightblue
LROCKCOL:   magenta
LFLOORTILE: floor_rough_lightblue
LROCKTILE:  wall_zot_magenta
MAP
.
ENDMAP
